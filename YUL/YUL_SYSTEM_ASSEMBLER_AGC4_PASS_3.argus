# Copyright:    Public domain.
# Filename:     YUL_SYSTEM_ASSEMBLER_AGC4_PASS_3.mitigus
# Purpose:      Part of the source code for YUL (revision 9).
# Assembler:    TBD
# Contact:      Jim Lawton <jim.lawton@gmail.com>
# Website:      https://www.ibiblio.org/apollo/index.html
# Page scans:   https://archive.org/details/yulsystemsourcec00hugh
# Pages:        540-547
# Mod history:  2016-10-06 JL   Created.
#               2017-01-11 JL   Transcribed pages 540-544.
#               2017-01-12 JL   Transcribed pages 545-547.

# Annotations:
#
#   B L A I R - S M I T H
#    Y U L   S Y S T E M
#
#       Listings in
#    MITIGUS  Format
#

# Note: Page numbers are scan page numbers rather than listing.


L      @YUL SYSTEM ASSEMBLER AGC4 PASS 3

# Page 540
#      @YUL SYSTEM ASSEMBLER AGC4 PASS 3                                         USER'S OWN PAGE NO.   1        PAGE  59

R0001   WE ARE DEEPLY INDEBTED TO THE ONE, THE ONLY, THE GREAT JOHN SHAW FOR THE FOLLOWING STRONG MAGIC....
R0003   JSOCTL



 0004              SETLOC,2     SIZ PASS 3    B1



 0005              MASKGRP,1    S,0           F,0



R0006              THE AGC4 PART OF PASS 3 CONTAINS SUBROUTINES WITH THE FOLLOWING FUNCTIONS:

R0008   A4 EDT DEF . . . . . . .SET A GIVEN ADDRESS IN PRINT IN WORDS 2 AND 3 OF THE PRINT LINE, OR SET UP BLOTS FOR A
R0010                           BAD ADDRESS.  TYPICALLY USED TO PRINT THE DEFINITION OF A SYMBOL.

R0012   A4 EDT AV1 . . . . . . .SET A GIVEN ADDRESS IN PRINT IN WORD 1 OF THE PRINT LINE.  TYPICALLY USED TO PRINT A
R0014                           LOWER LIMIT IN THE AVAILABILITY DISPLAY.

R0015   A4 PRIN PN . . . . . . .SET IN PRINT UPPER ADDRESS LIMIT, PARAGRAPH NUMBER, STICK NUMBER AND SENSE WIRE NUMBERS
R0017                           FOR A PARAGRAPH.

R0018   A4 EDIT WD . . . . . . .FORM A 14-CHARACTER PRINT IMAGE OF A BYPT WORD.  FLAG REFERENCES TO UNUSED FIXED MEMORY.
R0020                           GENERATE A CHECKSUM FOR EACH BANK OR SHOW THE REASON WHY NOT.

R0022   A4 EXPLAN  . . . . . . .PRINT A TWO-LINE SUBHEADING FOR AN OCTAL PARAGRAPH LISTING.



R0024              QUANTITIES FILLED IN BY THE AGC4 PART OF PASS 3 FOR REFERENCE BY THE GENERAL PART OF PASS 3.

 0026   A4 SUB SPX CAC          A4 PRIN PN    A4 EDT AV1    A4 EDT DEF
 0027              CAC                        A4 EXPLAN     A4 EDIT WD

 0028   A4 MAX LOC OCT          - 71777
 0029   A4 MAX SSN OCT          -100163
 0030   A4W TO     ALF           TO
 0031   A4W TO MSK OCT          007777

# Page 541
#      @YUL SYSTEM ASSEMBLER AGC4 PASS 3                                         USER'S OWN PAGE NO.   2        PAGE  60

P0032   CONSTANTS AND RESERVATIONS.



 0033   A4 FLG MSK OCT          0000 0070
 00332  A4 FLAG 4  OCT          0000 0040
 0034   A43 BLOTS  ALF,2               ▪▪▪▪
 0035   A43 BANK   ALF              00,0
 0036   M COMMON   RESERVE      2

 0037   OCT 1777   EQUALS       B39T48
 0039   L STIK NOS SPEC                                     STICK NOS -1
 0040   L WIRE NOS SPEC                                     WIRE NOS

 00401  BIT 2      OCT          2
 0041   PL 77777   OCT          +77777
 00414  OCT 2000   OCT          -2000
 0042   OCT 5777   OCT          -5777

 00431  OCTAL CTR  OCT          -7
 0044   L TC SELF  OCT          0
 0045   NEXT EQIV  OCT          0
 0046   CHECKSUM   OCT          0

 0047   BANK ZERS  ALF          00,0000
 0048   ZEROS 1T3  ALF          000
 0049   ZERO C8    ALF                 0
 0050   ZERO C4T7  ALF             0000
 0051   EMPTY WD   ALF           00000 0

 0052   LETTER C   ALF                C:
 0053   LETTER I   ALF                I:
 0054   W CKSUM    ALF              CKSM
 0055   W NOSUM    ALF             NOSUM

 0056   BADRF CUS  ALF              ▪REF
 0057   EOB CUSS   ALF              ▪EOB
 0058   SUM CUSS   ALF              ▪SUM

 0059   AT AVAIL   ALF             @
 0060   W BAD WORC ALF          BAD WORC
 0061   W NO CKSUM ALF,2             NO CHECKSUM

 0062   W PARAG NO ALF,3        PARAGRAPH # 000
 0063   EDIT HED   ALF,5        OCTAL LISTING OF PARAGRAPH # 000, WITH P
 0064              ALF,5        ARITY BIT IN BINARY AT THE RIGHT OF EACH
 0065              ALF,5         WORD; "@" DENOTES UNUSED FIXED MEMORY.

# Page 542
#      @YUL SYSTEM ASSEMBLER AGC4 PASS 3                                         USER'S OWN PAGE NO.   3        PAGE  61

P0066   FURTHER ALPHABETIC CONSTANTS.



 0067   W WD MARKS OCT          0002
 0068              ALF,5        ALL VALID WORDS ARE BASIC INSTRUCTIONS E
 0069              ALF,5        XCEPT THOSE MARKED "I" (INTERPRETIVE OPE
 0070              ALF,5        RATOR WORDS) OR "C" (CONSTANTS).

 0071   W SPECENT  ALF,4        SPECIAL AND CENTRAL REGISTERS &
 0072   W ERAS MEM ALF,2        ERASABLE MEMORY
 0073   W STICK N  ALF          STICK #
 0074   W MODULE   ALF            MODULE
 0075   W SENS LNS ALF,3        SENSE LINE SET 0 (WIRES

 0076   STICK NOS  ALF,4        R1B B28 R2A B29 R2B B29 R1A B28
 0077              ALF,4        S1B B21 S2A B22 S2B B22 S1A B21
 0078              ALF,4        T1B B23 T2A B24 T2B B24 T1A B23

 0079   WIRE NOS   ALF,4        1-16)   17-32)  33-48)  49-64)
 0080              ALF,4        65-80)  81-96)  97-112) 113-128)

# Page 543
#      @YUL SYSTEM ASSEMBLER AGC4 PASS 3                                         USER'S OWN PAGE NO.   4        PAGE  62

P0081              SUBROUTINE IN PASS 3 FOR AGC4 TO SET IN PRINT THE DEFINITION OF A SYMBOL OR THE UPPER LIMIT OF AN
R0083   ITEM IN THE AVAILABILITY DISPLAY.  SUPPLY AN INTEGER IN EQIVLENT OR ONES IF THERE IS NO DEFINITON.  ENTRY IS
R0085              TS           M EDIT DEF    -             Z,CSC          C    SETS UP THE DEFINITION USING THE BANK
R0087   NOTATION, OR SETS UP FOUR BLOTS IF NO DEFINITION.



 0088   A4 EDT DEF NA           EQIVLENT      ONES          C,+2           C
 0089              TN           A43 BLOTS     2             LINE +2             PRINT BLOTS AND EXIT.

 0091              LA           EQIVLENT      OCT  5777     NON BANK       C    BRANCH IF LOCATION IS NOT IN A BANK.
 0093              TX           A43 BANK      -             LINE +2        C
 0096              SWS, ACO5    EQIVLENT      5,L           LINE +2        C
 0097              SWS, ACO6    EQIVLENT      2,L           LINE +2        C    SET BANK NUMBER IN PRINT.
 0099              SM           EQIVLENT      OCT  6000     M COMMON       C    PUT SUBADDRESS IN THE RANGE 6000-7777.
 0101              TS           ZEROS 1T3     LINE +3       C,+3           C

 0102   NON BANK   TX           EQIVLENT      -             M COMMON       C
 0103              TS           ZERO C8       LINE +2       C,-2           C

 0104              SWS, ACO8    M COMMON      9             LINE +2        C
 0105              SWS, ACO1    M COMMON      12            LINE +3        C
 0106              SWS, ACO2    M COMMON      15            LINE +3        C
 0107              SWS, ACO3    M COMMON      18            LINE +3             SET SUBADDRESS IN PRINT AND EXIT.



R0109              SUBROUTINE IN PASS 3 FOR AGC4 TO SET IN PRINT THE LOWER LIMIT OF AN ITEM IN THE AVAILABILITY DISPLAY
R0111   OR AN ADDRESS IN THE PARAGRAPH EDIT.  WITH THE ADDRESS SUPPLIED AS AN INTEGER IN ADDRESS, ENTRY IS
R0113              SWE, CAC3    M EDIT AV1    16            Z,CSC          C    USES THE BANK NOTATION.



 0115   A4 EDT AV1 LA           ADDRESS       OCT  5777     NON BANK 1     C
 0116              TX           BANK ZERS     -             LINE +1        C
 0119              SWS, ACO1    ADDRESS       29,L          LINE +1        C
 0120              SWS, ACO2    ADDRESS       26,L          LINE +1        C    PRINT BANK NUMBER.
 0122              SM           ADDRESS       N,CSC,3       M COMMON       C    PUT SUBADDRESS IN THE RANGE 6000-7777.
 0124   OCT  6000  OCT          -6000

 0125   NON BANK 1 TX           ZERO C4T7     -             LINE +1        C
 0126              TX           ADDRESS       -             M COMMON       C

 0127              SWS, ACO4    M COMMON      15,L          LINE +1        C
 0128              SWS, ACO5    M COMMON      12,L          LINE +1        C
 0129              SWS, ACO6    M COMMON      9,L           LINE +1        C
 0130              SWS, ACO7    M COMMON      6,L           LINE +1             SET SUBADDRESS IN PRINT AND EXIT.

# Page 544
#      @YUL SYSTEM ASSEMBLER AGC4 PASS 3                                         USER'S OWN PAGE NO.   5        PAGE  63

P0132              SUBROUTINE IN PASS 3 FOR AGC4 TO MAKE A 14-CHARACTER IMAGE CORRESPONDING TO AN AGC4 WORD.  THE IMAGE
R0134   IS DELIVERED IN IMAGE AND IMAGE+1 AND CONSISTS OF ANY OF THE FOLLOWING, RIGHT-JUSTIFIED SO THAT THE RIGHT-HAND
R0136   14 CHARACTERS CAN BE USED:

R0137   IF AN UNUSED NON-FIXED LOCATION,
R0138   IF AN UNUSED FIXED LOCATION,               @                            OR "NO CHECKSUM" IF PREVENTS CHECKSUM.
R0140   IF A CONFLICT LOCATION,                 CONFLICT
R0141   IF A BAD WORD,                          BAD WORD
R0142   IF A BASIC INSTRUCTION,                  XXXXX P                   (FIVE OCTAL DIGITS AND A PARITY BIT)
R0144   IF REFERRING TO UNUSED LOCATION,    ▪REF XXXXX P
R0145   IF AN INTERPRETIVE OPERATOR WORD,     I: XXXXX P
R0146   IF A CONSTANT,                        C: XXXXX P                        PREFIX IS "CKSM" IF CONST IS A CHECKSUM.

R0148   OTHER PREFIXES: "▪EOB" FOR A USED WORD AFTER A CHECKSUM, "▪SUM" FOR A USED WORD THAT FOULS UP A PROPERLY RE-
R0150   QUESTED CHECKSUM, AND "NOSUM" FOR A USED WORD THAT PREVENTS A CHECKSUM.  "▪" PREFIXES COUNT AS CUSSED LINES.
R0152   ENTRY:     TX           M EDIT WD     -             Z,CSC          C    OR EQUIVALENT.



 0154   A4 EDIT WD TX           BLANKS        -             IMAGE          C
 01542             WA           OCTAL CTR     ONE           OCTAL CTR      C
 0155              LA           N,X2          W BAD WORC    TEST FLAG      C    BRANCH IF NOT "BAD WORD" OR "CONFLICT".
 01562             TX           N,X2          -             IMAGE +1       C
 0157              TS           A4 FLAG 4     N,X2          SET EOB FL     C    MARK AS SUCH, GO TO SET FLAG AND EXIT.

 0159              SS           ZERO          A4 FLG MSK    N,X2           C    REMOVE FLAG IF REFERS TO USED FIXED.
 0161   TEST FLAG  SSL, THREE   N,X2          27            C,+1           C    FOUR-WAY BRANCH ON TYPE OF FLAG.
 0163              TS           OCT 1777      AVAIL MSK     WHICH MEM      C
 0164              TS           LETTER I      IMAGE         C,-1           C
 0165              TS           LETTER C      IMAGE         C,-2           C

 0166              LA, B36T48   2,0           THREAD 11     TEST FLAG -1   C    BRANCH IF NOT REFERRING TO FIXED MEMORY.
 0168              SWS, B26T30  N,X2          17,L          C,+1           C
 0169              SWE, ONES    BIT 1         0             AVAIL MSK      C
 0170              SWE, B39T48  N,X2          26            M COMMON       C
 0171              SWS, B44T48  N,X2          6             M COMMON       C
 0172              WA           M COMMON      L AV TABLE    Z,X4           C
 0173              LA, AVAILMSK 4,0           ZERO          TEST FLAG -1   C    BRANCH IF LOCATION IS USED.

 01742 *           NA           SEG OWNER     BLANKS        TEST FLAG -1   C    NO CUSS IN SEGMENT ASSEMBLY.

 0175              TX           BADRF CUS     -             IMAGE          C    "▪REF" FOR REFERENCE TO UNUSED FIXED.

R0177   MINOR SUBROUTINE IN PASS 3 FOR AGC4 TO REGISTER AN ERROR FOUND DURING THE MAKING OF THE OCTAL STORAGE MAP.

 0179   P3 ERRORS  DA           N OCT ERRS    ONE           N OCT ERRS     C
 01812             SS           ZERO          BIT 16        SWITCH         C    PRINT REMAINDER OF OCTAL STORAGE MAP.
 0185              TX           Z,CSH         -             Z,CSC          C

# Page 545
#      @YUL SYSTEM ASSEMBLER AGC4 PASS 3                                         USER'S OWN PAGE NO.   6        PAGE  64

P0186   MAIN STREAM OF EDIT WD CONTINUED: PRINT WORD AND START CHECKSUM PROCESSING.

 0188   WHICH MEM  TS           EMPTY WD      IMAGE +1      WD PRINT       C
 01882             TX           ADDRESS       -             EQIVLENT       C
 0189              LA           OCT 2000      EQIVLENT      C,+3           C    BRANCH IF LOCATION IS IN FIXED MEMORY.
 0191              NA           N,X2          ZERO          SET EOB FL     C    SET FLAG AND EXIT IF USED.
 0193              TS           BLANKS        IMAGE +1      SET EOB FL     C    OTHERWISE SEND ALL BLANKS AND DITTO.

 0195              SS           OCTAL CTR     ACO8          EQIVLENT       C    FILL IN LAST OCTAL DIGIT OF ADDRESS.
 0198              TX           NEXT EQIV     -             M COMMON       C    FOR IMMEDIATE MISSING-PARAGRAPH CHECK.
 0200              WA           EQIVLENT      ONE           NEXT EQIV      C    SET CRITERION FOR MISSING-PARAG. CHECK.
 0202              NA, AVAILMSK EQIVLENT      ZERO          C,+2           C    BRANCH IF NOT THE BEGINNING OF A BANK.
 0204              TS           ZERO          CHECKSUM      TC SELF Q      C

 0205              NA           EQIVLENT      M COMMON      LACK PARA      C    BRANCH IF PARAGRAPH(S) MISSING.
 0207              LA           BIT 1         CHECKSUM      NO SUMING      C    BRANCH IF NO SUMMING TO BE DONE.
 0208              LA           BIT 2         CHECKSUM      PLANT SUM -1   C    BRANCH IF CHECKSUM MUST BE PLANTED NOW.
 0210              NA, AVAILMSK EQIVLENT      ONES          TC SELF Q      C    BRANCH IF NOT END OF BANK.
 0212              LA           N,X2          ZERO          PLANT SUM      C    BRANCH TO PLANT SUM IN LONE HOLE AT END.
 0214              TS           W NOSUM       IMAGE         SET EOB FL     C    SHOW NO CHECKSUM IN FULL BANK, EXIT.

 0220   NO SUMING  NA           N,X2          ZERO          C,+2           C    BRANCH ON USED WORD WHEN NOT SUMMING.
 0222              TX           AT AVAIL      -             IMAGE +1       S    EXIT WITH  AT  FOR UNUSED FIXED.
 0224              LA           ONES          CHECKSUM      N,SC,1         C    EXIT IF SUMMING WAS GIVEN UP.
 0226              NA           EQIVLENT      OCT 5777      SET EOB FL -1  C
 0227              NA           N,X2          N,CSC,2       SET EOB FL -1  C    FLAG AND EXIT IF IT'S THE EXTEND CONST.
 0229              OCT          -20 0011 7776
 0230              TS           EOB CUSS      IMAGE         P3 ERRORS      C    "▪EOB" FOR USED WORD AFTER CHECKSUM.
 0232   SET EOB FL TX           ONES          -             CHECKSUM       S

 0233              NA           N,X2          ZERO          SUM ERROR      C    BRANCH IF USED WD WHERE CKSM SHOULD GO.
 0235   PLANT SUM  SWE, B44T48  EQIVLENT      10            M COMMON       C    FORM POSITIVE BANK NUMBER.  CHECKSUM IS
 0237              SS           ZERO          BIT 2         CHECKSUM       C    SUCH THAT TOTAL SUM IS + OR - BANK NO.
 0238              LA           CHECKSUM      B35T48        C,+2           C    BRANCH IF CHECKSUM IS POSITIVE.
 0240              HA           M COMMON      CAC3          M COMMON       C    GIVE BANK NUMBER AGC SIGN OF CHECKSUM.
 0242              BS           M COMMON      CHECKSUM      N,X2           C
 0243              LA           N,X2          BIT 1         C,+2           C    BRANCH IF ALG. VAL. OF CKSM IS PLUS.
 0245              HA           N,X2          PL 77777      N,X2           C
 0246              SWE, B41T48  N,X2          8             M COMMON +1    C
 0247              HA           N,X2          M COMMON +1   M COMMON       C
 0248              SWE, D12     M COMMON      4             M COMMON +1    C
 0249              HA           M COMMON      M COMMON +1   M COMMON       C
 0250              SWE, THREE   M COMMON      2             M COMMON +1    C
 0251              HA           M COMMON      M COMMON +1   M COMMON       C
 0252              SWE, ONE     M COMMON      1             M COMMON +1    C
 0253              HA           M COMMON      M COMMON +1   M COMMON       C
 0254              HA           M COMMON      ONES          M COMMON       C
 0255              WA           N,X2          N,X2          N,X2           C
 0256              SS           M COMMON      B20 B48       N,X2           C    SHIFT CHECKSUM AND INSERT PARITY BIT.
 0258              TS           EMPTY WD      IMAGE +1      WD PRINT       C    PRINT WITH PREFIX "CKSM".
 0260              TS           W CKSM        IMAGE         SUM ERROR +1   C    SHOW THAT SUM IS DONE AND EXIT.

# Page 546
#      @YUL SYSTEM ASSEMBLER AGC4 PASS 3                                         USER'S OWN PAGE NO.   7        PAGE  65

P0262   PROCEDURE IN EDIT WD TO WATCH FOR TWO TC SELFS IN SUCCESSION, TO MAKE THE CHECKSUM FOLLOW THEM.

 0264   TC SELF Q  SWE, SPECMAG N,X2          1             M COMMON       C
 0265              TX           OCT 6000      -             M COMMON +1    C
 0266              LA           OCT 6000      EQIVLENT      C,+2           C    BRANCH IF LOCATION IS IN A BANK.
 0268              TS           EQIVLENT      M COMMON +1   C,+2           C
 0269              SS           EQIVLENT      OCT 1777      M COMMON +1    C
 0270              NA           M COMMON      M COMMON +1   NO TC SELF     C
 0271              NA           IMAGE         BLANKS        NO TC SELF     C    BRANCH IF NOT AN INSTRUCTION.

 0272              LA           EQIVLENT      L TC SELF     C,+2           C    BRANCH IF THIS IS 2ND TC SELF IN A ROW.
 0273              TS           NEXT EQIV     L TC SELF     ADD 2 SUM      C    SET CRITERION FOR TWO-IN-A-ROW CHECK.
 0274              NA, OCT377   EQIVLENT      ONES          ADD 2 SUM -1   C    BRANCH IF TC SELFS NOT AT END OF PARAGR.
 0275              SWE, ACO8    EQIVLENT      13            M COMMON       C
 02753             WA           L SUBSTRB     M COMMON      Z,X4           C
 02756             SWS, B26T30  EQIVLENT      10,L          C,+1           C
 0276              SWE, ONES    BIT 2         0             AVAIL MSK      C    LOOK IN SUBSTRAB FOR NEXT PARAGRAPH.
 0277              NA, AVAILMSK 4,0           ZERO          ADD 2 SUM -1   C    BRANCH IF NEXT PARAGRAPH EXISTS.

 0278   SUM ERROR  TS           SUM CUSS      IMAGE         P3 ERRORS      C    "▪SUM" FOR FOULED-UP CHECKSUM REQUEST.
 0280              TX           BIT 1         -             CHECKSUM       S    PREPARE POST-CHECKSUM TESTS, EXIT.

 0282   NO TC SELF NA           N,X2          ZERO          ADD 2 SUM      C
 0283              TX           W NO CKSUM    -             IMAGE          C    "   NO CHECKSUM" FOR AN UNUSED WORD THAT
 0285              TS           W NO CKSUM +1 IMAGE +1      SET EOB FL     C    PREVENTS FORMATION OF A CHECKSUM.

 0287              SM           CHECKSUM      BIT 2         CHECKSUM       C    REQUEST CHECKSUM IN NEXT WORD.
 0289   ADD 2 SUM  EX           N,X2          CAC3          M COMMON       C
 0290              SWS, SPECMAG N,X2          1             M COMMON       C    PUT WORD INTO SIGN-DUPLICATED FORM.
 0292              WA           CHECKSUM      M COMMON      CHECKSUM       C
 0293              SSL, ACO8    CHECKSUM      14            C,+1           C    TEST FOR END-AROUND CARRY AND OVERFLOW.

R0295   THIS SIX-WAY BRANCH ACHIEVES END-AROUND CARRY PROPAGATION, OVERFLOW CORRECTION, AND ADDITION OF SGN(OVERFLOW).

 0297              TX           -             -             -              S    000: NO EAC, +, NO OFLO.
 0299              WD           CHECKSUM      B35T48        CHECKSUM       S    001: NO EAC, +,    OFLO.
 0301   B35T48     OCT          -37777                                          010: IMPOSSIBLE.
 0303              TX           -             -             -              S    011: NO EAC, -, NO OFLO.
 0305              WD           CHECKSUM      CAC3          CHECKSUM       S    100:    EAC, +, NO OFLO.
 0307   B20 B48    OCT          -20 0000 0001                                   101: IMPOSSIBLE.
 0309              WA           CHECKSUM      B35T48        CHECKSUM       C    110:    EAC, -,    OFLO.
 0311              WD           CHECKSUM      CAC3          CHECKSUM       S    111:    EAC, -, NO OFLO.

R03121  RESPONSE TO DISCOVERY THAT ONE OR MORE PARAGRAPHS ARE MISSING... EFFECT ON CHECKSUM PROCESSING.

 03123  LACK PARA  LA, AVAILMSK M COMMON      ZERO          C,+4           C    BRANCH IF NEW PH MUST BE IN NEW BANK.
 03124             SS           EQIVLENT      AVAIL MSK     M COMMON       C
 03125             NA           EQIVLENT      M COMMON      C,+2           C    BRANCH IF NEW PARAGRAPH IS IN NEW BANK.
 03126             LA           BIT 1         CHECKSUM      NO SUMING      C    BRANCH IF NO SUMMING TO BE DONE.
 03127             NA           N,X2          ZERO          NO SUMING -1   C    BRANCH TO APPLY PREFIX "NOSUM" TO WORD.
 03128             TS           W NO CKSUM    IMAGE         NO TC SELF +2  C    BUT FOR EMPTY WORD WRITE "NO CHECKSUM".

# Page 547
#      @YUL SYSTEM ASSEMBLER AGC4 PASS 3                                         USER'S OWN PAGE NO.   8        PAGE  66

P0313   MINOR SUBROUTINE IN PASS 3 FOR AGC4 TO SET AN ASSEMBLED WORD IN PRINT FOR THE OCTAL STORAGE MAP.

 0316   WD PRINT   SWS, ACO2    N,X2          23,L          IMAGE +1       C
 0317              SWS, ACO3    N,X2          20,L          IMAGE +1       C
 0318              SWS, ACO4    N,X2          17,L          IMAGE +1       C
 0319              SWS, ACO5    N,X2          14,L          IMAGE +1       C
 0320              SWS, ACO6    N,X2          11,L          IMAGE +1       C    SET IN PRINT 5 OCTAL DIGITS OF WORD.
 0322              SS           N,X2          ONE           IMAGE +1       C    SET PARITY BIT IN PRINT.
 0324              TX           Z,CSH         -             Z,CSC          C


R0325              SUBROUTINE IN PASS 3 FOR AGC4 TO SET IN PRINT THE UPPER ADDRESS LIMIT, PARAGRAPH NUMBER, STICK NUMBER
R0327   AND SENSE WIRE NUMBERS FOR THE PARAGRAPH SUMMARY.  ENTRY IS
R0328              SWE, CAC3    M PRINT PN    32            Z,CSC          C    EXITS TO EDIT DEF TO PRINT UPPER LIMIT.


 0330   A4 PRIN PN TN           W PARAG NO    3             LINE +4        C
 0331              SWS, ACO5    Z,R0          12,L          LINE +5        C
 0332              SWS, ACO6    Z,R0          9,L           LINE +5        C
 0333              SWS, ACO7    Z,R0          6,L           LINE +5        C    EDIT PARAGRAPH NUMBER.
 0335              SWE, D12     Z,R0          2             Z,AU1          C
 0336              NA           Z,AU1         ZERO          FIX MEM        C

 0337              NA           LINE +5       W PARAG NO +1 C,+2           C    BRANCH IF PARAGRAPHS 001-003.
 0339              TN           W SPECENT     4             N,AU2          C    "SPECIAL AND CENTRAL REGISTERS & ".
 0341              TN           W ERAS MEM    2             N,AU2          C    "ERASABLE MEMORY ".
 0343              TS           EQIVLENT      M COMMON      NON BANK +1    C    JOIN NON-BANK PART OF A4 EDT DEF.

 0345   FIX MEM    WA           Z,AU1         L STIK NOS    Z,AU1          C
 0346              TX           W MODULE      -             N,AU2,1        C
 0347              SWS, C1T4    N,AU1         A,3,L         N,AU2,1        C    "  MODULE BXX    ".
 0349              TX           W STICK N     -             N,AU2,1        C
 0350              SS           N,AU1         C1T4          N,AU2,1        C    "STICK # XXX     ".
 0352              SWE, ACO8    Z,R0          4             Z,CSH          C
 0353              SS           Z,R0          THREE         Z,CSH          C
 0354              SS           Z,CSH         ACO 8         W SENS LNS +1  C    SET UP SENSE LINE SET NUMBER.
 0356              WA           Z,CSH         L WIRE NOS    Z,CSH          C
 0357              TN           W SENS LNS    3             N,AU2          C    "SENSE LINE SET N (WIRES ".
 0359              TS           N,CSH         N,AU2         A4 EDT DEF +2  C    SET UP WIRE NUMBERS AND JOIN A4 EDT DEF.



R0361              SUBROUTINE IN PASS 3 FOR AGC4 TO PRINT TWO EXPLANATORY LINES AT THE HEAD OF EACH PAGE OF AN OCTAL
R0363   PARAGRAPH LISTING.  ENTRY IS
R0364              SWE, CAC3    M EXPLAIN     16            Z,CSC          C


 0365   A4 EXPLAN  TN           EDIT HED      15            PAGE HED2 +1   C
 0366              SWS, ACO6    N,X2          6,L           PAGE HED2 +4   C
 0367              SWS, ACO7    N,X2          3,L           PAGE HED2 +4   C
 0368              SS           N,X2          ACO 8         PAGE HED2 +4   C    PUT PARAGRAPH NUMBER IN FIRST LINE.
 0370              TN           W WDMARKS     16            LINE           C
 0371              TS           Z,SC          Z,X4          PRINT LIN           PRINT EXPLANATION AND RETURN.
