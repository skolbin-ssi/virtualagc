   1   <PROGRAM>  ::=   <PROGRAM HEADING> <BLOCK> .
   2   <BLOCK>  ::=   <FIRSTPART> <SECONDPART>
   3   <FIRSTPART>  ::=   <LABEL DECLS> <CONST DECLS> <TYPE DEFNS>
   4   <SECONDPART>  ::=   <VAR DECLS> <PROC DECL LIST> <STATEMENT PART>
   5   <STATEMENT PART>  ::=   BEGIN <STATEMENT> <STAT LIST> END
   6   <STAT LIST>  ::=   ; <STATEMENT> <STAT LIST>
   7   <STAT LIST>  ::=   ... EMPTY PRODUCTION ...
   8   <LABEL DECLS>  ::=   LABEL <UNSIGNED INTEGER> <LABEL LIST> ;
   9   <LABEL DECLS>  ::=   ... EMPTY PRODUCTION ...
  10   <LABEL LIST>  ::=   , <UNSIGNED INTEGER> <LABEL LIST>
  11   <LABEL LIST>  ::=   ... EMPTY PRODUCTION ...
  12   <CONST DECLS>  ::=   CONST <IDENTIFIER> <CONST VALUE PART>
  13   <CONST DECLS>  ::=   ... EMPTY PRODUCTION ...
  14   <CONST DECL LIST>  ::=   <IDENTIFIER> <CONST VALUE PART>
  15   <CONST DECL LIST>  ::=   ... EMPTY PRODUCTION ...
  16   <CONST VALUE PART>  ::=   = <CONSTANT> <CONST DECL TAIL>
  17   <CONST DECL TAIL>  ::=   ; <CONST DECL LIST>
  18   <TYPE DEFNS>  ::=   TYPE <IDENTIFIER> <TYPE DESCRIPTOR PART>
  19   <TYPE DEFNS>  ::=   ... EMPTY PRODUCTION ...
  20   <TYPE DEF LIST>  ::=   <IDENTIFIER> <TYPE DESCRIPTOR PART>
  21   <TYPE DEF LIST>  ::=   ... EMPTY PRODUCTION ...
  22   <TYPE DESCRIPTOR PART>  ::=   = <TYPE> <TYPE DEFN TAIL>
  23   <TYPE DEFN TAIL>  ::=   ; <TYPE DEF LIST>
  24   <PROC DECL LIST>  ::=   <PROC DECL> <PROC DECL TAIL>
  25   <PROC DECL LIST>  ::=   ... EMPTY PRODUCTION ...
  26   <PROC DECL> ::= PROCEDURE <IDENTIFIER> <PARAM LIST> ; <BODY>
  27   <PROC DECL>  ::= FUNCTION <IDENTIFIER> <PARAM LIST> <RETURNS> ; <BODY>
  28   <BODY>  ::=   <BLOCK>
  29   <VAR DECLS>  ::=   VAR <ID LIST> : <TYPE> <VAR DECL TAIL>
  30   <VAR DECLS>  ::=   ... EMPTY PRODUCTION ...
  31   <VAR DECL LIST>  ::=   <ID LIST> : <TYPE> <VAR DECL TAIL>
  32   <VAR DECL LIST>  ::=   ... EMPTY PRODUCTION ...
  33   <VAR DECL TAIL>  ::=   ; <VAR DECL LIST>
  34   <ID LIST>  ::=   <IDENTIFIER> <ID LIST 2>
  35   <ID LIST 2>  ::=   , <ID LIST>
  36   <ID LIST 2>  ::=   ... EMPTY PRODUCTION ...
  37   <STATEMENT>  ::=   <UNSIGNED INTEGER> : <UNLABLED STATEMENT>
  38   <STATEMENT>  ::=   <UNLABLED STATEMENT>
  39   <UNLABLED STATEMENT>  ::=   <IDENTIFIER> <ID STATEMENT>
  40   <UNLABLED STATEMENT>  ::=   BEGIN <STATEMENT> <STAT LIST> END
  41   <UNLABLED STATEMENT>  ::= IF <EXPRESSION> THEN <STATEMENT> <ELSE PART>
  42   <UNLABLED STATEMENT> ::= CASE <EXPRESSION> OF <CASE LIST> END
  43   <UNLABLED STATEMENT>  ::=   WHILE <EXPRESSION> DO <STATEMENT>
  44   <UNLABLED STATEMENT>  ::= REPEAT <STATEMENT> <STAT LIST> UNTIL <EXPRESSION>
  45   <UNLABLED STATEMENT>  ::= FOR <IDENTIFIER> := <EXPRESSION> <FOR STAT 2>
  46   <UNLABLED STATEMENT>  ::= WITH <VARIABLE> <VAR LIST> DO <STATEMENT> <CLOSE>
  47   <UNLABLED STATEMENT>  ::=   GOTO <UNSIGNED INTEGER>
  48   <UNLABLED STATEMENT>  ::=   ... EMPTY PRODUCTION ...
  49   <ID STATEMENT>  ::=   := <EXPRESSION>
  50   <ID STATEMENT>  ::= (. <EXPRESSION> <EXP LIST> .) <QUALIFIER> := <EXPRESSION>
  51   <ID STATEMENT> ::= . <IDENTIFIER> <QUALIFIER> := <EXPRESSION>
  52   <ID STATEMENT>  ::=   @ <QUALIFIER> := <EXPRESSION>
  53   <ID STATEMENT> ::= ( <EXPRESSION> <EXP LIST> ) <ASSIGNMENT PART>
  54   <ID STATEMENT>  ::=   ... EMPTY PRODUCTION ...
  55   <EXP LIST>  ::=   , <EXPRESSION> <EXP LIST>
  56   <EXP LIST>  ::=   ... EMPTY PRODUCTION ...
  57   <ELSE PART>  ::=   ELSE <STATEMENT>
  58   <ELSE PART>  ::=   ... EMPTY PRODUCTION ...
  59   <FOR STAT 2>  ::=   DOWNTO <EXPRESSION> DO <STATEMENT>
  60   <FOR STAT 2>  ::=   TO <EXPRESSION> DO <STATEMENT>
  61   <VAR LIST>  ::=   , <VARIABLE> <VAR LIST>
  62   <VAR LIST>  ::=   ... EMPTY PRODUCTION ...
  63   <PARAM LIST>  ::=   ( <PARAM LIST 1> )
  64   <PARAM LIST>  ::=   ... EMPTY PRODUCTION ...
  65   <PARAM LIST 1>  ::=   <ID LIST> : <IDENTIFIER> <PARAM LIST 2>
  66   <PARAM LIST 1>  ::=   VAR <ID LIST> : <IDENTIFIER> <PARAM LIST 2>
  67   <PARAM LIST 1>  ::= FUNCTION <ID LIST> <PARAM TYPE LIST> : <IDENTIFIER> <PARAM LIST 2>
  68   <PARAM LIST 1>  ::= PROCEDURE <ID LIST> <PARAM TYPE LIST> <PARAM LIST 2>
  69   <PARAM LIST 2>  ::=   ; <PARAM LIST 1>
  70   <PARAM LIST 2>  ::=   ... EMPTY PRODUCTION ...
  71   <PARAM TYPE LIST>  ::=   ( <PARAM TYPES> )
  72   <PARAM TYPE LIST>  ::=   ... EMPTY PRODUCTION ...
  73   <CASE LIST>  ::= <CONSTANT> <CONST LIST> : <STATEMENT> <CASE LIST 2>
  74   <CASE LIST 2>  ::=   ; <CASE LIST>
  75   <CASE LIST 2>  ::=   ... EMPTY PRODUCTION ...
  76   <CONST LIST>  ::=   , <CONSTANT> <CONST LIST>
  77   <CONST LIST>  ::=   ... EMPTY PRODUCTION ...
  78   <VARIABLE>  ::=   <IDENTIFIER> <QUALIFIER>
  79   <QUALIFIER>  ::=   (. <EXPRESSION> <EXP LIST> .) <QUALIFIER>
  80   <QUALIFIER>  ::=   . <IDENTIFIER> <QUALIFIER>
  81   <QUALIFIER>  ::=   @ <QUALIFIER>
  82   <QUALIFIER>  ::=   ... EMPTY PRODUCTION ...
  83   <SIMPLE TYPE>  ::=   <IDENTIFIER> <SIMPLE TYPE 1>
  84   <SIMPLE TYPE>  ::=   ( <ID LIST> )
  85   <SIMPLE TYPE>  ::=   + <CONSTANT 1> .. <SIGN> <CONSTANT 1>
  86   <SIMPLE TYPE>  ::=   - <CONSTANT 1> .. <SIGN> <CONSTANT 1>
  87   <SIMPLE TYPE>  ::=   <UNSIGNED INTEGER> .. <SIGN> <CONSTANT 1>
  88   <SIMPLE TYPE>  ::=   <STRING> .. <SIMPLE TYPE 3>
  89   <SIMPLE TYPE 1>  ::=   .. <SIMPLE TYPE 2>
  90   <SIMPLE TYPE 1>  ::=   ... EMPTY PRODUCTION ...
  91   <SIMPLE TYPE 2>  ::=   <SIGN> <CONSTANT 1>
  92   <SIMPLE TYPE 2>  ::=   <STRING>
  93   <SIMPLE TYPE 3>  ::=   <IDENTIFIER>
  94   <SIMPLE TYPE 3>  ::=   <STRING>
  95   <CONSTANT 1>  ::=   <IDENTIFIER>
  96   <CONSTANT 1>  ::=   <UNSIGNED INTEGER>
  97   <TYPE>  ::=   <SIMPLE TYPE>
  98   <TYPE>  ::=   @ <TYPE IDENTIFIER>
  99   <TYPE>  ::=   <STRUC TYPE>
 100   <TYPE>  ::=   PACKED <STRUC TYPE>
 101   <STRUC TYPE> ::= ARRAY  <INDEX  AND  ELEMENTS  TYPE>
 102   <STRUC TYPE>  ::=   FILE OF <TYPE>
 103   <STRUC TYPE>  ::=   SET OF <SIMPLE TYPE>
 104   <STRUC TYPE>  ::=   RECORD <FIELD LIST> <RECORD ENDING>
 105   <SIMPLE TYPE LIST>  ::=   , <SIMPLE TYPE> <SIMPLE TYPE LIST>
 106   <SIMPLE TYPE LIST>  ::=   ... EMPTY PRODUCTION ...
 107   <FIELD LIST>  ::=   <ID LIST> : <TYPE> <NEXT FIELD>
 108   <FIELD LIST>  ::= CASE <IDENTIFIER> : <IDENTIFIER> OF <VARIANT INSTANCE>
 109   <NEXT FIELD>  ::=   ; <FIELD LIST>
 110   <NEXT FIELD>  ::=   ... EMPTY PRODUCTION ...
 111   <TAG IDENT>  ::=   <IDENTIFIER>
 112   <VARIANT INSTANCE LIST>  ::= ; <VARIANT INSTANCE> <VARIANT INSTANCE LIST>
 113   <VARIANT INSTANCE LIST>  ::=   ... EMPTY PRODUCTION ...
 114   <VARIANT INSTANCE>  ::= <CONSTANT> <CONST LIST> : <VARIANT INSTANCE 2>
 115   <VARIANT INSTANCE 2>  ::=   ( <FIELD LIST> )
 116   <CONSTANT>  ::=   <STRING>
 117   <CONSTANT>  ::=   <SIGN> <CONSTANT 2>
 118   <CONSTANT 2>  ::=   <IDENTIFIER>
 119   <CONSTANT 2>  ::=   <UNSIGNED INTEGER>
 120   <CONSTANT 2>  ::=   <UNSIGNED REAL>
 121   <SIGN>  ::=   +
 122   <SIGN>  ::=   -
 123   <SIGN>  ::=   ... EMPTY PRODUCTION ...
 124   <EXPRESSION>  ::=   <SIMPLE EXPRESSION> <EXPRESSION 2>
 125   <EXPRESSION 2>  ::=   <RELOP> <SIMPLE EXPRESSION>
 126   <EXPRESSION 2>  ::=   ... EMPTY PRODUCTION ...
 127   <RELOP>  ::=   =
 128   <RELOP>  ::=   <
 129   <RELOP>  ::=   >
 130   <RELOP>  ::=   <>
 131   <RELOP>  ::=   <=
 132   <RELOP>  ::=   >=
 133   <RELOP>  ::=   IN
 134   <SIMPLE EXPRESSION>  ::=   + <TERM> <SIMPLE EXPRESSION 2>
 135   <SIMPLE EXPRESSION>  ::=   - <TERM> <SIMPLE EXPRESSION 2>
 136   <SIMPLE EXPRESSION>  ::=   <TERM> <SIMPLE EXPRESSION 2>
 137   <SIMPLE EXPRESSION 2>  ::=   <ADDOP> <TERM> <SIMPLE EXPRESSION 2>
 138   <SIMPLE EXPRESSION 2>  ::=   ... EMPTY PRODUCTION ...
 139   <ADDOP>  ::=   +
 140   <ADDOP>  ::=   -
 141   <ADDOP>  ::=   OR
 142   <TERM>  ::=   <FACTOR> <TERM 2>
 143   <TERM 2>  ::=   <MULOP> <FACTOR> <TERM 2>
 144   <TERM 2>  ::=   ... EMPTY PRODUCTION ...
 145   <MULOP>  ::=   *
 146   <MULOP>  ::=   /
 147   <MULOP>  ::=   DIV
 148   <MULOP>  ::=   MOD
 149   <MULOP>  ::=   AND
 150   <FACTOR>  ::=   <IDENTIFIER> <FACTOR 2>
 151   <FACTOR>  ::=   ( <EXPRESSION> <RPARENT>
 152   <FACTOR>  ::=   NOT <FACTOR>
 153   <FACTOR>  ::=   (. <ELEMENTS> .)
 154   <FACTOR>  ::=   <UNSIGNED INTEGER>
 155   <FACTOR>  ::=   <UNSIGNED REAL>
 156   <FACTOR>  ::=   <STRING>
 157   <FACTOR>  ::=   NIL
 158   <FACTOR 2>  ::=   <QUALIFIER>
 159   <QUALIFIER>::= ( <EXPRESSION> <EXP LIST> ) <QUALIFIER>
 160   <ELEMENTS>  ::=   <EXPRESSION> <ELEMENTS 2> <ELEMENT LIST>
 161   <ELEMENTS>  ::=   ... EMPTY PRODUCTION ...
 162   <RPARENT>  ::=   )
 163   <PROGRAM HEADING> ::= PROGRAM <IDENTIFIER> <PROGRAM HEADING TAIL>
 164   <RETURNS>  ::=   : <TYPE IDENTIFIER>
 165   <RETURNS>  ::=   ... EMPTY PRODUCTION ...
 166   <BODY>  ::=   FORWARD
 167   <CASE LIST>  ::=   <CASE LIST 2>
 168   <FIELD LIST>  ::=   <NEXT FIELD>
 169   <VARIANT INSTANCE>  ::=   ... EMPTY PRODUCTION ...
 170   <ELEMENTS 2>  ::=   .. <EXPRESSION>
 171   <ELEMENTS 2>  ::=   ... EMPTY PRODUCTION ...
 172   <ELEMENT LIST>  ::=   , <EXPRESSION> <ELEMENTS 2> <ELEMENT LIST>
 173   <ELEMENT LIST>  ::=   ... EMPTY PRODUCTION ...
 174   <PARAM TYPES>  ::=   <ID LIST> <PARAM TYPES TAIL>
 175   <PARAM TYPES>  ::=   VAR <ID LIST> <PARAM TYPES TAIL>
 176   <PARAM TYPES>  ::=   FUNCTION <FUNC TYPES> <PARAM TYPES TAIL>
 177   <PARAM TYPES>  ::=   PROCEDURE <PROC TYPES> <PARAM TYPES TAIL>
 178   <PARAM TYPES>  ::=   <PARAM TYPES TAIL>
 179   <PARAM TYPES TAIL>  ::=   ... EMPTY PRODUCTION ...
 180   <PARAM TYPES TAIL>  ::=   ; <PARAM TYPES>
 181   <PROGRAM HEADING TAIL>  ::=   ( <ID LIST> ) <SEMICOLON>
 182   <SEMICOLON>  ::=   ;
 183   <PROC DECL TAIL>  ::=   ; <PROC DECL LIST>
 184   <FUNC TYPES>  ::= ( <PARAM TYPES> ) : <IDENTIFIER> <FUNC TYPES TAIL>
 185   <FUNC TYPES TAIL>  ::=   , <FUNC TYPES>
 186   <FUNC TYPES TAIL>  ::=   ... EMPTY PRODUCTION ...
 187   <PROC TYPES>  ::=   ( <PARAM TYPES> ) <PROC TYPES TAIL>
 188   <PROC TYPES TAIL>  ::=   , <PROC TYPES>
 189   <PROC TYPES TAIL>  ::=   ... EMPTY PRODUCTION ...
 190   <CLOSE>  ::=   ... EMPTY PRODUCTION ...
 191   <TYPE IDENTIFIER>  ::=   <IDENTIFIER>
 192   <RECORD ENDING>  ::=   END
 193   <DUMMY FOR STAT>  ::=   <FOR STAT 2>
 194   <DUMMY FOR STAT>  ::=   ... EMPTY PRODUCTION ...
 195   <DUMMY ENDING>  ::=   END
 196   <DUMMY ENDING>  ::=   ... EMPTY PRODUCTION ...
 197   <DUMMYBLOCK>  ::=   <BLOCK>
 198   <DUMMYBLOCK>  ::=   ... EMPTY PRODUCTION ...
 199   <SEMICOLON1>  ::=   ;
 200   <DUMMY FIELD>  ::=   <TYPE> <NEXT FIELD>
 201   <DUMMY STATEMENT TAIL>  ::=   OF <CASE LIST> END
 202   <DUMMY STATEMENT TAIL>  ::=   DO <STATEMENT>
 203   <DUMMY STATEMENT TAIL>  ::=   <FOR STAT 2>
 204   <DUMMY STATEMENT TAIL>  ::=   ... EMPTY PRODUCTION ...
 205   <DUMMY VAR DECL BODY>  ::=   : <TYPE> <VAR DECL TAIL>
 206   <DUMMY TYPE BODY>  ::=   <TYPE DESCRIPTOR PART>
 207   <DUMMY TYPE BODY>  ::=   <TYPE> <TYPE DEFN TAIL>
 208   <ASSIGNMENT     PART>  ::=   <QUALIFIER> := <EXPRESSION>
 209   <ASSIGNMENT     PART>  ::=   ... EMPTY PRODUCTION ...
 210   <INDEX  AND  ELEMENTS  TYPE>  ::= (. <SIMPLE TYPE> <SIMPLE TYPE LIST> .) OF <TYPE>
 211   <INDEX  AND  ELEMENTS  TYPE>  ::= ( <SIMPLE TYPE> <SIMPLE TYPE LIST> ) OF <TYPE>
 212   <DUMMY STATEMENT TAIL>  ::=   THEN <STATEMENT> <ELSE PART>
