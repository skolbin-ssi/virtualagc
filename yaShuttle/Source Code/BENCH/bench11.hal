 ACOSD: PROGRAM;
  REPLACE ITER_CNT BY "300";
  REPLACE PREC BY "DOUBLE";
C
C VARIABLES FOR RANDOM NUMBER GENERATOR
C
  DECLARE RM INTEGER DOUBLE INITIAL(2**20);
  DECLARE FM SCALAR DOUBLE CONSTANT(2**20);
  DECLARE RA INTEGER DOUBLE CONSTANT(2**10 + 3);
  DECLARE RX INTEGER DOUBLE INITIAL(566387);
C
C GLOBAL VARIABLES:
C
  DECLARE PI SCALAR PREC CONSTANT(3.1415926535897932);
  DECLARE TOLS ARRAY(2) SCALAR PREC INITIAL(5.24E-11,5.65E-11);
  DECLARE MAXERR ARRAY(2) SCALAR PREC INITIAL(0);
  DECLARE TEST_DEFS ARRAY(2) CHARACTER(10) INITIAL(
    'ACOS      ',
    'ASIN      ');
  DECLARE INTEGER SINGLE, FAIL_CTR INITIAL(0), TEST_NDX, TEST_NUM;
  DECLARE SCALAR PREC, ARG, VAL1, VAL2;
C
C ASIN = XASIN
C
 XASIN: FUNCTION (Y) SCALAR PREC;
  DECLARE Y SCALAR PREC;
  DECLARE C1 SCALAR PREC CONSTANT(  .00587162904063511),
          C2 SCALAR PREC CONSTANT(-4.44110670602864049),
          C3 SCALAR PREC CONSTANT(-3.64565146031194167),
          C4 SCALAR PREC CONSTANT(-1.74882357832528117),
          C5 SCALAR PREC CONSTANT(-1.19625261960154476),
          D1 SCALAR PREC CONSTANT(- .49961647241138661),
          D2 SCALAR PREC CONSTANT(-4.53770940160639666),
          D3 SCALAR PREC CONSTANT(- .41896233680025977),
          D4 SCALAR PREC CONSTANT(- .03737027365107758);
  DECLARE BOOLEAN, BOOL1, BOOL2;
  DECLARE SCALAR PREC, TEMP, X, XSQ;
  X = Y;
  IF X < 0 THEN
    DO;
      BOOL1 = TRUE;
      X = -X;
    END;
  ELSE
    BOOL1 = FALSE;
  IF X > .5 THEN
    DO;
      BOOL2 = TRUE;
      XSQ = (1-X)/2;
      X = SQRT(2(1-X));
    END;
  ELSE
    DO;
      BOOL2 = FALSE;
      XSQ = X**2;
    END;
  TEMP = X + XSQ X(C1 + D1/(XSQ + C2 + D2/(XSQ + C3 + D3/(XSQ + C4 +
    D4/(XSQ + C5)))));
  IF BOOL2 THEN
    TEMP = PI/2 - TEMP;
  IF BOOL1 THEN
    TEMP = -TEMP;
  RETURN TEMP;
  CLOSE XASIN;
C
C ACOS = XACOS
C
 XACOS: FUNCTION (Y) SCALAR PREC;
  DECLARE Y SCALAR PREC;
  DECLARE SCALAR PREC, TEMP, X;
  TEMP = XASIN(Y);
  X = PI/2 - TEMP;
  RETURN X;
  CLOSE XACOS;
C
 CHECK_VAL: PROCEDURE(PARMS, VALUE1, VALUE2);
  DECLARE SCALAR PREC, PARMS, VALUE1, VALUE2;
  DECLARE SCALAR PREC, ERRS, ABSERR;
  ERRS = VALUE1 - VALUE2;
  ABSERR = ABS(ERRS);
  IF ABSERR > MAXERR$(TEST_NDX) THEN
    MAXERR$(TEST_NDX) = ABSERR;
  IF ERRS > TOLS$(TEST_NDX) THEN
    DO;
      FAIL_CTR = FAIL_CTR + 1;
      WRITE(6) TEST_DEFS$(TEST_NDX:), PARMS, VALUE1, VALUE2, ERRS;
    END;
 CLOSE CHECK_VAL;
C
 RANDFUNC: FUNCTION (LOWLIM, HIGHLIM) SCALAR DOUBLE;
  DECLARE SCALAR DOUBLE, LOWLIM, HIGHLIM;
  DECLARE SCALAR DOUBLE, TEMP;
  RX = MOD(RA RX,RM);
  TEMP = RX/FM;
  RETURN (1 - TEMP) LOWLIM + TEMP HIGHLIM;
  CLOSE RANDFUNC;
C
C TEST THE ACOS ROUTINE - FIRST, FOR EXPLICIT VALUES
C
  WRITE(6) '*** BENC11 TEST INITIATED ***';
C
  TEST_NDX = 1;
  ARG = -1;
  VAL1 = XACOS(ARG);
  VAL2 = ARCCOS(ARG);
  CALL CHECK_VAL(ARG, VAL1, VAL2);
  ARG = 0;
  VAL1 = XACOS(ARG);
  VAL2 = ARCCOS(ARG);
  CALL CHECK_VAL(ARG, VAL1, VAL2);
  ARG = .5;
  VAL1 = XACOS(ARG);
  VAL2 = ARCCOS(ARG);
  CALL CHECK_VAL(ARG, VAL1, VAL2);
  ARG = 1;
  VAL1 = XACOS(ARG);
  VAL2 = ARCCOS(ARG);
  CALL CHECK_VAL(ARG, VAL1, VAL2);
  DO FOR TEST_NUM = 1 TO ITER_CNT;
    ARG = RANDFUNC(-1,1);
    VAL1 = XACOS(ARG);
    VAL2 = ARCCOS(ARG);
    CALL CHECK_VAL(ARG, VAL1, VAL2);
  END;
C
C TEST THE ASIN ROUTINE - FIRST, FOR EXPLICIT VALUES
C
  TEST_NDX = 2;
  ARG = -1;
  VAL1 = XASIN(ARG);
  VAL2 = ARCSIN(ARG);
  CALL CHECK_VAL(ARG, VAL1, VAL2);
  ARG = 0;
  VAL1 = XASIN(ARG);
  VAL2 = ARCSIN(ARG);
  CALL CHECK_VAL(ARG, VAL1, VAL2);
  ARG = .5;
  VAL1 = XASIN(ARG);
  VAL2 = ARCSIN(ARG);
  CALL CHECK_VAL(ARG, VAL1, VAL2);
  ARG = 1;
  VAL1 = XASIN(ARG);
  VAL2 = ARCSIN(ARG);
  CALL CHECK_VAL(ARG, VAL1, VAL2);
  DO FOR TEST_NUM = 1 TO ITER_CNT;
    ARG = RANDFUNC(-1,1);
    VAL1 = XASIN(ARG);
    VAL2 = ARCSIN(ARG);
    CALL CHECK_VAL(ARG, VAL1, VAL2);
  END;
C
C WE ARE DONE WITH THE TEST: SUMMARIZE THE RESULTS
C
  WRITE(6) '*** BENCH11 TEST COMPLETED ***';
  WRITE(6) 'TEST DEFN.','MAX ERROR    ','TOLERANCE';
  DO FOR TEMPORARY I = 1 TO 2;
    WRITE(6) TEST_DEFS$(I:), MAXERR$(I), TOLS$(I);
  END;
  IF FAIL_CTR = 0 THEN
    DO;
      WRITE(6) 'TEST SUCCESSFUL -- ALL RESULTS WITHIN TOLERANCE';
    END;
  ELSE
    DO;
      WRITE(6) '*** TEST FAILURE --',FAIL_CTR,
        'VALUES WERE OUTSIDE OF TOLERANCE';
    END;
  CLOSE ACOSD;
