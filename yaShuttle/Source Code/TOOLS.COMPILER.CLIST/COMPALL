00001000/********************************************************************/
00002000/*------------------------------------------------------------------*/
00003000/*                                                                  */
00004000/*   NAME    : COMPALL                                              */
00005000/*                                                                  */
00006000/*------------------------------------------------------------------*/
00007000/*                                                                  */
00008003/*   FUNCTION:   A CLIST TO COMPARE THE OBJECT CODE AND EXECUTION   */
00009003/*               RESULT FOR ALL MEMBER OF TWO PDS. THE TWO PDS WERE */
00009103/*               GENERATED BY TWO VERSIONS OF THE PASS OR BFS       */
00009203/*               COMPILER. FIRST, THE OBJECT CODE IS COMPARED BY    */
00009303/*               EXECUTING FBYTECMP CLIST. IF ANY OBJECT CODE IS    */
00009403/*               DIFFERENT, THEN THE COMPFC CLIST IS INVOKED TO     */
00009503/*               COMPARE THE ASSEMBLY LISTINGS. FINALLY,THE SUPERC  */
00009714/*               CLIST IS CALLED TO COMPARE THE EXECUTION RESULTS,  */
00009803/*               SIM FILE AND ERROR MESSAGES PDS (LISTXPDS).        */
00009903/*               THIS CLIST IS DESIGNED TO RUN ON A FUNCTIONAL TEST */
00010003/*               CATEGORY OR BFS FSW.                               */
00010004/*                                                                  */
00010103/*             EX: COMPALL ARRAY REL(R26V0) OLDREL(R25V1)           */
00010203/*                 COMPALL BFS  REL(REL10V0) OLDREL(REL9V1)         */
00010300/*                                                                  */
00010400/*------------------------------------------------------------------*/
00010500/*                                                                  */
00010600/*   SYNTAX :                                                       */
00010700/*     COMPALL 'CAT'  ID(&SYSUID.) REL(REL99V0)                     */
00010800/*              LIB(&ID..&REL..&CAT.) OLDREL(CURRENT)               */
00010900/*              OLDLIB(&ID..&OLDREL..&CAT.) BFSOI(BFS253XX)         */
00011000/*              TRACE REFRESH LIST() CMPFCMEM() NOOBJ OBJ SIM       */
00011100/*                                                                  */
00011200/*     REQUIRED - 'CAT'                                             */
00011300/*                                                                  */
00011400/*------------------------------------------------------------------*/
00011500/*                                                                  */
00011600/*   INVOKES:   PDSALL                                              */
00011700/*              FBYTECMP                                            */
00011800/*              COMPFC                                              */
00011903/*              SUPERC                                              */
00012000/*                                                                  */
00012100/*------------------------------------------------------------------*/
00012200/*                                                                  */
00012300/*   PARAMETERS:                                                    */
00012400/*                                                                  */
00012500/*     CAT         REQUIRED                                         */
00013003/*                 FUNCTIONAL CATEGORY NAME OR                      */
00013100/*                 BFS FOR BFS FSW COMPARISON                       */
00013200/*                                                                  */
00013300/*     ID          DEFAULT: &SYSUID                                 */
00013400/*                 HIGH LEVEL QUALIFIER                             */
00013503/*                                                                  */
00013603/*     REL         DEFUALT: REL99V0                                 */
00013703/*                 COMPILER RELEASE                                 */
00013803/*                                                                  */
00013903/*     OLDREL      DEFUALT: CURRENT                                 */
00014003/*                 CURRENT COMPILER RELEASE                         */
00014200/*                                                                  */
00014303/*     LIB         DEFAULT: &ID..&REL..&CAT                         */
00014403/*                 DATA SET WHERE THE MEMBERS RESIDE FOR NEW RELEASE*/
00014500/*                                                                  */
00014603/*     OLDLIB      DEFAULT: &ID..&OLDREL..&CAT                      */
00014703/*                 DATA SET WHERE THE MEMBERS RESIDE FOR OLD RELEASE*/
00014803/*                                                                  */
00014900/*     LIST        DEFAULT: NULL                                    */
00015000/*                 MEMBER LIST ALREADY EXISTS, USE FILE SPECIFIED   */
00015100/*                                                                  */
00015110/*     CMPFCMEM    DEFAULT: NULL                                    */
00015120/*                 DATA SET CONTAINS MEMBERS TO RUN COMPFC UPON.    */
00015121/*                 A USER SPECIFIED MEMBER LIST USED TO RUN COMPFC  */
00015130/*                 FOR BFS                                          */
00015140/*                                                                  */
00015200/*     BFSOI       DEFAULT: BFS253XX                                */
00015300/*                 BFS FSW OI VERSION                               */
00015301/*                                                                  */
00015310/*     NOOBJ       OPTIONAL                                         */
00015320/*                 OPTION TO RUN ALL COMPARISONS EXCEPT OBJECT CODE */
00015330/*                                                                  */
00015340/*     OBJ         OPTIONAL                                         */
00015350/*                 OPTION TO ONLY RUN THE COMPARISON ON OBJECT CODE */
00015360/*                                                                  */
00015370/*     SIM         OPTIONAL                                         */
00015380/*                 OPTION TO RUN COMPARISON ON .SIM FILES           */
00015400/*                                                                  */
00015502/*     REFRESH     OPTIONAL                                         */
00015603/*                 REFRESH THE REPORT FILES, .COMPLST AND .OUTLST   */
00015702/*                                                                  */
00016000/*     TRACE       OPTIONAL                                         */
00016100/*                 TRACE THE CLISTS EXECUTION                       */
00016200/*                                                                  */
00016300/*------------------------------------------------------------------*/
00016400/*                                                                  */
00016500/*   INPUT DATA SET NAMES:                                          */
00016600/*                                                                  */
00016714/*     <OLDLIB>.OBJ           <LIB>.OBJ                             */
00016914/*     <OLDLIB>.LST           <LIB>.LST                             */
00017114/*     <OLDLIB>.OUT           <LIB>.OUT                             */
00017214/*     <OLDLIB>.LISTXPDS      <LIB>.LISTXPDS                        */
00017300/*     <OLDLIB>.SIM           <LIB>.SIM                             */
00017310/*                                                                  */
00017400/*------------------------------------------------------------------*/
00017500/*                                                                  */
00017600/*   REVISION HISTORY:                                              */
00017700/*                                                                  */
00017800/*  DATE      PCR#     NAME   DESCRIPTION                           */
00017900/*  --------  -----    ----   -------------------------------       */
00018003/*  05/28/94  334      LJK    BASELINED OBJECT COMPARISON CLIST     */
00019000/*  06/16/94  366      LJK    ADDED FICOM, NOFICOM & SIM PARAMETERS */
00019100/*  09/25/94  358      LJK    CHNGED TO USE SUPERC INSTEAD OF FICOM */
00019200/*                            ADDED COMPRESSION OF FILES            */
00019300/*  05/23/95  435      LJK    ADDED DELDD FOR ALL SUPERC INVOCATION */
00019400/*                            RENAMED COMPFC.MEMLIST TO COMPFC.MLIST*/
00020000/*------------------------------------------------------------------*/
00021000/********************************************************************/
00021100 
00021200PROC  1   CAT                               +
00021300          SRCDSN(NCAMCM.TESTLIB.&CAT..HAL)  +
00021400          ID(&SYSUID.)                      +
00021503          REL(REL99V0)                      +
00021603          OLDREL(CURRENT)                   +
00021700          LIB(&ID..&REL..&CAT.)             +
00021800          OLDLIB(&ID..&OLDREL..&CAT.)       +
00021900          BFSOI(BFS253XX)                   +
00022100          REFRESH                           +
00022200          NOOBJ                             +
00022210          OBJ                               +
00022220          SIM                               +
00022300          LIST()                            +
00022310          CMPFCMEM()                        +
00022400          TRACE
00022500 
00022600GLOBAL RETCODE
00022700IF &TRACE.  NE THEN +
00022800   CONTROL  LIST CONLIST
00022900         /***************************************/
00023000         /*                                     */
00024000         /*        GENERATE MEMBER LIST         */
00025000         /*                                     */
00026000         /***************************************/
00027000CONTROL  NOMSG END(DONE)
00029000IF &CAT = BFS THEN DO
00030000   SET &LIB = &ID..&BFSOI..&REL.
00040000   SET &OLDLIB = &ID..&BFSOI..&OLDREL.
00050000   SET &SRCDSN = NROKSTB.&BFSOI..CODEHAL
00060100DONE
00060200IF &LIST.    NE THEN SET &LIST1 = &LIST.
00060300IF &REFRESH. NE THEN SET &RERUN = RERUN
00060310IF &NOOBJ. NE THEN GOTO CMP2
00060400IF &LIST EQ THEN  DO
00060500    PDSALL &LIB..OBJ LIB(&LIB.) MAKELIST
00060600    SET &LIST =  &LIB..MEMLIST
00060700DONE
00060800         /***************************************/
00060900         /*                                     */
00061000         /*    ON ATTN, DO THE FOLLOWING:       */
00062000         /*                                     */
00063000         /***************************************/
00064000ATTN     DO
00065000         ERROR    OFF
00066000         CLOSFILE MEMBERS
00067000         FREE     F(MEMBERS)
00068000         EXIT
00069000DONE
00070000 
00080000CONTROL  NOMSG
00090000         FREE     F(MEMBERS)
00100000CONTROL  MSG NOFLUSH
00160000ALLOC    F(MEMBERS)  DA('&LIST.') SHR
00170000OPENFILE MEMBERS  INPUT
00190000         /***************************************/
00200000         /*                                     */
00210000         /* ON ERROR, DO THE FOLLOWING:         */
00220000         /*                                     */
00230000         /***************************************/
00240000ERROR DO
00250000   CLOSFILE MEMBERS
00260000   FREE     F(MEMBERS)
00270000   ERROR OFF
00280000   GOTO CMP2
00290000DONE
00300000         /***************************************/
00310000         /*                                     */
00320016         /*   RUN BYTECOMP ON THE PDS.          */
00330000         /*                                     */
00340000         /***************************************/
00350000IF &RERUN = RERUN THEN SET &REFRESH = REFRESH
00360000                  ELSE SET &REFRESH =
00370000DO   WHILE    &SYSUID NE
00380000     GETFILE  MEMBERS
00390000     SET &MEMLEN = 1
00400000     DO WHILE &SUBSTR(&MEMLEN.:&MEMLEN.,&MEMBERS.) NE
00410000        SET &MEMLEN = &MEMLEN + 1
00420000     DONE
00430000     SET &MEMLEN = &MEMLEN - 1
00440000     SET &NAME = &SUBSTR(1:&MEMLEN.,&MEMBERS.)
00460000     IF &SYSDSN('&LIB..OBJ(&NAME.)') EQ OK THEN DO
00470000       IF &SYSDSN('&OLDLIB..OBJ(&NAME.)') EQ OK THEN DO
00480000          WRITE ** EXECUTION OF BYTECOMP FOR &NAME. NEW/OLD.OBJ **
00481004 
00490001          FBYTECMP &NAME.                       +
00500001                   LIB(&LIB.)                   +
00510001                   IN1(&OLDLIB..OBJ(&NAME.))    +
00520001                   IN2(&LIB..OBJ(&NAME.))       +
00520101                   GENLIST NOUTPUT              +
00521001                   &REFRESH.                    +
00522001                   &TRACE
00530000          SET &REFRESH =
00540000       DONE
00550000       ELSE WRITE COMPARES NOT EXECUTED, &OLDLIB..OBJ(&NAME.) MISSING
00560000     DONE
00570000     ELSE WRITE COMPARES NOT EXECUTED, &LIB..OBJ(&NAME.) MISSING
00580000DONE
00590000 
00600000CMP2: CONTROL NOMSG
00600100IF &CMPFCMEM. NE THEN SET &LIST = &CMPFCMEM.
00620000                 ELSE SET &LIST = &LIST1.
00630000IF &LIST EQ THEN  DO
00640000    PDSALL &SRCDSN.  LIB(&LIB.) MAKELIST
00650000    SET &LIST =  &LIB..MEMLIST
00660000DONE
00660100IF &NOOBJ. NE THEN GOTO CMP3
00660200IF &SYSDSN('&LIB..COMPFC.MLIST') NE OK THEN GOTO CMP3
00670000CONTROL  MSG
00680000ALLOC    F(MEMBERS)  DA('&LIST.') SHR
00690000OPENFILE MEMBERS  INPUT
00700000 
00710000ERROR DO
00720000    CLOSFILE MEMBERS
00730000    FREE     F(MEMBERS)
00740000    ERROR OFF
00750000    GOTO CMP3
00760000DONE
00770000          /***************************************/
00780000          /*                                     */
00790000          /*   RUN COMPFC ON THE PDS             */
00800000          /*                                     */
00810000          /***************************************/
00820000IF &RERUN = RERUN THEN SET &REFRESH = REFRESH
00830000                  ELSE SET &REFRESH =
00840000 DO  WHILE    &SYSUID NE
00850000     GETFILE  MEMBERS
00860000     SET &MEMLEN = 1
00870000     DO WHILE &SUBSTR(&MEMLEN.:&MEMLEN.,&MEMBERS.) NE
00880000        SET &MEMLEN = &MEMLEN + 1
00890000     DONE
00890100     SET &MEMLEN = &MEMLEN - 1
00890200     SET &NAME = &SUBSTR(1:&MEMLEN.,&MEMBERS.)
00890400     IF &SYSDSN('&LIB..LST(&NAME.)') EQ OK THEN DO
00890500       IF &SYSDSN('&OLDLIB..LST(&NAME.)') EQ OK THEN DO
00890600          WRITE ** EXECUTION OF COMPFC FOR &NAME. NEW/OLD.LST **
00890704 
00890800          COMPFC &NAME.                     +
00890900                 LIB(&LIB.)                 +
00891000                 ID(&ID.)                   +
00891100                 NEWREL(&REL.)              +
00891200                 IN1(&OLDLIB..LST(&NAME.))  +
00891300                 IN2(&LIB..LST(&NAME.))     +
00891400                 NODELETES                  +
00891500                 &REFRESH.                  +
00891600                 &TRACE.
00891700          SET &REFRESH =
00891800       DONE
00891900       ELSE WRITE COMPARES NOT EXECUTED, &OLDLIB..LST(&NAME.) MISSING
00892000     DONE
00892100     ELSE WRITE COMPARES NOT EXECUTED, &LIB..LST(&NAME.) MISSING
00893000 DONE
00893100 
00893200CMP3: CONTROL NOMSG
00893300IF &CAT EQ BFS   THEN GOTO FINISH
00893400IF &OBJ. NE THEN GOTO FINISH
00893417IF &RERUN = RERUN THEN DO
00893419    DELETE '&LIB..OUTLST'
00893420    DELETE '&LIB..ERRLST'
00893421    DELETE '&LIB..SIMLST'
00893430DONE
00893470IF &SYSDSN('&LIB..OUTLST') NE OK THEN                              +
00893480   ALLOC DA('&LIB..OUTLST') DSORG(PO) SPACE(1,1) CYLINDERS DIR(25) +
00893490         LRECL(133) BLKSIZE(6384) RECFM(F B) RELEASE REUSE
00893491IF &SYSDSN('&LIB..ERRLST') NE OK THEN                              +
00893492   ALLOC DA('&LIB..ERRLST') DSORG(PO) SPACE(1,1) CYLINDERS DIR(25) +
00893493         LRECL(133) BLKSIZE(6384) RECFM(F B) RELEASE REUSE
00893500IF &SIM. NE AND &SYSDSN('&LIB..SIMLST') NE OK THEN                 +
00893600   ALLOC DA('&LIB..SIMLST') DSORG(PO) SPACE(1,1) CYLINDERS DIR(25) +
00893700         LRECL(133) BLKSIZE(6384) RECFM(F B) RELEASE REUSE
00893900CONTROL  MSG
00894000ALLOC    F(MEMBERS)  DA('&LIST.') SHR
00894100OPENFILE MEMBERS  INPUT
00894200ERROR DO
00894300    CLOSFILE MEMBERS
00894400    FREE     F(MEMBERS)
00894500    ERROR OFF
00894600    GOTO FINISH
00894700DONE
00894803          /***************************************/
00894903          /*                                     */
00895003          /*   RUN SUPERC ON THE PDS             */
00895103          /*                                     */
00895203          /***************************************/
00895204SET &NEWRUNBT = 0
00895300IF &RERUN = RERUN THEN SET &REFRESH = REFRESH
00895400                  ELSE SET &REFRESH =
00895600 DO  WHILE    &SYSUID NE
00895700     GETFILE  MEMBERS
00895800     SET &MEMLEN = 1
00895900     DO WHILE &SUBSTR(&MEMLEN.:&MEMLEN.,&MEMBERS.) NE
00896000        SET &MEMLEN = &MEMLEN + 1
00896100     DONE
00896200     SET &MEMLEN = &MEMLEN - 1
00896300     SET &NAME = &SUBSTR(1:&MEMLEN.,&MEMBERS.)
00896400     IF &SYSDSN('&LIB..OUT(&NAME.)') EQ OK THEN DO
00896500       IF &SYSDSN('&OLDLIB..OUT(&NAME.)') EQ OK THEN DO
00896606           WRITE ** EXECUTION OF SUPERC FOR &NAME. NEW/OLD.OUT **
00896800           SUPERC &NAME.                          +
00896900                  LSTLIB(&LIB.)                   +
00897000                  OLDFILE(&OLDLIB..OUT(&NAME.))   +
00897100                  NEWFILE(&LIB..OUT(&NAME.))      +
00897207                  OUTDD(&LIB..OUTLST(&NAME.))     +
00897208                  DELDD(&LIB..SUPERC.UPDT)        +
00897300                  &TRACE. &REFRESH. REPORT
00897400           SET &REFRESH =
00897512       DONE
00897612       ELSE WRITE COMPARES NOT EXECUTED, &OLDLIB..OUT(&NAME.) MISSING
00897712     DONE
00897812     ELSE WRITE COMPARES NOT EXECUTED, &LIB..OUT(&NAME.) MISSING
00897912 
00898012     IF &SYSDSN('&LIB..LISTXPDS(&NAME.)') EQ OK THEN DO
00898112       IF &SYSDSN('&OLDLIB..LISTXPDS(&NAME.)') EQ OK THEN DO
00898212           WRITE ** EXECUTION OF SUPERC FOR &NAME. NEW/OLD.LISTXPDS **
00898412           SUPERC &NAME.                             +
00898512                  LSTLIB(&LIB.)                      +
00898612                  OLDFILE(&OLDLIB..LISTXPDS(&NAME.)) +
00898712                  NEWFILE(&LIB..LISTXPDS(&NAME.))    +
00898813                  OUTDD(&LIB..ERRLST(&NAME.))        +
00898814                  DELDD(&LIB..SUPERC.UPDT)           +
00898911                  &TRACE. REPORT
00899000       DONE
00899112      ELSE WRITE COMPARE NOT EXECUTED,&OLDLIB..LISTXPDS(&NAME.) MISSING
00899200     DONE
00899312     ELSE WRITE COMPARES NOT EXECUTED, &LIB..LISTXPDS(&NAME.) MISSING
00899313 
00899314     IF &SIM. NE THEN DO
00899315        IF &SYSDSN('&LIB..SIM(&NAME.)') EQ OK THEN DO
00899316          IF &SYSDSN('&OLDLIB..SIM(&NAME.)') EQ OK THEN DO
00899317             WRITE ** EXECUTION OF SUPERC FOR &NAME. NEW/OLD.SIM **
00899318             SUPERC &NAME.                           +
00899319                    LSTLIB(&LIB.)                    +
00899320                    OLDFILE(&OLDLIB..SIM(&NAME.))    +
00899321                    NEWFILE(&LIB..SIM(&NAME.))       +
00899322                    OUTDD(&LIB..SIMLST(&NAME.))      +
00899323                    DELDD(&LIB..SUPERC.UPDT)         +
00899324                    &TRACE. REPORT
00899325          DONE
00899326          ELSE WRITE COMPARE NOT EXECUTED,&OLDLIB..SIM(&NAME.) MISSING
00899327        DONE
00899328        ELSE WRITE COMPARES NOT EXECUTED, &LIB..SIM(&NAME.) MISSING
00899330     DONE
00899340 
00899341     SET &NEWRUNBT  = &NEWRUNBT.  +  1
00899346     IF &NEWRUNBT. = 5  THEN DO
00899347        COMPRESS '&LIB..OUTLST'
00899348        COMPRESS '&LIB..ERRLST'
00899349        IF &SIM. NE THEN +
00899350           COMPRESS '&LIB..SIMLST'
00899354        SET &NEWRUNBT  =  1
00899360     DONE
00899390 
00899400 DONE
00899500 FINISH:CONTROL NOMSG
00899510 DELETE '&LIB..SUPERC.UPDT'
00899520 CONTROL MSG
00899600 WRITE *****************************************
00899700 WRITE *** COMPARISON OF &CAT. COMPLETED
00900000 WRITE *****************************************
