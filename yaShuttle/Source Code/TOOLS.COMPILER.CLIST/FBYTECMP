00010000/********************************************************************/
00020000/*------------------------------------------------------------------*/
00030000/*                                                                  */
00040000/*   NAME:     FBYTECMP                                             */
00050000/*                                                                  */
00060000/*------------------------------------------------------------------*/
00070000/*                                                                  */
00080000/*   FUNCTION: THIS ROUITNE IS THE CLIST INTERFACE ROUTINE FOR THE  */
00090000/*             UTILITY BY THE SAME NAME.  THE FBYTECMP UTILITY      */
00100000/*             (CLIST AND LOAD MODULE) WILL COMPARE TWO OBJECT      */
00110000/*             MODULES, GENERATED BY EITHER THE PASS OR BFS         */
00120000/*             COMPILERS, AND INDICATE WHETHER OR NOT THERE ARE ANY */
00130000/*             DIFFERENCES BETWEEN THEM.  THE COMPARE ROUTINE       */
00140000/*             ALLOWS FOR THE EXISTENCE OF DATE AND TIME STAMPS IN  */
00150000/*             THE OBJECT CODE AND ALLOWS FOR THE EXISTENCE OF      */
00160000/*             COMPILER VERSION NUMBERS.                            */
00170000/*                                                                  */
00171000/*             THIS IS A VERSION OF BYTECOMP SPECIFICALLY INVOKED   */
00172000/*             BY FSWCMP FOR FSW OBJECT COMPARISION. THE CLIST IS   */
00173000/*             UPDATED SO THAT THE .OBJ WON'T BE DELETED AFTER      */
00174000/*             THE COMPARISON. AND &IN2 PARAMETER IS HARDCODED USING */
00175000/*             AN EXISTING DATASET WHICH CONTAINS THE OLD OBJECT.   */
00176000/*                                                                  */
00177000/*------------------------------------------------------------------*/
00178000/*                                                                  */
00179000/*   SYNTAX :                                                       */
00180000/*     FBYTECMP 'NAME1' NAME2(&NAME1.) ID(&SYSUID.) LIB(&ID..TEST)  */
00190000/*              IN1(&LIB..OBJ(&NAME1.)) IN2(&LIB..OBJ(&NAME2.))     */
00200000/*              NOUTPUT REFRESH DELOBJ GENLIST TRACE                */
00210000/*                                                                  */
00220000/*     ALIAS    - NONE.                                             */
00230000/*     REQUIRED - NONE.                                             */
00240000/*                                                                  */
00250000/*------------------------------------------------------------------*/
00260000/*                                                                  */
00270000/*   INVOKES:  NCAMCM.TOOLS.HALLOAD(BYTECOMP)                       */
00280000/*                                                                  */
00290000/*------------------------------------------------------------------*/
00300000/*                                                                  */
00310000/*   PARAMETERS:                                                    */
00320000/*                                                                  */
00330000/*     NAME1    REQUIRED                                            */
00340000/*              NAME1 IS THE MEMBER NAME OF THE OBJECT MODULE TO BE */
00350000/*              COMPARED.                                           */
00360000/*                                                                  */
00370000/*     NAME2    DEFAULT: &NAME1                                     */
00380000/*              NAME2 IS THE MEMBER NAME OF THE OBJECT MODULE TO BE */
00390000/*              COMPARED AGAINST.  IF THIS PARM IS NOT USED THE     */
00400000/*              CLIST ASSUMES THE OBJECT MODULE TO BE COMPARED ARE  */
00410000/*              IN SEPARATE DATA SETS.                              */
00420000/*                                                                  */
00430000/*     ID       DEFAULT: &SYSUID.                                   */
00440000/*              THE USERS HIGH LEVEL QUALIFIER. NOT GENERALLY       */
00450000/*              NEEDED.                                             */
00460000/*                                                                  */
00470000/*     LIB      DEFAULT: &ID..TEST                                  */
00480000/*              IF THE FILES TO BE COMPARED ARE NOT IN DATA SETS    */
00490000/*              WITH THE FORM <HIGH LEVEL QUAL>.TEST.OBJ(..) THEN   */
00500000/*              LIB SHOULD BE SPECIFIED. IF SPECIFIED THE HLQ AND   */
00510000/*              TEST QUALIFIERS WILL BE REPLACED.  THE LIB SHOULD   */
00520000/*              ALWAYS INCLUDE THE HIGH LEVEL QUALIFIER.            */
00530000/*                                                                  */
00540000/*     IN1      DEFAULT: &LIB..OBJ(&NAME1.)                         */
00550000/*              THIS IS THE COMPLETE NAME OF THE FIRST DATA SET TO  */
00560000/*              BE COMPARED. THIS PARM SHOULD ONLY BE SUPPLIED WHEN */
00570000/*              THE OBJECT MODULES TO BE COMPARED ARE NOT IN DATA   */
00580000/*              SETS WITH EXPECTED FORMATS.                         */
00590000/*                                                                  */
00600000/*     IN2      DEFAULT: &LIB..OBJ(&NAME2.)                         */
00610000/*              SECOND OBJECT TO BE COMPARED. (SEE IN1)             */
00620000/*                                                                  */
00630000/*     SYSTEM   DEFUALT: PASS                                       */
00640000/*              PASS OR BFS, FOR VERSION SEPARATION PURPOSES        */
00650000/*                                                                  */
00660000/*     NOUTPUT  OPTIONAL                                            */
00670000/*              IF USED THEN ALL SCREEN DIRECTED OUTPUT IS SENT TO A*/
00680000/*              NULL UNIT.                                          */
00690000/*                                                                  */
00700000/*     REFRESH  OPTIONAL                                            */
00710000/*              USED WHEN A USER INVOKES THIS CLIST DIRECTLY TO     */
00720000/*              DELETE AND REALLOCATE THE REPORT FILE AND THE       */
00730000/*              COMPFC MEMBER LIST FILE.  WE ARE STARTING OVER.     */
00740000/*                                                                  */
00750000/*     DELOBJ   OPTIONAL                                            */
00760000/*              NORMALLY USED WHEN CALLED FROM ANOTHER CLIST {FOR   */
00770000/*              AUTOMATED TESTING}.  SPECIFIES THAT THE OBJECT FILES*/
00780000/*              COMPARED SHOULD BE DELETED AT THE CONCLUSION OF THE */
00790000/*              COMPARE IF THE COMPARE WAS GOOD.                    */
00800000/*                                                                  */
00810000/*     GENLIST  OPTIONAL                                            */
00820000/*              NORMALLY USED WHEN CALLED FROM ANOTHER CLIST {FOR   */
00830000/*              AUTOMATED TESTING}.  SPECIFIES THAT THE NAME OF THE */
00840000/*              OBJECT MODULE BEING TESTED SHOULD BE ADDED TO THE   */
00850000/*              MEMBER LIST IF IT FAILS THE TEST.                   */
00860000/*                                                                  */
00870000/*     TRACE    OPTIONAL                                            */
00880000/*              STANDARD TRACE FLAG                                 */
00890000/*                                                                  */
00900000/*------------------------------------------------------------------*/
00910000/*                                                                  */
00920000/*   INPUT DATA SET NAMES:                                          */
00930000/*                                                                  */
00940000/*   <ID>.TEST.OLD.OBJ(<NAME1>)                                     */
00950000/*     THIS IS THE FORM OF THE DATA SET NAME FOR THE FIRST OBJECT   */
00960000/*     TO BE COMPARED WHEN NAME1 AND NAME2 ARE THE SAME.            */
00970000/*     NOTE1: IF THE INPUT PARAMETER NAME1 IS USED AND NAME2 IS NOT */
00980000/*     THE CLIST ASSUMES THAT THE TWO OBJECTS ARE CONTAINED IN      */
00990000/*     SEPARATE DATA SETS.  IF NAME2 IS SPECIFIED THEN THE CLIST    */
01000000/*     ASSUMES THAT THE OBJECTS ARE IN THE SAME DATA SET.  IN THIS  */
01010000/*     CASE THE DSN FORMAT WOULD BE : <ID>.TEST.OBJ(<NAMEX>) WHERE  */
01020000/*     X IS 1 OR 2                                                  */
01030000/*     NOTE2: <LIB> CAN BE PROVIDED TO REPLACE THE <ID>.TEST        */
01040000/*     IN EITHER OF THE ABOVE NAME FORMATS. <LIB> MUST CONTAIN A    */
01050000/*     HIGH LEVEL QUALIFIER.                                        */
01060000/*                                                                  */
01070000/*   <ID>.TEST.NEW.OBJ(<NAME2>)                                     */
01080000/*     DSN FORMAT FOR SECOND OBJECT.  NOTES 1 AND 2 ABOVE APPLY.    */
01090000/*                                                                  */
01100000/*------------------------------------------------------------------*/
01110000/*                                                                  */
01120000/*   OUTPUT DATA SET NAMES:                                         */
01130000/*                                                                  */
01140000/*   <LIB>.BYTCMP.RPT                                               */
01150000/*     THIS IS THE NAME FORMAT FOR THE PHYSICAL SEQUENTIAL DATA SET */
01160000/*     CONTAINING THE REPORT OF THE RESULTS OF THE COMPARE.  THIS   */
01170000/*     IS A CUMULATIVE RECORD OF COMPARES AND IS CLEARED OUT ONLY   */
01180000/*     WHEN THE USER SPECIFIES REFRESH.  IT IS ALLOCATED AUTOMAT-   */
01190000/*     ICALLY IF THE FILE DOES NOT EXIST.                           */
01200000/*                                                                  */
01210000/*   <LIB>.COMPFC.MLIST                                             */
01220000/*     THIS DATA SET IS ALSO A PHYSICAL SEQUENTIAL DATA SET BUT IS  */
01230000/*     USUALLY ONLY USED WHEN THE CLIST IS INVOKED BY ANOTHER CLIST.*/
01240000/*     THIS FILE CONTAINS A LIST OF THE MEMBERS THAT DID NOT HAVE   */
01250000/*     A SUCCESSFUL COMPARE (IE NAME1 OBJECT WAS DIFFERENT FROM     */
01260000/*     NAME2 OBJECT CODE).   IT IS ALLOCATED AUTOMATICALLY IF THE   */
01270000/*     FILE DOES NOT EXIST.                                         */
01280000/*                                                                  */
01290000/*------------------------------------------------------------------*/
01300000/*                                                                  */
01310000/*   TEMPORARY DATA SET NAMES:                                      */
01320000/*                                                                  */
01330000/*     <NAME1>.TEMPLIST                                             */
01340000/*     TEMPORARY DATASET USED TO PASS A COMPLETION CODE FROM THE    */
01350000/*     PROGRAM BYTECOMP TO THE CLIST.  THE HIGH LEVEL QUALIFIER IS  */
01360000/*     ALLOWED TO DEFAULT TO <SYSUID>.                              */
01370000/*                                                                  */
01380000/*------------------------------------------------------------------*/
01390000/*                                                                  */
01400000/*   COMMENTS:                                                      */
01410000/*     THIS CLIST IS INTENDED TO BE USED BY EITHER ANOTHER CLIST OR */
01420000/*     DIRECTLY BY A USER.  WHEN THIS CLIST IS CALLED BY ANOTHER    */
01430000/*     CLIST THEN THE FOLLOWING FORMAT FOR THE CALL IS RECOMMENDED: */
01440000/*                                                                  */
01450000/*       FBYTECMP NAME {LIB(LIB NAME)} NOUTPUT {DELOBJ} {GENLIST}   */
01460000/*                                                                  */
01470000/*     IT IS STRONGLY RECOMMENDED THAT THE NOUTPUT OPTION BE USED   */
01480000/*     WHEN CALLING THIS CLIST FROM ANOTHER CLIST TO SUPPRESS ANY   */
01490000/*     EXTRANEOUS OUTPUT.  THE OPTIONS ENCLOSED IN {} ARE CONSIDERED*/
01500000/*     OPTIONAL IN THIS CASE.                                       */
01510000/*                                                                  */
01520000/*     THE RECOMMENDED FORMAT TO USE WHEN A USER CALLS THIS CLIST   */
01530000/*     DIRECTLY IS :                                                */
01540000/*                                                                  */
01550000/*       FBYTECMP NAME {LIB(LIB NAME)} {REFRESH}                    */
01560000/*                                                                  */
01570000/*     IN THIS INSTANCE ONLY THE MEMBER NAME OF THE OBJECT TO BE    */
01580000/*     COMPARED IS NEEDED.  THE CLIST ASSUMES THAT THE TWO OBJECT   */
01590000/*     FILES HAVE THE SAME NAME BUT APPEAR IN DIFFERENT DATA SETS   */
01600000/*     WITH NAMES OF THE FORM :                                     */
01610000/*                                                                  */
01620000/*       &ID..TEST.OLD.OBJ(&NAME1.) - FOR OLD OBJECT FILE           */
01630000/*       &ID..TEST.NEW.OBJ(&NAME1.) - FOR NEW OBJECT FILE           */
01640000/*                                                                  */
01650000/*     THE REFRESH OPTION, AS STATED EARLIER, CAUSES THE CLIST TO   */
01660000/*     DELETE AND REALLOCATE THE REPORT FILE AND THE COMPFC         */
01670000/*     MEMBER LIST FILE.  THE LIB OPTION WILL REPLACE THE           */
01680000/*        &ID..TEST   IN THE DSN FORMATS WITH WHATEVER IS           */
01690000/*     SPECIFIED WITHIN THE ()'S.  OTHER OPTIONS, SUCH AS IN1 AND   */
01700000/*     IN2, CAN BE USED WHEN A USER INVOCATION IS PERFORMED IF      */
01710000/*     NECESSARY.  OPTIONS OTHER THAN THE ONES LISTED ABOVE CAN BE  */
01720000/*     USED IN A USER INVOCATION BUT IT IS NOT RECOMMEND BECAUSE OF */
01730000/*     THE NUMBER OF PARAMETERS TO BE TYPED IN.  IF YOU MUST USE    */
01740000/*     THESE PARAMETERS IN A USER INVOCATION PLEASE BE SURE TO      */
01750000/*     REVIEW THE CLIST AND BE SURE OF THEIR EFFECTS.               */
01760000/*                                                                  */
01770000/*------------------------------------------------------------------*/
01780000/*                                                                  */
01790000/*   REVISION HISTORY:                                              */
01800000/*                                                                  */
01810000/*     DATE      PCR#  NAME  DESCRIPTION                            */
01820000/*     --------  ----  ----  -------------------------------------  */
01830000/*     10/27/89  064   DJB   CREATED AND PUT INTO CM                */
01840000/*                                                                  */
01850000/*     6/25/90   099   TEV   UPDATED HEADER. ADDED CODE THAT CHECKS */
01860000/*                           THE PREFIX OF THE OBJ FILE FOR A #P,   */
01870000/*                           #C OR $ IF THE BFS SYSTEM IS SPECIFIED.*/
01880000/*                                                                  */
01890000/*     11/15/90  133   SDM   MODIFIED OUTPUT MESSAGES TO USER,      */
01900000/*                           CHANGED PARAMETER INIT TO REFRESH,     */
01910000/*                           DELETED PARAMETER NEWRUN, AND FREED    */
01920000/*                           CHANNEL7 BEFORE EXITING.               */
01930000/*                                                                  */
01940000/*     02/13/91  148   RAH   CHANGED LOCATION OF LOAD MODULE.       */
01950000/*                                                                  */
01960000/*     05/15/91  169   LJK   DONT DELETE OLD.OBJ & HARDCODED &IN1   */
01970000/*                                                                  */
01980000/*     09/13/91  186   LJK   RENAMED CM LOAD MODULE                 */
01990000/*                                                                  */
01991000/*     04/05/94  332   LJK   DO NOT DELETE NEW OBJ AND DELETE THE   */
01992000/*                           HARDCODED &IN2                         */
01992100/*                                                                  */
01993000/*     06/01/94  334   LJK   ONLY ALLOCATE THE .COMPFC.MEMLIST WHEN */
01994000/*                           OBJECT CODE IS DIFFERENT; PRINT THE    */
01995000/*                           STATUS OF COMPARISON TO THE SCREEN     */
01996000/*     07/14/94  366   LJK   RENAME BYTECOMP.RPT TO BYTCMP.RPT      */
01996100/*     03/16/95  337   JCS   CHECK FOR EMPTY INPUT FILES            */
01996110/*     06/30/95  435   LJK   RENAME COMPFC.MEMLIST COMPFC.MLIST     */
02000000/*------------------------------------------------------------------*/
02010000/********************************************************************/
02020000 
02030000PROC 1 NAME1                     +
02040000       NAME2(&NAME1.)            +
02050000       ID(&SYSUID.)              +
02060000       LIB(&ID..TEST)            +
02070000       IN1(&LIB..OBJ(&NAME1.))   +
02080000       IN2(&LIB..OBJ(&NAME2.))   +
02090000       SYSTEM(PASS)              +
02100000       NOUTPUT                   +
02110000       REFRESH                   +
02120000       DELOBJ                    +
02130000       GENLIST                   +
02140000       TRACE
02150000 
02160000IF &TRACE EQ TRACE THEN CONTROL CONLIST LIST
02170000CONTROL END(DONE)
02180000CONTROL NOMSG
02190000  FREE F(CHANNEL4 CHANNEL5 CHANNEL6 CHANNEL7 CHANNEL8)
02200000  FREE ATTR(FBA133)
02210000  ATTR FBA133 RECFM(F B A) LRECL(133) BLKSIZE(6517)
02220000  DEL &NAME1..TEMPLIST
02230000CONTROL MSG
02240000 
02250000IF &REFRESH. NE THEN DO
02260000   CONTROL NOMSG
02270000   ERROR OFF
02280000   FREE DA('&LIB..BYTCMP.RPT')
02290000   DELETE '&LIB..BYTCMP.RPT'
02300000   FREE DA('&LIB..COMPFC.MLIST')
02310000   DELETE '&LIB..COMPFC.MLIST'
02320000   CONTROL MSG
02330000DONE
02340000IF &SYSDSN('&LIB..BYTCMP.RPT') NE OK THEN                       +
02350000   ALLOC DA('&LIB..BYTCMP.RPT') SPACE(1,1) TRACK DSORG(PS)      +
02360000                                RECFM(F B) LRECL(133) BLKSIZE(6517)
02400000 
02410000IF &SYSTEM. EQ &STR(BFS) THEN   +
02420000   IF &NAME1. EQ &NAME2. THEN   DO
02430000      SET &CHCKDSN1  = &STR(&SYSDSN('&LIB..OLD.OBJ(#P&NAME1.)'))
02440000      IF  &CHCKDSN1  = &STR(OK) THEN   +
02450000          SET &NAME1 = &STR(#P&NAME1.)
02460000      ELSE   DO
02470000        SET &CHCKDSN1  = &STR(&SYSDSN('&LIB..OLD.OBJ(#C&NAME1.)'))
02480000        IF  &CHCKDSN1  = &STR(OK) THEN +
02490000            SET &NAME1 = &STR(#C&NAME1.)
02500000        ELSE    +
02510000            SET &NAME1 = &STR($&NAME1.)
02520000      DONE
02530000        SET &IN1  EQ  &STR(&LIB..OLD.OBJ(&NAME1.))
02540000        SET &IN2  EQ  &STR(&LIB..NEW.OBJ(&NAME2.))
02550000   DONE
02560000   ELSE   DO
02570000     SET &CHCKDSN1  = &STR(&SYSDSN('&LIB..OBJ(#P&NAME1.)'))
02580000     SET &CHCKDSN2  = &STR(&SYSDSN('&LIB..OBJ(#P&NAME2.)'))
02590000     IF  &CHCKDSN1  = &STR(OK) AND &CHCKDSN2 = &STR(OK) THEN  DO
02600000         SET &NAME1.= &STR(#P&NAME1.)
02610000         SET &NAME2 = &STR(#P&NAME2.)
02620000     DONE
02630000     ELSE   DO
02640000       SET &CHCKDSN1  = &STR(&SYSDSN('&LIB..OBJ(#C&NAME1.)'))
02650000       SET &CHCKDSN2  = &STR(&SYSDSN('&LIB..OBJ(#C&NAME2.)'))
02660000       IF  &CHCKDSN1  = &STR(OK) AND &CHCKDSN2 = &STR(OK) THEN DO
02670000           SET &NAME1 = &STR(#C&NAME1.)
02680000           SET &NAME2 = &STR(#C&NAME2.)
02690000       DONE
02700000       ELSE   DO
02710000           SET &NAME1 = &STR($&NAME1.)
02720000           SET &NAME2 = &STR($&NAME2.)
02730000       DONE
02740000     DONE
02750000   DONE
02760000/*ELSE + */
02770000/*IF &NAME1. = &NAME2. THEN + */
02780000/*   SET &IN1 = &STR(&LIB..OLD.OBJ(&NAME1.)) */
02790000/*   SET &IN2 = &STR(&LIB..NEW.OBJ(&NAME2.)) */
02800000/*DONE */
02810000 
02820000GLOBAL SAVECC
02830000SET &SAVECC EQ 0
02840000/* IF AN ERROR OCCURS IN THIS PROCEDURE */
02850000/* THEN EXIT                            */
02860000ERROR DO
02870000  FREE F(CHANNEL4 CHANNEL5 CHANNEL6 CHANNEL7 CHANNEL8)
02880000  FREE ATTR(FBA133)
02890000  ATTR FBA133 RECFM(F B A) LRECL(133) BLKSIZE(6517)
02900000  ERROR OFF
02910000  DEL &NAME1..TEMPLIST
02920000  ERROR  EXIT
02930000  EXIT
02940000DONE
02950000 
02960000ALLOC F(CHANNEL4) DA('&IN1.') SHR
02970000ALLOC F(CHANNEL5) DA('&IN2.') SHR
02980000ALLOC F(CHANNEL7) DA(&NAME1..TEMPLIST) RELEASE USING(FBA133) NEW +
02990000  SP(5 0) TRACK
03000000 
03010000IF &NOUTPUT NE THEN DO
03020000   ALLOC F(CHANNEL6) DUMMY
03030000   ALLOC F(CHANNEL8) DUMMY
03040000   DONE
03050000ELSE DO
03060000   ALLOC F(CHANNEL6) DA(*)
03070000   ALLOC F(CHANNEL8) DA(*)
03080000DONE
03090000 
03100000CALL 'NCAMCM.TOOLS.HALLOAD(BYTECOMP)'
03110000 
03120000OPENFILE CHANNEL7 INPUT
03130000GETFILE CHANNEL7
03140000SET SAVECC EQ &CHANNEL7
03150000CLOSFILE CHANNEL7
03160000CONTROL NOMSG
03170000FREE F(CHANNEL4 CHANNEL5 CHANNEL6 CHANNEL7 CHANNEL8)
03180000FREE ATTR(FBA133)
03190000DEL &NAME1..TEMPLIST
03200000CONTROL MSG
03210000 
03220000ALLOC F(REPORT) DA('&LIB..BYTCMP.RPT') MOD
03230000OPENFILE REPORT OUTPUT
03232300 
03232400   /* WRITE TO THE COMPFC.RPT FILE */
03232500 
03240000IF &SAVECC = 1          0 THEN DO
03241000  WRITE ***
03242000  WRITE *** OBJECT CODES ARE THE SAME
03243000  WRITE ***
03250000  SET REPORT = &STR(   OBJECT CODES &IN1. AND &IN2. MATCH)
03251000DONE
03251100ELSE
03251200IF &SAVECC = 1          2 THEN DO
03251300  WRITE ***
03251400  WRITE *** ERROR - &IN1. OR &IN2. IS EMPTY
03251500  WRITE ***
03251600  SET REPORT = &STR(***ERROR - &IN1. OR &IN2. IS EMPTY)
03251700DONE
03260000ELSE
03260100IF &SAVECC = 1          1 THEN DO
03260200  WRITE ***
03260300  WRITE *** OBJECT CODES ARE DIFFERENT
03260400  WRITE ***
03261000  IF &SYSDSN('&LIB..COMPFC.MLIST') NE OK THEN                       +
03262000    ALLOC DA('&LIB..COMPFC.MLIST') SPACE(1,1) TRACK DSORG(PS)       +
03263000                                  RECFM(F B) LRECL(80) BLKSIZE(6160)
03270000  SET REPORT = &STR(***OBJECT CODES &IN1. AND &IN2. ARE DIFFERENT)
03280000  IF &GENLIST NE THEN DO
03290000     ALLOC F(MLIST) DA('&LIB..COMPFC.MLIST') MOD
03300000     OPENFILE MLIST OUTPUT
03310000     SET MLIST = &STR(&NAME1.)
03320000     PUTFILE MLIST
03330000     CLOSFILE MLIST
03340000     FREE F(MLIST)
03350000  DONE
03360000DONE
03370000PUTFILE REPORT
03380000CLOSFILE REPORT
03390000FREE F(REPORT)
03400000 
03410000IF &DELOBJ NE AND &SAVECC = 1          0 THEN DO
03420000   CONTROL NOMSG
03430000   ERROR OFF
03440000   FREE F(CHANNEL4 CHANNEL5)
03450000/* DEL '&IN1.' */
03460000/* DEL '&IN2.' */
03470000   DEL '&LIB..LST(&NAME1.)'
03490000   CONTROL MSG
03500000DONE
