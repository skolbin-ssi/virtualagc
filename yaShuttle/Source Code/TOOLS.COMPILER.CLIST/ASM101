00000100/********************************************************************/
00000200/*------------------------------------------------------------------*/
00000300/*                                                                  */
00000400/*   NAME:     ASM101                                               */
00000500/*                                                                  */
00000600/*------------------------------------------------------------------*/
00000700/*                                                                  */
00000800/*   FUNCTION: ASSEMBLES THE AP101 ASSEMBLY CODE FOR PASS OR BFS.   */
00000900/*             ALSO LINKS FOR PASS. DATASETS ARE DEFAULT TO RUNTIME */
00001000/*             LIBRARY ROUTINES.                                    */
00001010/*                                                                  */
00001100/*------------------------------------------------------------------*/
00001200/*                                                                  */
00001300/*   SYNTAX:                                                        */
00001400/*                                                                  */
00001500/*     ASM101 'NAME'  ID(&SYSUID.) TEMPID(&SYSUID)                  */
00001600/*            REL(REL99V0) LIB(&ID..&SYSTEM..&REL.)                 */
00001610/*            SYSPRINT(&LIB..RUNLST) OBJECT(&LIB..RUNOBJ)           */
00001620/*            RUNLIB(&LIB..RUNLIB)                                  */
00001700/*            SRCDSN(&LIB..RUNASM) TMPLIB(&TMPID..&SYSTEM..&REL.)   */
00001800/*            OPT() LOPT() TRACE PACK() MAP TEMPMAC() SYSTEM(PASS)  */
00001900/*            RUNMAC(NCAMCM.&SYSTEM..CURRENT.RUNMAC) FIRST ASM      */
00002000/*                                                                  */
00002100/*     ALIAS    - NONE.                                             */
00002200/*     REQUIRED - 'NAME'                                            */
00002300/*                                                                  */
00002400/*------------------------------------------------------------------*/
00002500/*                                                                  */
00002600/*   INVOKES:  FOR PASS:                                            */
00002700/*                                                                  */
00002800/*             SRSSOI29.GO.ASM101(ASM101)                           */
00002900/*             NCAMCM.TOOLS.XPLOBJ(FIXOBJ)                          */
00003000/*             SRSSOI29.GO.LNK101(LNK101)                           */
00003100/*                                                                  */
00003200/*             FOR BFS:                                             */
00003300/*                                                                  */
00003400/*             SRSSOI29.GO.ASM101(ASM101)                           */
00003500/*             NROKSTB.SUPPORT.OILOAD.OI283(SATSOBJ)                */
00003600/*             NROKSTB.SUPPORT.OILOAD.OI283(SATSLIST)               */
00003700/*                                                                  */
00003800/*------------------------------------------------------------------*/
00003900/*                                                                  */
00004000/*   PARAMETERS:                                                    */
00004100/*                                                                  */
00004200/*     NAME     REQUIRED                                            */
00004300/*              MEMBER NAME OF THE AP101 ASSEMBLY CODE              */
00004500/*                                                                  */
00004600/*     ID       DEFAULT: &SYSUID                                    */
00004700/*              HIGH LEVEL QUALIFIER                                */
00004800/*                                                                  */
00004900/*     TEMPID   DEFAULT: &SYSUID                                    */
00005000/*              HIGH LEVEL QUALIFIER OF THE TEMPORARY FILES CREATED */
00005100/*                                                                  */
00005200/*     REL      DEFAULT: REL99V0                                    */
00005300/*              COMPILER RELEASE                                    */
00005400/*                                                                  */
00005500/*     SRCDSN   DEFAULT: &LIB..RUNASM                               */
00005600/*              PDS CONTAINING THE AP101 ASSEMBLY SOURCE CODE       */
00005700/*                                                                  */
00005800/*     LIB      DEFAULT: &ID..&SYSTEM..&REL.                        */
00005900/*              PDS THAT WILL CONTAIN THE ASSEMBLY LISTING, OBJECT  */
00006000/*              AND LOAD MODULES                                    */
00006100/*                                                                  */
00006200/*     TMPLIB   DEFAULT: &TEMPID..&SYSTEM..&REL.                    */
00006300/*              DATASET NAME (EXCEPT FOR THE LAST NODE) OF THE      */
00006400/*              TEMPORARY FILES CREATED                             */
00006500/*                                                                  */
00006600/*     SYSPRINT DEFAULT: &LIB..RUNLST                               */
00006700/*              PDS CONTAINING THE ASSEMBLY LISTING                 */
00006800/*                                                                  */
00006900/*     OBJECT   DEFAULT: &LIB..RUNOBJ                               */
00007000/*              PDS CONTAINING THE ASSEMBLER PRODUCED OBJECT CODE   */
00007100/*                                                                  */
00007110/*     RUNLIB   DEFAULT: &LIB..RUNLIB                               */
00007120/*              PDS CONTAINING THE LINKER PRODUCED LOAD MODULE      */
00007130/*                                                                  */
00007200/*     OPT()    DEFAULT: NULL                                       */
00007300/*              ADDITIONAL ASSEMBLER OPTION                         */
00007400/*                                                                  */
00007500/*     LOPT()   DEFAULT: NULL                                       */
00007600/*              ADDITIONAL LINK OPTION FOR PASS                     */
00007700/*                                                                  */
00007800/*     FIRST    OPTIONAL                                            */
00007900/*              ALLOCATE NECESSARY DATASETS THE FIRST TIME          */
00008000/*                                                                  */
00008100/*     TRACE    OPTIONAL                                            */
00008200/*              EXECUTION OF THE CLIST WILL BE DISPLAYED AT THE     */
00008300/*              TERMINAL                                            */
00008400/*                                                                  */
00008500/*     PACK()   DEFAULT: NULL                                       */
00008600/*              SPECIFIES TO COMPRESS THE DATASETS IF A VALUE OF 20 */
00008700/*              IS SPECIFIED                                        */
00008800/*                                                                  */
00008900/*     ASM      OPTIONAL                                            */
00009000/*              WHEN SPECIFIED, DO THE ASSEMBLY STEP ONLY           */
00009100/*                                                                  */
00009200/*     MAP      OPTIONAL                                            */
00009300/*              WHEN SPECIFIED, GENERATES A MAPPED LISTING          */
00009400/*                                                                  */
00009500/*     TEMPMAC  DEFAULT: NULL                                       */
00009600/*              PDS THAT CONTAINS TEMPORARY RUNTIME LIBRARY MACROS. */
00009700/*              THIS DATASET IS CONCATENATED WITH THE CURRENT RUN-  */
00009800/*              TIME MACROS.    THE  ASSEMBLER USES THE TEMPORARY   */
00009900/*              MACROS FIRST, THEN USES THE COMPILER RELEASE MACROS */
00010000/*                                                                  */
00010100/*     SYSTEM   DEFAULT: PASS                                       */
00010200/*              INDICATES WHETHER THE LIBRARY ROUTINE IS BEING      */
00010300/*              ASSEMBLED FOR PASS OR BFS.                          */
00010500/*                                                                  */
00010600/*     RUNMAC   DEFAULT: NCAMCM.&SYSTEM..CURRENT.RUNMAC             */
00010700/*              PDS CONTAINING THE CURRENT RUNTIME LIBRARY MACROS   */
00010800/*                                                                  */
00010900/*------------------------------------------------------------------*/
00011000/*                                                                  */
00011100/*   INPUT DATA SET NAMES:                                          */
00011200/*                                                                  */
00011300/*     <TEMPMAC>                                                    */
00011400/*         TEMPORARY RUNTIME LIBRARY MACROS                         */
00011500/*                                                                  */
00011600/*     <RUNMAC>                                                     */
00011700/*         CURRENT RUNTIME LIBRARY MACROS                           */
00011800/*                                                                  */
00011900/*     <SRCDSN>(<NAME>)                                             */
00012000/*         AP101 ASSEMBLY SOURCE CODE TO ASSEMBLE                   */
00012100/*                                                                  */
00012200/*------------------------------------------------------------------*/
00012300/*                                                                  */
00012400/*   OUTPUT DATA SET NAMES:                                         */
00012500/*                                                                  */
00012600/*     <RUNLIB>(<NAME>)                                             */
00012700/*         LOAD MODULE                                              */
00012800/*                                                                  */
00012900/*     <OBJECT>(<NAME>)                                             */
00013000/*         ASSEBLER PRODUCED OBJECT CODE                            */
00013100/*                                                                  */
00013200/*     <SYSPRINT>(<NAME>)                                           */
00013400/*         ASSEMBLER LISTING                                        */
00013500/*                                                                  */
00013600/*     <TMPLIB>.MAP(<NAME>)                                         */
00013700/*         ONLY CREATED FOR PASS - LOAD MODULE MAP                  */
00013900/*                                                                  */
00014000/*     <LIB>.SDFASM(<NAME>)                                         */
00014100/*         ONLY CREATED FOR BFS - SDF OBJECT CODE                   */
00014200/*                                                                  */
00014300/*     <LIB>.SATSLST(<NAME>)                                        */
00014400/*         ONLY CREATED FOR BFS - SATSLIST LISTNG                   */
00014500/*                                                                  */
00014600/*                                                                  */
00014700/*------------------------------------------------------------------*/
00014800/*                                                                  */
00014900/*   TEMPORARY DATA SET NAMES:                                      */
00015000/*                                                                  */
00015400/*     <TMPLIB>.TEMPFIX                                             */
00015500/*         USED ONLY FOR PASS - TEMPORARY FILE USED FOR FIX OBJ     */
00015600/*                                                                  */
00015700/*------------------------------------------------------------------*/
00015800/*                                                                  */
00015900/*   COMMENTS:  THIS CLIST WAS MADE GENERAL SO THE SOURCE IS TAKEN  */
00016000/*              FROM "SRCDSN" WHICH CAN BE ANY AP101 ASSEMBLY SOURCE*/
00016100/*              SUCH AS THE RUNTIME LIBRARY SOURCE(RUNASM), ZCONLIB */
00016200/*              SOURCE(ZCONASM) OR SIMLIB SOURCE(SIMASM).           */
00016400/*                                                                  */
00016500/*              ASM101  WAS MADE GENERIC BETWEEN PASS AND BFS. THE  */
00016600/*              DEFAULT SYSTEM IS PASS. IF ASSEMBLING A BFS RUNTIME */
00016700/*              LIBRARY ROUTINE IS DESIRED, ASM101 MUST BE INVOKED  */
00016800/*              WITH THE SYSTEM SET TO BFS.                         */
00016900/*                                                                  */
00017000/*              IF AN ASM OPTION IS SPECIFIED, IT WILL ONLY PERFORM */
00017100/*              THE ASSSEMBLY STEP AND GENERATE THE OBJECT MODULE.  */
00017500/*                                                                  */
00017600/*              IF MAP FILES ARE KEPT, OBJECT CODE PRODUCED IS      */
00017700/*              DIFFERENT.                                          */
00017800/*                                                                  */
00017900/*------------------------------------------------------------------*/
00018000/*                                                                  */
00018100/*   REVISION HISTORY:                                              */
00018200/*                                                                  */
00018300/*     DATE    PCR#  NAME  DESCRIPTION                              */
00018400/*   --------  ----  ----  ---------------------------------------  */
00025710/*   01/10/96  0350  LJK   DERIVED FROM RTLASM AND MADE IT GENERIC  */
00025720/*                         FOR BUILDING RUNLIB/ZCONLIB/SIMLIB       */
00025730/*   02/05/96  0462  LJK   MODIFY TO USE THE NEW FIXOBJ TOOL        */
00025740/*   12/16/96  0505  JCS   STEP UP TO SPF27020 (28V0/12V0)          */
00025740/*   06/26/98  0552  LJK   STEP UP TO SPF28040 (29V0)               */
00025740/*   01/22/99  0621  LJK   STEP UP TO OI283 (14V0)                  */
00025740/*   05/10/99  0637  LJK   STEP UP TO SRSSOI29 (29V1)               */
00025800/*------------------------------------------------------------------*/
00025900/********************************************************************/
00026000 
00026100PROC 1 NAME                                   +
00026200       ID(&SYSUID.)                           +
00026300       TEMPID(&SYSUID.)                       +
00026400       SYSTEM(PASS)                           +
00026500       REL(REL99V0)                           +
00026600       LIB(&ID..&SYSTEM..&REL.)               +
00026700       TMPLIB(&TEMPID..&SYSTEM..&REL.)        +
00026800       SRCDSN  (&LIB..RUNASM)                 +
00026900       SYSPRINT(&LIB..RUNLST)                 +
00027000       OBJECT  (&LIB..RUNOBJ)                 +
00027100       RUNLIB  (&LIB..RUNLIB)                 +
00027200       OPT()                                  +
00027300       LOPT()                                 +
00027400       FIRST                                  +
00027500       ASM                                    +
00027600       TRACE                                  +
00027700       PACK()                                 +
00027800       MAP                                    +
00027900       TEMPMAC()                              +
00028000       RUNMAC(NCAMCM.&SYSTEM..CURRENT.RUNMAC)
00028100 
00028200WRITE
00028300IF &TRACE. NE THEN +
00028400  CONTROL LIST CONLIST
00028500 
00028600     /**********************************/
00028700     /* ALLOCATE DATASETS IF NECESSARY */
00028800     /**********************************/
00028900 
00029000CONTROL NOMSG
00029100IF &FIRST NE THEN DO
00029200   ALLOC DA('&SYSPRINT.') DSORG(PO) RECFM(F B A) +
00029300      LRECL(121) BLKSIZE(6534) TRACKS SPACE(100 20) DIR(70) REUSE
00029400   ALLOC DA('&OBJECT.') DSORG(PO) RECFM(F B) +
00029500      LRECL(80) BLKSIZE(400) CYLINDERS SPACE(3 3) DIR(70) REUSE
00029600   IF &ASM. EQ THEN DO
00029700   IF &SYSTEM = BFS THEN DO
00029800      ALLOC DA('&RUNLIB.') DSORG(PO) RECFM(F B) +
00029900         LRECL(80) BLKSIZE(6160) TRACKS SPACE(50 10) DIR(70) REUSE
00030000      ALLOC DA('&LIB..SATSLST') DSORG(PO) RECFM(F B M) +
00030100         LRECL(133) BLKSIZE(6517) CYLINDERS SPACE(12 5) DIR(70) REUSE
00030200      ALLOC DA('&LIB..SDFASM') DSORG(PO) RECFM(F) REUSE +
00030300         LRECL(1680) BLKSIZE(1680) CYLINDERS SPACE(2 1) DIR(70)
00030400   END
00030500   ELSE DO
00030600      ALLOC DA('&RUNLIB.') DSORG(PO) RECFM(U) +
00030700         LRECL(0) BLKSIZE(7294) TRACKS SPACE(50 10) DIR(70) REUSE
00030800      IF &MAP NE THEN +
00030900         ALLOC DA('&TMPLIB..MAP') DSORG(PO) RECFM(F B A) +
00031000         LRECL(121) BLKSIZE(6534) TRACKS SPACE(20 10) DIR(70) REUSE
00031100   END
00031200   END
00031300END
00031400 
00031500     /********************************/
00031600     /* ASSEMBLY  STEP               */
00031700     /********************************/
00031800 
00031900IF &PACK. EQ 20  AND &MAP. NE  THEN COMPRESS '&TMPLIB..MAP'
00032000IF &PACK. EQ 20  AND &ASM. EQ  THEN COMPRESS '&RUNLIB.'
00032100IF &PACK. EQ 20  THEN COMPRESS '&OBJECT.'
00032200IF &PACK. EQ 20  THEN COMPRESS '&SYSPRINT.'
00032300IF &SYSTEM. = &STR(BFS) THEN DO
00032400   IF &PACK. EQ 20  AND &ASM. EQ THEN DO
00032500      COMPRESS '&LIB..SATSLST'
00032600      COMPRESS '&LIB..SDFASM'
00032700   END
00032800END
00032900CONTROL END(DONE)
00033000CONTROL NOMSG
00033100FREE F(SYSLIB SYSUT1 SYSPRINT SYSLIN SYSTERM SYSIN) +
00033200   ATTR(FBA121 FB80 FBM121)
00033300DELETE ('&SYSPRINT.(&NAME.)')
00033400DELETE ('&OBJECT.(&NAME.)')
00033500CONTROL MSG
00033600ATTR  FBA121   RECFM(F B A) LRECL(121) BLKSIZE(6534)
00033700ATTR  FBM121   RECFM(F B M) LRECL(121) BLKSIZE(6534)
00033800ATTR  FB80     RECFM(F B)   LRECL(80)  BLKSIZE(80)
00033900IF &TEMPMAC. NE THEN  SET &TMPMAC = '&TEMPMAC'
00034000                ELSE  SET &TMPMAC =
00034100ALLOC F(SYSLIB)   DA(&TMPMAC. '&RUNMAC.') SHR
00034200ALLOC F(SYSUT1)   SPACE(10 10) TRACK
00034300ALLOC F(SYSPRINT) DA('&SYSPRINT.(&NAME.)') SHR
00034400ALLOC F(SYSLIN)   DA('&OBJECT.(&NAME.)') SHR
00034500ALLOC F(SYSTERM)  DA(*)
00034600ALLOC F(SYSIN)    DA('&SRCDSN.(&NAME.)') SHR
00034700WRITENR ASSEMBLY  OF &SRCDSN.(&NAME.)
00034800IF &SYSTEM. EQ PASS THEN DO
00034900   IF &OPT. EQ THEN                                   +
00035000      CALL 'SRSSOI29.GO.ASM101(ASM101)'               +
00035100           'ESD,RLD,SYSPARM=&SYSTEM.'
00035200   ELSE CALL 'SRSSOI29.GO.ASM101(ASM101)'             +
00035300           'ESD,RLD,&OPT.,SYSPARM=&SYSTEM.'
00035400   DONE
00035500ELSE DO
00035600   IF &OPT. EQ THEN                                   +
00035700      CALL 'SRSSOI29.GO.ASM101(ASM101)'               +
00035800           'SREF,LINECNT=52,SYSPARM=&SYSTEM.'
00035900   ELSE CALL 'SRSSOI29.GO.ASM101(ASM101)'             +
00036000           'SREF,LINECNT=52,&OPT.,SYSPARM=&SYSTEM.'
00036100DONE
00036200SET &SAVECC=&LASTCC
00036300FREE F(SYSLIB SYSUT1 SYSPRINT SYSLIN SYSTERM SYSIN)   +
00036400  ATTR(FBA121 FB80 FBM121)
00036500IF &SAVECC = 0 THEN +
00036600     WRITE SUCCESSFUL
00036700ELSE DO
00036800     WRITE ERROR CODE = &SAVECC
00036900     EXIT
00037000DONE
00037100 
00037200IF &ASM. NE THEN EXIT
00037300IF &SYSTEM. EQ PASS THEN DO
00037400 
00037500     /********************************/
00037600     /* FIX OBJ STEP                 */
00037700     /********************************/
00037800 
00037900   CONTROL NOMSG
00038000   FREE F(SYSIN OUTPUT3 PROGRAM SYSPRINT) ATTR(FB80)
00038010   DELETE '&TMPLIB..TEMPFIX'
00038011   CONTROL MSG
00038012   ATTR  FB80        RECFM(F B) LRECL(80) BLKSIZE(400)
00038013   ALLOC F(SYSIN)    DA('&OBJECT.(&NAME.)') SHR
00038014   ALLOC F(OUTPUT3)  DA('&TMPLIB..TEMPFIX') USING(FB80) TRACK +
00038015                     SPACE(10 2) BLOCK(80) RELEASE
00038016   ALLOC F(PROGRAM)  DA('NCAMCM.TOOLS.XPLOBJ(FIXOBJ)') SHR
00038017   ALLOC F(SYSPRINT) DA(*)
00038020   WRITENR FIXOBJ    OF &NAME.
00038021   CALL 'NCAMCM.XPL.TOOLS.MONITOR(XPLSM)'
00038022   SET &SAVECC=&LASTCC
00038030   FREE F(SYSIN OUTPUT3 PROGRAM SYSPRINT) ATTR(FB80)
00039200   IF &SAVECC = 0 THEN +
00039300      WRITE SUCCESSFUL
00039400   ELSE DO
00039500      WRITE ERROR CODE = &SAVECC
00039600      EXIT
00039700   DONE
00039800 
00039900     /********************************/
00040000     /* LINK EDIT STEP               */
00040100     /********************************/
00040200 
00040300   CONTROL NOMSG
00040400   FREE F(SYSPRINT SYSLIN SYSLMOD SYSUT1 SYSTERM) +
00040500        ATTR(FBM121 U7294)
00040600   IF &MAP. NE THEN DELETE '&TMPLIB..MAP(&NAME.)'
00040700   CONTROL MSG
00040800   ATTR FBM121       LRECL(121) BLKSIZE(1210) RECFM(F B M)
00040900   ATTR U7294        LRECL(0) BLKSIZE(7294) RECFM(U)
00041000   IF &MAP. NE THEN                                   +
00041100      ALLOC F(SYSPRINT) DA('&TMPLIB..MAP(&NAME.)') USING(FBM121)
00041200   ELSE  ALLOC F(SYSPRINT) SYS(9) USING(FBM121)
00041300   ALLOC F(SYSLIN)   DA('&TMPLIB..TEMPFIX')
00041400   ALLOC F(SYSLMOD)  DA('&RUNLIB.') SHR
00041500   ALLOC F(SYSUT1)   SP(10 10) TR
00041600   ALLOC F(SYSTERM)  SYS(9) USING(FBM121)
00041700   WRITENR LINK EDIT OF &NAME.
00041800   CALL 'SRSSOI29.GO.LNK101(LNK101)'               +
00041900        'NCAL,NOSTK,NOAUTO,NOTABLE,ESD=250,&LOPT.'
00042000   SET &SAVECC=&LASTCC
00042100   FREE F(SYSPRINT SYSLIN SYSLMOD SYSUT1 SYSTERM) ATTR(FBM121 U7294)
00042200   CONTROL NOMSG
00042300      DEL '&TMPLIB..TEMPFIX'
00042400   CONTROL MSG
00042500   IF &SAVECC = 0 THEN DO
00042600      WRITE SUCCESSFUL
00042700      CONTROL NOMSG
00042800         IF &MAP. EQ THEN DELETE '&TMPLIB..MAP(&NAME.)'
00042900      CONTROL MSG
00043000   DONE
00043100   ELSE +
00043200     WRITE ERROR CODE = &SAVECC
00043300DONE
00043400 
00043500ELSE DO /* FOR BFS */
00043600 
00043700     /********************************/
00043800     /* SATSOBJ STEP                 */
00043900     /********************************/
00044000 
00044100   CONTROL NOMSG
00044200      FREE F(CHANNEL0 ASMSDF OBJOUT CHANNEL5 FILE1 FILE2) +
00044300           ATTR(FB80 F5040)
00044400   CONTROL MSG
00044500   ATTR  FB80        RECFM(F B) LRECL(80) BLKSIZE(400)
00044600   ATTR  F5040       RECFM(F) LRECL(5040) BLKSIZE(5040)
00044700   ALLOC F(CHANNEL0) SP(2 1) TRACK BLOCK(80) RECFM(F B) LRECL(80)
00044800   OPENFILE CHANNEL0 OUTPUT
00044900   SET CHANNEL0 EQ &STR(TABLES SYM(EBCDIC,ALIAS) OBJ(EBCDIC,ALIAS))
00045000   PUTFILE CHANNEL0
00045100   SET CHANNEL0 EQ &STR(CODE(*) DATA(#L*))
00045200   PUTFILE CHANNEL0
00045300   CLOSFILE CHANNEL0
00045400   ALLOC F(ASMSDF)   DA('&LIB..SDFASM') SHR
00045500   ALLOC F(OBJOUT)   DA('&RUNLIB.(&NAME.)') SHR
00045600   ALLOC F(CHANNEL5) DA('&OBJECT.(&NAME.)') SHR
00045700   ALLOC F(CHANNEL6) SYS(9)
00045800   ALLOC F(FILE1)    SPACE(10 10) TRACK USING(F5040)
00045900   ALLOC F(FILE2)    SPACE(10 10) TRACK USING(F5040)
00046000   WRITENR SATSOBJ  OF &NAME.
00046100   CALL 'NROKSTB.SUPPORT.OILOAD.OI283(SATSOBJ)'       +
00046200        'COND=EVEN,REGION=256K'
00046300   SET &SAVECC=&LASTCC
00046400   FREE F(CHANNEL0 ASMSDF OBJOUT CHANNEL5 CHANNEL6 FILE1 FILE2)  +
00046500        ATTR(FB80 F5040)
00046600   IF &SAVECC = 0 THEN +
00046700      WRITE SUCCESSFUL
00046800   ELSE DO
00046900      WRITE ERROR CODE = &SAVECC
00047000      EXIT
00047100   DONE
00047200 
00047300     /********************************/
00047400     /* SATSLIST STEP                */
00047500     /********************************/
00047600 
00047700   CONTROL NOMSG
00047800      DELETE '&LIB..SATSLST(&NAME.)'
00047900      FREE F(CHANNEL5 CHANNEL6 CHANNEL7)
00048000   CONTROL MSG
00048100   ALLOC F(CHANNEL7) DA('&LIB..SATSLST(&NAME.)') SHR
00048200   ALLOC F(CHANNEL5) DA('&SYSPRINT.(&NAME.)') SHR
00048300   ALLOC F(CHANNEL6) SYS(9)
00048400   WRITENR SATSLIST  OF &NAME.
00048500   CALL 'NROKSTB.SUPPORT.OILOAD.OI283(SATSLIST)'               +
00048600        'COND=EVEN,REGION=192K,MSGLEVEL=0,HALPARM=  STAT=END   +
00048700         COUNT=END LINECNT=57 NOB'
00048800   SET &SAVECC=&LASTCC
00048900   FREE F(CHANNEL5 CHANNEL6 CHANNEL7)
00049000   IF &SAVECC = 0 THEN DO
00049100      WRITE SUCCESSFUL
00049200   DONE
00049300   ELSE +
00049400      WRITE ERROR CODE = &SAVECC
00049500DONE
