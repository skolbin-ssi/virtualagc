 /* 
    License:    Declared to be in the Public Domain by its author,
    		Ron Burkey.
    Purpose:	Validation and regression testing of the "modern" HAL/S
    		compiler
    Filename:	val.hal
 */
 
 DECLARE ALLTESTS BOOLEAN INITIAL(TRUE);
 WRITE(6) 'Enter 0 to disable time-consuming tests, non-zero to enable.';
 READ(5) ALLTESTS;
 
 /*---------------------------------------------------------------------------- 
   Some functions for helping to automate tests of declarations.
 	
   The TESTXXXX functions are used for testing whether a DECLARE
   produced the correct result.  To use them, feed them the name
   of an identifier, in string form, along with its basic type
   ('INTEGER', 'SCALAR', 'VECTOR', 'MATRIX', 'BIT', 'CHARACTER'),
   along with the number of a field in the ARRAY(20) returned by
   the typeof() function, and the expected value for that 
   field in either character-string form (TESTCHAR), integer form
   (TESTINT), or scalar form (TESTSCALAR).  The TESTXXXX function
   checks the basic type and the extra field value, and returns
   either TRUE (on match) or FALSE (on failure).  Note that while
   the TESTINT function is pretty foolproof, the TESTSCALAR 
   function may fail because of roundoff error and the TESTCHAR
   function may fail if your notion of how typeof() has stringified
   the fields being tested isn't quite right.
 */
 
 TESTCHAR: FUNCTION(IDENTIFIER, TYPE, FIELD, VALUE) BOOLEAN;
 	DECLARE CHARACTER(32), TYPE, IDENTIFIER;
 	DECLARE CHARACTER(2048), VALUE;
 	DECLARE INTEGER, FIELD;
 	DECLARE TEST ARRAY(20) CHARACTER(256);
 	TEST = typeof(IDENTIFIER);
 	IF TEST$1 = TYPE AND TEST$FIELD = VALUE THEN RETURN TRUE;
 	RETURN FALSE;
 CLOSE TESTCHAR;
 
 TESTINT: FUNCTION(IDENTIFIER, TYPE, FIELD, VALUE) BOOLEAN;
 	DECLARE CHARACTER(32), TYPE, IDENTIFIER;
 	DECLARE INTEGER, FIELD, VALUE;
 	DECLARE TEST ARRAY(20) CHARACTER(256);
 	TEST = typeof(IDENTIFIER);
 	IF TEST$1 = TYPE AND INTEGER(TEST$FIELD) = VALUE THEN RETURN TRUE;
 	RETURN FALSE;
 CLOSE TESTINT;
 
 TESTSCALAR: FUNCTION(IDENTIFIER, TYPE, FIELD, VALUE) BOOLEAN;
 	DECLARE CHARACTER(32), IDENTIFIER, TYPE;
 	DECLARE INTEGER, FIELD;
 	DECLARE SCALAR, VALUE;
 	DECLARE TEST ARRAY(20) CHARACTER(256);
 	TEST = typeof(IDENTIFIER);
 	IF TEST$1 = TYPE AND SCALAR(TEST$FIELD) = VALUE THEN RETURN TRUE;
 	RETURN FALSE;
 CLOSE TESTSCALAR;
 
 /* The procedure PRINT_RESULT() is used to print the result ('success' or 
    'fail') of a test with a given character-string designation.  It also
    updates the global count of ERRORS, and should be CALL'd with 
    ASSIGN(ERRORS, TOTAL). */
 DECLARE ERRORS INTEGER, TOTAL INTEGER;
 ERRORS = 0;
 TOTAL = 0;
 PRINT_RESULT:
 PROCEDURE(DESIGNATION, IS_SUCCESS) ASSIGN(ERRORS, TOTAL);
 DECLARE DESIGNATION CHARACTER(64), IS_SUCCESS BOOLEAN, 
 	 	ERRORS INTEGER, TOTAL INTEGER;
 	TOTAL = TOTAL + 1;
 	IF IS_SUCCESS THEN WRITE(6) DESIGNATION, ': success';
 	ELSE DO;
 		WRITE(6) DESIGNATION, ': fail';
 		ERRORS = ERRORS + 1;
 	END;
 CLOSE PRINT_RESULT;
 
 /* Automates comparison of an object to expected values that are in linear
    progression.  Given the starting value and increment for the linear
    progression and the unraveling of the object as a VECTOR,
    compares them and prints the result of the comparison. */
 CHECK_LINEAR: PROCEDURE(MSG, START, STEP, UNRAVELED) ASSIGN(ERRORS, TOTAL);
    DECLARE MSG CHARACTER(64), START SCALAR, STEP SCALAR, 
    	    UNRAVELED ARRAY(*) SCALAR, ERRORS INTEGER, TOTAL INTEGER;
    DECLARE TOLERANCE SCALAR CONSTANT(1E-6), EXPECTED SCALAR, BB BOOLEAN;
    DECLARE WIDTH INTEGER;
    WIDTH = SIZE(UNRAVELED);
    BB = TRUE;
    EXPECTED = START;
    DO FOR TEMPORARY I = 1 TO WIDTH;
       IF ABS(EXPECTED - UNRAVELED$I) > TOLERANCE THEN BB = FALSE;
       EXPECTED = EXPECTED + STEP;
    END;
    CALL PRINT_RESULT(MSG, BB) ASSIGN(ERRORS, TOTAL);
 CLOSE CHECK_LINEAR;
 
 /*--------------------------------------------------------------------------*/ 
 
 WRITE(6) ;
 WRITE(6) 'Each test has a designation, such as "4.56B" or "DOT PRODUCT", for';
 WRITE(6) 'cross-referencing failures back to the associated HAL/S source code';
 WRITE(6) 'for the test procedures.';
 WRITE(6) ;
 
 WRITE(6) 'Various tests of DECLARE.';
 DECLARE SCALAR, T, X INITIAL(2.0), Y CONSTANT(3.0), Z DOUBLE;
 CALL PRINT_RESULT('1.00A', TESTCHAR('T', 'SCALAR', 16, '')) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('1.00B', TESTSCALAR('X', 'SCALAR', 5, 2.0)) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('1.00C', TESTSCALAR('Y', 'SCALAR', 4, 3.0)) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('1.00D', TESTCHAR('Z', 'SCALAR', 6, 'DOUBLE')) ASSIGN(ERRORS, TOTAL);
 	
 DECLARE A, B DOUBLE, C INITIAL(4.0), D SCALAR CONSTANT(2 Y - 12);
 CALL PRINT_RESULT('1.01A', TESTCHAR('A', 'SCALAR', 6, '')) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('1.01B', TESTCHAR('B', 'SCALAR', 6, 'DOUBLE')) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('1.01C', TESTSCALAR('C', 'SCALAR', 5, 4.0)) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('1.01D', TESTSCALAR('D', 'SCALAR', 4, -6.0)) ASSIGN(ERRORS, TOTAL);
 	
 DECLARE INTEGER, I, J CONSTANT(4 D + 2), K INITIAL(J + D);
 CALL PRINT_RESULT('1.02A', TESTCHAR('I', 'INTEGER', 4, '')) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('1.02B', TESTINT('J', 'INTEGER', 4, -22)) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('1.02C', TESTINT('K', 'INTEGER', 5, -28)) ASSIGN(ERRORS, TOTAL);
 
 DECLARE L INTEGER DOUBLE, M INTEGER;
 CALL PRINT_RESULT('1.03A', TESTCHAR('L', 'INTEGER', 6, 'DOUBLE')) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('1.03B', TESTCHAR('M', 'INTEGER', 6, '')) ASSIGN(ERRORS, TOTAL);
 
 DECLARE CHARACTER(20), C1, C2 INITIAL('abc'), C3 CONSTANT('def'),
 	C4 CONSTANT(C3 CAT 'ghi');
 CALL PRINT_RESULT('1.04A', TESTINT('C1', 'CHARACTER', 2, 20)) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('1.04B', TESTCHAR('C2', 'CHARACTER', 5, 'abc')) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('1.04C', TESTCHAR('C3', 'CHARACTER', 4, 'def')) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('1.04D', TESTCHAR('C4', 'CHARACTER', 4, 'defghi')) ASSIGN(ERRORS, TOTAL);
 	
 DECLARE C5 CHARACTER(30), C6 CHARACTER(20) INITIAL('sam ' CAT C3);
 CALL PRINT_RESULT('1.05A', TESTINT('C5', 'CHARACTER', 2, 30)) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('1.05B', TESTCHAR('C6', 'CHARACTER', 5, 'sam def')) ASSIGN(ERRORS, TOTAL);
 	
 DECLARE BOOLEAN, B1, B2 CONSTANT(TRUE), B3 INITIAL(NOT B2);
 CALL PRINT_RESULT('1.06A', TESTINT('B1', 'BIT', 2, 1)) ASSIGN(ERRORS, TOTAL);
 DECLARE BB BOOLEAN;
 IF B2 AND TESTINT('B2', 'BIT', 2, 1) THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('1.06B', BB) ASSIGN(ERRORS, TOTAL);
 IF NOT B3 AND TESTINT('B3', 'BIT', 2, 1) THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('1.06C', BB) ASSIGN(ERRORS, TOTAL);
 	
 DECLARE B4 BOOLEAN, B5 BOOLEAN CONSTANT(NOT B2 AND B2), 
 	 B6 BOOLEAN INITIAL(B2 OR ~B2); 
 CALL PRINT_RESULT('1.07A', TESTINT('B4', 'BIT', 2, 1)) ASSIGN(ERRORS, TOTAL);
 IF NOT B5 AND TESTCHAR('B5', 'BIT', 4, '0, 1') THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('1.07B', BB) ASSIGN(ERRORS, TOTAL);
 IF B6 AND TESTCHAR('B6', 'BIT', 5, '1, 1') THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('1.07C', BB) ASSIGN(ERRORS, TOTAL);
 
 DECLARE CV1 CONSTANT(2); 
 DECLARE VN1 VECTOR(5); 
 DECLARE VN1A VECTOR(CV1 + 3); 
 CALL PRINT_RESULT('1.08A', TESTSCALAR('CV1', 'SCALAR', 4, 2.0)) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('1.08B', TESTINT('VN1', 'VECTOR', 2, 5)) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('1.08C', TESTINT('VN1A', 'VECTOR', 2, 5)) ASSIGN(ERRORS, TOTAL);
 
 DECLARE VN2 VECTOR(10) INITIAL(2.47); 
 DECLARE VN2A VECTOR(10) INITIAL(CV1 + 0.47); 
 DECLARE VN3 VECTOR(20) INITIAL(1, 2, 3, 4, 5, *); 
 CALL PRINT_RESULT('1.08D', TESTCHAR('VN2', 'VECTOR', 5, 
 		'2.47, 2.47, 2.47, 2.47, 2.47, 2.47, 2.47, 2.47, 2.47, 2.47')) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('1.08E', TESTCHAR('VN2A', 'VECTOR', 5, 
 		'2.47, 2.47, 2.47, 2.47, 2.47, 2.47, 2.47, 2.47, 2.47, 2.47')) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('1.08F', TESTCHAR('VN3', 'VECTOR', 5,
 	'1.0, 2.0, 3.0, 4.0, 5.0, None, None, None, None, None, None, None, None, None, None, None, None, None, None, None')) ASSIGN(ERRORS, TOTAL);
 	
 DECLARE VN3A VECTOR(3 CV1) INITIAL(1, CV1, 3, 4, 1 + CV1 2, *); 
 DECLARE VN4 VECTOR(15) CONSTANT(12); 
 DECLARE VN4A VECTOR(15) CONSTANT(12, CV1 + 11, 2 CV1 +10, *); 
 CALL PRINT_RESULT('1.08G', TESTCHAR('VN3A', 'VECTOR', 5,
 		'1.0, 2.0, 3.0, 4.0, 5.0, None')) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('1.08H', TESTCHAR('VN4', 'VECTOR', 4,
 	'12.0, 12.0, 12.0, 12.0, 12.0, 12.0, 12.0, 12.0, 12.0, 12.0, 12.0, 12.0, 12.0, 12.0, 12.0')) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('1.08I', TESTCHAR('VN4A', 'VECTOR', 4,
 	'12.0, 13.0, 14.0, None, None, None, None, None, None, None, None, None, None, None, None')) ASSIGN(ERRORS, TOTAL);
 
 /*--------------------------------------------------------------------------*/	
 WRITE(6) ;
 WRITE(6) 'Some tests of expressions and arithmetic.';
 I = 1.4;
 A = 2;
 B = 12.6E2B-1 A + 6 ( I A**3 - 21 ) / 4.2;
 IF I = 1 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('2.00A', BB) ASSIGN(ERRORS, TOTAL);
 IF A = 2.0 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('2.00B', BB) ASSIGN(ERRORS, TOTAL);
 IF ABS(B - 1241.428571) <= 0.0000005 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('2.00C', BB) ASSIGN(ERRORS, TOTAL);
 IF TRUE THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('2.00D', BB) ASSIGN(ERRORS, TOTAL);
 IF FALSE THEN BB = FALSE; ELSE BB = TRUE;
 CALL PRINT_RESULT('2.00E', BB) ASSIGN(ERRORS, TOTAL);
 IF NOT FALSE THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('2.00F', BB) ASSIGN(ERRORS, TOTAL);
 IF NOT TRUE THEN BB = FALSE; ELSE BB = TRUE;
 CALL PRINT_RESULT('2.00G', BB) ASSIGN(ERRORS, TOTAL);
 
 I = 5; 
 IF I < 10 THEN BB = TRUE; ELSE BB = FALSE; 
 CALL PRINT_RESULT('2.01A', BB) ASSIGN(ERRORS, TOTAL);
 IF I < 5 THEN BB = FALSE; ELSE BB = TRUE;
 CALL PRINT_RESULT('2.01B', BB) ASSIGN(ERRORS, TOTAL); 
 IF I < 2 THEN BB = FALSE; ELSE BB = TRUE;
 CALL PRINT_RESULT('2.01C', BB) ASSIGN(ERRORS, TOTAL); 
 IF I <= 10 THEN BB = TRUE; ELSE BB = FALSE; 
 CALL PRINT_RESULT('2.01D', BB) ASSIGN(ERRORS, TOTAL);
 IF I <= 5 THEN BB = TRUE; ELSE BB = FALSE; 
 CALL PRINT_RESULT('2.01E', BB) ASSIGN(ERRORS, TOTAL);
 IF I <= 2 THEN BB = FALSE; ELSE BB = TRUE;
 CALL PRINT_RESULT('2.01F', BB) ASSIGN(ERRORS, TOTAL); 
 IF I > 2 THEN BB = TRUE; ELSE BB = FALSE; 
 CALL PRINT_RESULT('2.01G', BB) ASSIGN(ERRORS, TOTAL);
 IF I > 5 THEN BB = FALSE; ELSE BB = TRUE;
 CALL PRINT_RESULT('2.01H', BB) ASSIGN(ERRORS, TOTAL); 
 IF I > 10 THEN BB = FALSE; ELSE BB = TRUE; 
 CALL PRINT_RESULT('2.01I', BB) ASSIGN(ERRORS, TOTAL);
 IF I >= 2 THEN BB = TRUE; ELSE BB = FALSE; 
 CALL PRINT_RESULT('2.01J', BB) ASSIGN(ERRORS, TOTAL);
 IF I >= 5 THEN BB = TRUE; ELSE BB = FALSE; 
 CALL PRINT_RESULT('2.01K', BB) ASSIGN(ERRORS, TOTAL);
 IF I >= 10 THEN BB = FALSE; ELSE BB = TRUE; 
 CALL PRINT_RESULT('2.01L', BB) ASSIGN(ERRORS, TOTAL);
 IF I = 2 THEN BB = FALSE; ELSE BB = TRUE;
 CALL PRINT_RESULT('2.01M', BB) ASSIGN(ERRORS, TOTAL); 
 IF I = 5 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('2.01N', BB) ASSIGN(ERRORS, TOTAL); 
 IF I = 10 THEN BB = FALSE; ELSE BB = TRUE;
 CALL PRINT_RESULT('2.01O', BB) ASSIGN(ERRORS, TOTAL); 
 IF I ~= 2 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('2.01P', BB) ASSIGN(ERRORS, TOTAL); 
 IF I ~= 5 THEN BB = FALSE; ELSE BB = TRUE;
 CALL PRINT_RESULT('2.01Q', BB) ASSIGN(ERRORS, TOTAL); 
 IF I ~= 10 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('2.01R', BB) ASSIGN(ERRORS, TOTAL); 

 IF 'ABC' = 'A' || 'BC' THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('2.02A', BB) ASSIGN(ERRORS, TOTAL); 
 IF 'AB' || 'C' = 'ABC' THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('2.02B', BB) ASSIGN(ERRORS, TOTAL); 
 IF 'ABC' = 'AB' THEN BB = FALSE; ELSE BB = TRUE;
 CALL PRINT_RESULT('2.02C', BB) ASSIGN(ERRORS, TOTAL); 
 
 /*--------------------------------------------------------------------------*/	
 WRITE(6) ;
 WRITE(6) 'Some tests of simple subscripting.';
 DO FOR TEMPORARY I = 1 TO 10; VN2$I = 1; END;
 IF ROUND(VN2$1) = 1 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('3.00A', BB) ASSIGN(ERRORS, TOTAL);
 IF ROUND(VN2$2) = 1 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('3.00B', BB) ASSIGN(ERRORS, TOTAL);
 IF ROUND(VN2$3) = 1 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('3.00C', BB) ASSIGN(ERRORS, TOTAL);
 IF ROUND(VN2$4) = 1 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('3.00D', BB) ASSIGN(ERRORS, TOTAL);
 IF ROUND(VN2$5) = 1 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('3.00E', BB) ASSIGN(ERRORS, TOTAL);
 IF ROUND(VN2$6) = 1 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('3.00F', BB) ASSIGN(ERRORS, TOTAL);
 IF ROUND(VN2$7) = 1 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('3.00G', BB) ASSIGN(ERRORS, TOTAL);
 IF ROUND(VN2$8) = 1 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('3.00H', BB) ASSIGN(ERRORS, TOTAL);
 IF ROUND(VN2$9) = 1 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('3.00I', BB) ASSIGN(ERRORS, TOTAL);
 IF ROUND(VN2$10) = 1 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('3.00J', BB) ASSIGN(ERRORS, TOTAL);
 
 I = 0; DO WHILE I < 10; I = I + 1; VN2$I = I; END;
 IF 1 = ROUND(VN2$1 + 0) THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('3.01A', BB) ASSIGN(ERRORS, TOTAL);
 IF 1 = ROUND(-1 + VN2$2) THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('3.01B', BB) ASSIGN(ERRORS, TOTAL);
 IF 1 = ROUND(VN2$3 / 3) THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('3.01C', BB) ASSIGN(ERRORS, TOTAL);
 IF 1 = ROUND(0.25 VN2$4) THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('3.01D', BB) ASSIGN(ERRORS, TOTAL);
 IF 1 = ROUND(VN2$5 0.2) THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('3.01E', BB) ASSIGN(ERRORS, TOTAL);
 IF 1 = ROUND(VN2$6 - 5) THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('3.01F', BB) ASSIGN(ERRORS, TOTAL);
 IF 1 = ROUND(VN2$7 - 6) THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('3.01G', BB) ASSIGN(ERRORS, TOTAL);
 IF 1 = ROUND(VN2$8 - 7) THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('3.01H', BB) ASSIGN(ERRORS, TOTAL);
 IF 1 = ROUND(VN2$9 - 8) THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('3.01I', BB) ASSIGN(ERRORS, TOTAL);
 IF 1 = ROUND(VN2$10 - 9) THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('3.01J', BB) ASSIGN(ERRORS, TOTAL);
 	
 DO FOR TEMPORARY I = 1 TO 10; VN2$I = 1; END;
 K = 0; DO FOR TEMPORARY I = 1 TO 10; IF VN2$I = 1 THEN K = K + 1; END;
 IF K = 10 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('3.02', BB) ASSIGN(ERRORS, TOTAL);
 B1=TRUE; I=0; 
 DO WHILE B1; 
 	I=I+1; 
 	IF I>=10 THEN B1=FALSE; 
 	VN2$I = I; 
 END; 	
 K = 0; DO FOR TEMPORARY I = 1 TO 10; IF VN2$I = I THEN K = K + 1; END;
 IF K = 10 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('3.03', BB) ASSIGN(ERRORS, TOTAL);
 
 B1 = TRUE; DO WHILE FALSE; B1 = FALSE; END;
 CALL PRINT_RESULT('3.04', B1) ASSIGN(ERRORS, TOTAL);
 B1 = FALSE; DO UNTIL TRUE; B1 = TRUE; END;
 CALL PRINT_RESULT('3.05', B1) ASSIGN(ERRORS, TOTAL);
 
 DO FOR TEMPORARY I = 1 TO 10; VN2$I = 1; END;
 I=0; DO UNTIL I >= 10; I=I+1; VN2$I = I; END; 
 K = 0; DO FOR TEMPORARY I = 1 TO 10; IF VN2$I = I THEN K = K + 1; END;
 IF K = 10 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('3.06', BB) ASSIGN(ERRORS, TOTAL);
 
 /*--------------------------------------------------------------------------*/	
 WRITE(6) ;
 WRITE(6) 'Various tests of DO WHILE, DO UNTIL, DO FOR.';
 IF ALLTESTS THEN DO;
 
 WRITE(6) 'The following timing tests take about 3 seconds each ...';
 TEMPORARY SCALAR, START, THISSTART, THISTIME, STOP;
 TEMPORARY INTEGER, DELTA, RUNS CONSTANT(3);
 
 START = RUNTIME; 
 THISSTART = START;
 I = 1; DO WHILE I <= RUNS; 
 	THISTIME = RUNTIME;
 	DO WHILE THISTIME - THISSTART < 1.0; 
 		THISTIME = RUNTIME; 
 	END; 
 	I = I + 1; 
 	THISSTART = THISTIME; 
 END; 
 STOP = RUNTIME;
 DELTA = ROUND(STOP - START);
 IF DELTA = RUNS THEN B1 = TRUE; ELSE B1 = FALSE;
 CALL PRINT_RESULT('4.00', B1) ASSIGN(ERRORS, TOTAL);
 
 START = RUNTIME; 
 THISSTART = START;
 I = 1; DO WHILE I <= RUNS; 
 	DO UNTIL THISTIME - THISSTART >= 1.0; 
 		THISTIME = RUNTIME; 
 	END; 
 	I = I + 1; 
 	THISSTART = THISTIME; 
 END; 
 STOP = RUNTIME;
 DELTA = ROUND(STOP - START);
 IF DELTA = RUNS THEN B1 = TRUE; ELSE B1 = FALSE;
 CALL PRINT_RESULT('4.01', B1) ASSIGN(ERRORS, TOTAL);
 
 START = RUNTIME; 
 THISSTART = START;
 I = 1; DO UNTIL I > RUNS; 
 	THISTIME = RUNTIME;
 	DO WHILE THISTIME < THISSTART + 1.0; 
 		THISTIME = RUNTIME ;
 	END; 
 	I = I + 1; 
 	THISSTART = THISTIME; 
 END; 
 STOP = RUNTIME;
 DELTA = ROUND(STOP - START);
 IF DELTA = RUNS THEN B1 = TRUE; ELSE B1 = FALSE;
 CALL PRINT_RESULT('4.02', B1) ASSIGN(ERRORS, TOTAL);
 
 START = RUNTIME; 
 THISSTART = START;
 I=1; DO UNTIL I > RUNS; 
 	DO UNTIL THISTIME >= THISSTART + 1.0; 
 		THISTIME = RUNTIME;
 	END; 
 	I = I + 1; 
 	THISSTART = THISTIME; 
 END; 
 STOP = RUNTIME;
 DELTA = ROUND(STOP - START);
 IF DELTA = RUNS THEN B1 = TRUE; ELSE B1 = FALSE;
 CALL PRINT_RESULT('4.03', B1) ASSIGN(ERRORS, TOTAL);
 
 START = RUNTIME; 
 THISSTART = START;
 DO FOR TEMPORARY I = 1 TO RUNS; 
 	DO UNTIL THISTIME >= THISSTART + 1.0; 
 		THISTIME = RUNTIME; 
 	END; 
 	THISSTART = THISTIME; 
 END; 
 STOP = RUNTIME;
 DELTA = ROUND(STOP - START);
 IF DELTA = RUNS THEN B1 = TRUE; ELSE B1 = FALSE;
 CALL PRINT_RESULT('4.04', B1) ASSIGN(ERRORS, TOTAL);
 
 START = RUNTIME; 
 THISSTART = START;
 DO FOR TEMPORARY I = 1 TO 2 RUNS BY 2; 
 	DO UNTIL THISTIME >= THISSTART + 1.0; 
 		THISTIME = RUNTIME; 
 	END; 
 	THISSTART = THISTIME; 
 END; 
 STOP = RUNTIME;
 DELTA = ROUND(STOP - START);
 IF DELTA = RUNS THEN B1 = TRUE; ELSE B1 = FALSE;
 CALL PRINT_RESULT('4.05', B1) ASSIGN(ERRORS, TOTAL);
 
 START = RUNTIME; 
 THISSTART = START;
 DO FOR TEMPORARY I = RUNS TO 1 BY -1; 
 	DO UNTIL THISTIME >= THISSTART + 1.0; 
 		THISTIME = RUNTIME; 
 	END; 
 	THISSTART = THISTIME; 
 END; 
 STOP = RUNTIME;
 DELTA = ROUND(STOP - START);
 IF DELTA = RUNS THEN B1 = TRUE; ELSE B1 = FALSE;
 CALL PRINT_RESULT('4.06', B1) ASSIGN(ERRORS, TOTAL);
 
 START = RUNTIME; 
 THISSTART = START;
 DO FOR TEMPORARY I = 1 TO 100 UNTIL I >= RUNS; 
 	DO UNTIL THISTIME >= THISSTART + 1.0; 
 		THISTIME = RUNTIME; 
 	END; 
 	THISSTART = THISTIME; 
 END; 
 STOP = RUNTIME;
 DELTA = ROUND(STOP - START);
 IF DELTA = RUNS THEN B1 = TRUE; ELSE B1 = FALSE;
 CALL PRINT_RESULT('4.07', B1) ASSIGN(ERRORS, TOTAL);
 
 START = RUNTIME; 
 THISSTART = START;
 DO FOR TEMPORARY I = 1 TO 100 WHILE I <= RUNS; 
 	DO UNTIL THISTIME >= THISSTART + 1.0; 
 		THISTIME = RUNTIME; 
 	END; 
 	THISSTART = THISTIME; 
 END; 
 STOP = RUNTIME;
 DELTA = ROUND(STOP - START);
 IF DELTA = RUNS THEN B1 = TRUE; ELSE B1 = FALSE;
 CALL PRINT_RESULT('4.08', B1) ASSIGN(ERRORS, TOTAL);
 
 WRITE(6) 'The following timing tests take about 15 seconds each ...';
 START = RUNTIME; 
 THISSTART = START;
 DO FOR A = 1.4 TO 26.3 BY 1.7; 
 	DO UNTIL THISTIME >= THISSTART + 1.0; 
 		THISTIME = RUNTIME; 
 	END; 
 	THISSTART = THISTIME; 
 END; 
 STOP = RUNTIME;
 DELTA = ROUND(STOP - START);
 IF DELTA = 15 THEN B1 = TRUE; ELSE B1 = FALSE;
 CALL PRINT_RESULT('4.09', B1) ASSIGN(ERRORS, TOTAL);
 
 START = RUNTIME; 
 THISSTART = START;
 DO FOR A = 25.2 TO 0.0 BY -1.7; 
 	DO UNTIL THISTIME >= THISSTART + 1.0; 
 		THISTIME = RUNTIME; 
 	END; 
 	THISSTART = THISTIME; 
 END; 
 STOP = RUNTIME;
 DELTA = ROUND(STOP - START);
 IF DELTA = 15 THEN B1 = TRUE; ELSE B1 = FALSE;
 CALL PRINT_RESULT('4.10', B1) ASSIGN(ERRORS, TOTAL);
 
 END; ELSE WRITE(6) 'Skipping 4.xx tests.';
 
 /*--------------------------------------------------------------------------*/	
 WRITE(6) ;
 WRITE(6) 'Various simple tests of FUNCTION and PROCEDURE.';
 /* Function that averages 3 floats. */ 
 AVG3: 
 FUNCTION(X, Y, Z) SCALAR; 
 	DECLARE SCALAR, X, Y, Z, T; 
 
 	/* Function that adds 3 floats. */
 	ADD3: 
 	FUNCTION(X, Y, Z) SCALAR; 
 		DECLARE X, Y, Z; 
 		DECLARE ADD2 FUNCTION SCALAR; 
 		RETURN ADD2(X, ADD2(Y, Z)); 
 		
 		/* Function that adds 2 floats. */
 		ADD2: 
 		FUNCTION(X, Y) SCALAR; 
 			DECLARE X, Y; 
 			RETURN X + Y; 
 		CLOSE; 
 		
 	CLOSE ADD3; 
 
	T = ADD3(X, Y, Z); 
 	RETURN T/3; 
 CLOSE AVG3; 
 IF ABS(AVG3(4, 5, 7) - 5.33333) <= 0.000005 THEN B1 = TRUE; ELSE B1 = FALSE;
 CALL PRINT_RESULT('5.00', B1) ASSIGN(ERRORS, TOTAL);
 
 /* A "projection" function that picks out just one
    parameter from among three, using W as an index. */
 PROJECTION3: 
 FUNCTION(W, X, Y, Z); 
 	DECLARE X, Y, Z; 
 	DECLARE W INTEGER; 
 	IF W = 1 THEN RETURN X; 
 	IF W = 2 THEN RETURN Y; 
 	IF W = 3 THEN RETURN Z; 
 	RETURN 0; 
 CLOSE PROJECTION3; 
 IF PROJECTION3(1, 100, 200, 300) = 100.0 THEN B1 = TRUE; ELSE B1 = FALSE;
 CALL PRINT_RESULT('5.01A', B1) ASSIGN(ERRORS, TOTAL);
 IF PROJECTION3(2, 100, 200, 300) = 200.0 THEN B1 = TRUE; ELSE B1 = FALSE;
 CALL PRINT_RESULT('5.01B', B1) ASSIGN(ERRORS, TOTAL);
 IF PROJECTION3(3, 100, 200, 300) = 300.0 THEN B1 = TRUE; ELSE B1 = FALSE;
 CALL PRINT_RESULT('5.01C', B1) ASSIGN(ERRORS, TOTAL);
 
 DECLARE X1 SCALAR INITIAL(0), Y1 SCALAR INITIAL(1), Y2, Z1 SCALAR INITIAL(2); 
 P: 
 PROCEDURE(X) ASSIGN(Y); 
	 DECLARE X, Y; 
	 Y=X; 
 CLOSE; 
 CALL P(39) ASSIGN(Z1); 
 IF Y1 = 1.0 AND Z1 = 39.0 THEN B1 = TRUE; ELSE B1 = FALSE;
 CALL PRINT_RESULT('5.02', B1) ASSIGN(ERRORS, TOTAL);
 
 P2: 
 PROCEDURE(A, B, C) ASSIGN(D, E, F); 
	 DECLARE A, B, C, D, E, F; 
	 D = A; 
	 E = B; 
	 F = C; 
 CLOSE P2; 
 CALL P2(123, 245, 789) ASSIGN(X1, Y2, Z1); 
 IF X1 = 123.0 AND Y2 = 245.0 AND Z1 = 789.0 THEN B1 = TRUE; ELSE B1 = FALSE;
 CALL PRINT_RESULT('5.03', B1) ASSIGN(ERRORS, TOTAL);
 
 /*--------------------------------------------------------------------------*/	
 WRITE(6) ;
 WRITE(6) 'Some tests of "shaping" functions.';
 DECLARE VC100 VECTOR(5) CONSTANT(1, 1#, 3, *); 
 CALL PRINT_RESULT('6.00', TESTCHAR('VC100', 'VECTOR', 4, '1.0, None, 3.0, None, None')) ASSIGN(ERRORS, TOTAL);
 DECLARE VC101 VECTOR(20) CONSTANT(VC100, 2#(2#(1,2,3), 4), 24); 
 CALL PRINT_RESULT('6.01', TESTCHAR('VC101', 'VECTOR', 4, 
 	'1.0, None, 3.0, None, None, 1.0, 2.0, 3.0, 1.0, 2.0, 3.0, 4.0, 1.0, 2.0, 3.0, 1.0, 2.0, 3.0, 4.0, 24.0')) ASSIGN(ERRORS, TOTAL);
 DECLARE VC102 VECTOR(5) CONSTANT(1, 1#2, 3, 4, 5); 
 CALL PRINT_RESULT('6.02', TESTCHAR('VC102', 'VECTOR', 4, '1.0, 2.0, 3.0, 4.0, 5.0')) ASSIGN(ERRORS, TOTAL);
 DECLARE VC103 VECTOR(5) CONSTANT(VC102); 
 CALL PRINT_RESULT('6.03', TESTCHAR('VC103', 'VECTOR', 4, '1.0, 2.0, 3.0, 4.0, 5.0')) ASSIGN(ERRORS, TOTAL);
 DECLARE VC104 VECTOR(20) CONSTANT(VC102, 2#(2#(1,2,3), 4), 24); 
 CALL PRINT_RESULT('6.04', TESTCHAR('VC104', 'VECTOR', 4, 
 	'1.0, 2.0, 3.0, 4.0, 5.0, 1.0, 2.0, 3.0, 1.0, 2.0, 3.0, 4.0, 1.0, 2.0, 3.0, 1.0, 2.0, 3.0, 4.0, 24.0')) ASSIGN(ERRORS, TOTAL);
 DECLARE VC105 VECTOR(10) CONSTANT(1, 2, VC102, 3, 4, 5); 
 CALL PRINT_RESULT('6.05', TESTCHAR('VC105', 'VECTOR', 4,
 	'1.0, 2.0, 1.0, 2.0, 3.0, 4.0, 5.0, 3.0, 4.0, 5.0')) ASSIGN(ERRORS, TOTAL);
 DECLARE A100 ARRAY(20) INTEGER; 
 CALL PRINT_RESULT('6.06', TESTINT('A100', 'INTEGER', 16, 20)) ASSIGN(ERRORS, TOTAL);
 DECLARE B100 ARRAY(20) SCALAR; 
 CALL PRINT_RESULT('6.07', TESTINT('B100', 'SCALAR', 16, 20)) ASSIGN(ERRORS, TOTAL);
 A100 = INTEGER(2#VC100, 10#5); 
 IF 1 = ROUND(A100$1/VC100$1) AND ROUND(A100$3/VC100$3) = 1 AND 
 	1 = ROUND(A100$6/VC100$1) AND ROUND(A100$8/VC100$3) = 1 AND
 	1 = ROUND(A100$11/5) AND ROUND(A100$12/5) = 1 AND
 	1 = ROUND(A100$13/5) AND ROUND(A100$14/5) = 1 AND
 	1 = ROUND(A100$15/5) AND ROUND(A100$16/5) = 1 AND
 	1 = ROUND(A100$17/5) AND ROUND(A100$18/5) = 1 AND 
 	1 = ROUND(A100$19/5) AND ROUND(A100$20/5) = 1 
 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('6.08A', BB) ASSIGN(ERRORS, TOTAL);
 DECLARE MYTYPE ARRAY(20) CHARACTER(2048);
 MYTYPE = typeofv(INTEGER(2#VC100, 10#5));
 IF MYTYPE$1 = 'INTEGER' AND MYTYPE$16 = '20' THEN B1 = TRUE; ELSE B1 = FALSE;
 CALL PRINT_RESULT('6.08B', B1) ASSIGN(ERRORS, TOTAL);
 B100 = SCALAR(3#VC100, 5#6); 
 IF 1 = ROUND(B100$1/VC100$1) AND 1 = ROUND(B100$3/VC100$3) AND
 	1 = ROUND(B100$6/VC100$1) AND 1 = ROUND(B100$8/VC100$3) AND
 	1 = ROUND(B100$11/VC100$1) AND 1 = ROUND(B100$13/VC100$3) AND
 	1 = ROUND(B100$16/6) AND 1 = ROUND(B100$17/6) AND
 	1 = ROUND(B100$18/6) AND 1 = ROUND(B100$19/6) AND
 	1 = ROUND(B100$20/6) THEN B1 = TRUE; ELSE B1 = FALSE;
 CALL PRINT_RESULT('6.09A', B1) ASSIGN(ERRORS, TOTAL);
 MYTYPE = typeofv(SCALAR(3#VC100, 5#6));
 IF MYTYPE$1 = 'SCALAR' AND MYTYPE$16 = '20' THEN B1 = TRUE; ELSE B1 = FALSE;
 CALL PRINT_RESULT('6.09B', B1) ASSIGN(ERRORS, TOTAL);
 I = 27; K = 52;
 DECLARE A201 ARRAY(10) INTEGER; 
 BB = TRUE; 
 DO FOR TEMPORARY I = 1 TO 10; 
 	A201 = INTEGER(1, 2, 3, 4, 2 I, 6, 7, 8, 9, 10); 
 	DO FOR TEMPORARY K = 1 TO 10;
 		IF K = 5 AND A201$K ~= 2 I THEN BB = FALSE;
 		ELSE IF K ~= 5 AND A201$K ~= K THEN BB = FALSE; 
 	END;
 END; 
 CALL PRINT_RESULT('6.09C', BB) ASSIGN(ERRORS, TOTAL);
 IF 1 = ROUND(I / 27) AND 1 = ROUND(K / 52) THEN B1 = TRUE; ELSE B1 = FALSE;
 CALL PRINT_RESULT('6.09D', B1) ASSIGN(ERRORS, TOTAL);
 
 /*--------------------------------------------------------------------------*/	
 WRITE(6) ;
 WRITE(6) 'Some tests of repitition factors (#).';
 DECLARE B300 BIT(6);
 CALL PRINT_RESULT('7.00A', TESTINT('B300', 'BIT', 2, 6)) ASSIGN(ERRORS, TOTAL);
 MYTYPE = typeofv(34);
 IF MYTYPE$1 = 'INTEGER' THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('7.00B', BB) ASSIGN(ERRORS, TOTAL);
 MYTYPE = typeofv(34.2);
 IF MYTYPE$1 = 'SCALAR' THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('7.00C', BB) ASSIGN(ERRORS, TOTAL);
 B300 = 34.2;
 IF INTEGER(B300) = 34 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('7.00D', BB) ASSIGN(ERRORS, TOTAL);
 B300 = 98;
 IF INTEGER(B300) = 34 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('7.00E', BB) ASSIGN(ERRORS, TOTAL);
 MYTYPE = typeofv(INTEGER(B300));
 IF MYTYPE$1 = 'INTEGER' THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('7.00F', BB) ASSIGN(ERRORS, TOTAL);
 MYTYPE = typeofv(INTEGER('34'));
 IF MYTYPE$1 = 'INTEGER' THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('7.00G', BB) ASSIGN(ERRORS, TOTAL);
 MYTYPE = typeofv(INTEGER('34.2'));
 IF MYTYPE$1 = 'INTEGER' THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('7.00H', BB) ASSIGN(ERRORS, TOTAL);
 
 MYTYPE = typeofv(34.0);
 IF MYTYPE$1 = 'SCALAR' THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('7.01A', BB) ASSIGN(ERRORS, TOTAL);
 MYTYPE = typeofv(34.0);
 IF MYTYPE$1 = 'SCALAR' THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('7.01B', BB) ASSIGN(ERRORS, TOTAL);
 B300 = 98;
 MYTYPE = typeofv(SCALAR(B300));
 IF MYTYPE$1 = 'SCALAR' THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('7.01C', BB) ASSIGN(ERRORS, TOTAL);
 MYTYPE = typeofv(SCALAR('34'));
 IF MYTYPE$1 = 'SCALAR' THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('7.00D', BB) ASSIGN(ERRORS, TOTAL);
 MYTYPE = typeofv(SCALAR('34.2'));
 IF MYTYPE$1 = 'SCALAR' THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('7.01E', BB) ASSIGN(ERRORS, TOTAL);
 
 DECLARE VECTOR, V300 CONSTANT(1.0, 2.0, 3.0), V301, V302; 
 DECLARE MATRIX, M300 CONSTANT(1, 2, 3, 4, 5, 6, 7, 8, 9), M301, M302; 
 DECLARE ARRAY(3), A300; 
 DECLARE A301 ARRAY(9) SCALAR, A302 ARRAY(3) SCALAR; 
 CALL PRINT_RESULT('7.02A', TESTCHAR('V300', 'VECTOR', 4, '1.0, 2.0, 3.0')) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('7.02B', TESTINT('V301', 'VECTOR', 2, 3)) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('7.02C', TESTINT('V302', 'VECTOR', 2, 3)) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('7.02D', TESTCHAR('M300', 'MATRIX', 4, 
 				'1.0, 2.0, 3.0, 4.0, 5.0, 6.0, 7.0, 8.0, 9.0')) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('7.02E', TESTINT('M301', 'MATRIX', 2, 3)) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('7.02F', TESTINT('M301', 'MATRIX', 3, 3)) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('7.02G', TESTINT('M302', 'MATRIX', 2, 3)) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('7.02H', TESTINT('M302', 'MATRIX', 3, 3)) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('7.02I', TESTINT('A300', 'SCALAR', 16, 3)) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('7.02J', TESTINT('A301', 'SCALAR', 16, 9)) ASSIGN(ERRORS, TOTAL);
 
 IF V300$1 = 1.0 AND V300$2 = 2.0 AND V300$3 = 3.0 THEN BB = TRUE; 
 ELSE BB = FALSE;
 CALL PRINT_RESULT('7.02K', BB) ASSIGN(ERRORS, TOTAL);
 MYTYPE = typeofv(INTEGER(V300));
 IF MYTYPE$1 = 'VECTOR' AND MYTYPE$2 = '3' AND MYTYPE$3 = '' THEN BB = TRUE;
 ELSE BB = FALSE;
 CALL PRINT_RESULT('7.02L', BB) ASSIGN(ERRORS, TOTAL);
 V301 = INTEGER(V300); 
 MYTYPE = typeofv(V301$1);
 IF MYTYPE$1 = 'SCALAR' THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('7.02M', BB) ASSIGN(ERRORS, TOTAL);
 IF V301$1 = 1 AND V301$2 = 2 AND V301$3 = 3 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('7.02N', BB) ASSIGN(ERRORS, TOTAL);
 MYTYPE = typeofv(INTEGER(1#V300));
 IF MYTYPE$1 = 'INTEGER' AND MYTYPE$16 = '3' THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('7.02O', BB) ASSIGN(ERRORS, TOTAL);
 A300 = INTEGER(1#V300); 
 IF A300$1 = 1 AND V300$2 = 2 AND V300$3 = 3 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('7.02P', BB) ASSIGN(ERRORS, TOTAL);
 
 BB = TRUE; K = 1;
 DO FOR TEMPORARY I = 1 TO 3; DO FOR TEMPORARY J = 1 TO 3;
 	IF M300$(I,J) ~= K THEN BB = FALSE;
 	K = K + 1;
 END; END;
 CALL PRINT_RESULT('7.03A', BB) ASSIGN(ERRORS, TOTAL);
 MYTYPE = typeofv(INTEGER(M300));
 M301 = INTEGER(M300);
 BB = TRUE; IF MYTYPE$1 ~= 'MATRIX' THEN BB = FALSE;
 CALL PRINT_RESULT('7.03B', BB) ASSIGN(ERRORS, TOTAL);
 BB = TRUE; K = 1;
 DO FOR TEMPORARY I = 1 TO 3; DO FOR TEMPORARY J = 1 TO 3;
 	IF M301$(I,J) ~= K THEN BB = FALSE;
 	K = K + 1;
 END; END;
 CALL PRINT_RESULT('7.03C', BB) ASSIGN(ERRORS, TOTAL);
 MYTYPE = typeofv(INTEGER(1#M300));
 BB = TRUE; 
 IF MYTYPE$1 ~= 'INTEGER' OR MYTYPE$16 ~= '9'THEN BB = FALSE;
 CALL PRINT_RESULT('7.03D', BB) ASSIGN(ERRORS, TOTAL);
 A301 = INTEGER(1#M300); 
 BB = TRUE; DO FOR TEMPORARY I = 1 TO 9; IF A301$I ~= I THEN BB = FALSE; END;
 CALL PRINT_RESULT('7.03E', BB) ASSIGN(ERRORS, TOTAL);
 MYTYPE = typeofv(SCALAR(V301));
 BB = TRUE; IF MYTYPE$1 ~= 'VECTOR' THEN BB = FALSE;
 CALL PRINT_RESULT('7.03F', BB) ASSIGN(ERRORS, TOTAL);
 V302 = SCALAR(V301);
 BB = TRUE; DO FOR TEMPORARY K = 1 TO 3; IF V302$K ~= K THEN BB = FALSE; END;
 CALL PRINT_RESULT('7.03G', BB) ASSIGN(ERRORS, TOTAL);
 MYTYPE = typeofv(SCALAR(1#V301));
 BB = TRUE; IF MYTYPE$1 ~= 'SCALAR' OR MYTYPE$16 ~= '3' THEN BB = FALSE;
 CALL PRINT_RESULT('7.03H', BB) ASSIGN(ERRORS, TOTAL);
 A300 = SCALAR(1#V301); 
 BB = TRUE; DO FOR TEMPORARY K = 1 TO 3; IF A300$K ~= K THEN BB = FALSE; END;
 CALL PRINT_RESULT('7.03I', BB) ASSIGN(ERRORS, TOTAL);
 MYTYPE = typeofv(SCALAR(M301));
 BB = TRUE; 
 IF MYTYPE$1 ~= 'MATRIX' OR MYTYPE$2 ~= '3' OR MYTYPE$3 ~= '3' THEN BB = FALSE;
 CALL PRINT_RESULT('7.03J', BB) ASSIGN(ERRORS, TOTAL);
 M302 = SCALAR(M301);
 BB = TRUE; K = 1;
 DO FOR TEMPORARY I = 1 TO 3; DO FOR TEMPORARY J = 1 TO 3;
 	IF M302$(I,J) ~= K THEN BB = FALSE;
 	K = K + 1;
 END; END;
 CALL PRINT_RESULT('7.03K', BB) ASSIGN(ERRORS, TOTAL);
 MYTYPE = typeofv(SCALAR(1#M301));
 BB = TRUE; 
 IF MYTYPE$1 ~= 'SCALAR' OR MYTYPE$16 ~= '9' THEN BB = FALSE;
 CALL PRINT_RESULT('7.03L', BB) ASSIGN(ERRORS, TOTAL);
 A301 = SCALAR(1#M301);
 BB = TRUE; DO FOR TEMPORARY K = 1 TO 9; IF A301$K ~= K THEN BB = FALSE; END;
 CALL PRINT_RESULT('7.03M', BB) ASSIGN(ERRORS, TOTAL);
 
 /*--------------------------------------------------------------------------*/	
 WRITE(6) ;
 WRITE(6) 'Some tests of initialized vs uninitialized values and "shaping" functions.';
 DECLARE V400 VECTOR(10), V401 VECTOR(10), V402 VECTOR(10); 
 DECLARE MATRIX, M400, M400U, M401, M402; 
 CALL PRINT_RESULT('8.00A', NOT initialized(V400)) ASSIGN(ERRORS, TOTAL);
 V400$1=100; 
 V400$3=200; 
 V400$5=300; 
 BB = TRUE;
 DO FOR TEMPORARY I = 1 TO 10;
 	IF I = 1 OR I = 3 OR I = 5 THEN 
 		DO; IF V400$I ~= ROUND(50 (I + 1)) THEN BB = FALSE; END;
 	ELSE IF initialized(V400$I) THEN BB = FALSE;
 END; 
 CALL PRINT_RESULT('8.00B', BB) ASSIGN(ERRORS, TOTAL);
 DO FOR TEMPORARY I = 1 TO 10; V401$I = 49 I; END;
 CALL PRINT_RESULT('8.00C', initialized(V401)) ASSIGN(ERRORS, TOTAL);
 CALL PRINT_RESULT('8.00D', NOT initialized(M400U)) ASSIGN(ERRORS, TOTAL);
 BB = TRUE;
 DO FOR TEMPORARY I = 1 TO 3; DO FOR TEMPORARY J = 1 TO 3;
 	IF NOT initialized(M300$(I,J)) THEN BB = FALSE;
 END; END;
 CALL PRINT_RESULT('8.00E', BB) ASSIGN(ERRORS, TOTAL);
 
 MYREVERSE:
 PROCEDURE(V) ASSIGN(W); 
 	DECLARE VECTOR(10), V, W; 
 	DO FOR TEMPORARY I = 1 TO 10; 
 		W$I = V$(11-I); 
 	END; 
 CLOSE MYREVERSE; 
 CALL MYREVERSE(V401) ASSIGN(V402); 
 BB = TRUE;
 DO FOR TEMPORARY I = 1 TO 10;
 	IF V402$I ~= 49 (11 - I) THEN BB = FALSE; 
 END; 
 CALL PRINT_RESULT('8.01A', BB) ASSIGN(ERRORS, TOTAL);
 
 DO FOR TEMPORARY I = 1 TO 3; 
 	DO FOR TEMPORARY J = 1 TO 3; 
 		M400$(I,J) = 10 I + J; 
 	END; 
 END; 
 DECLARE M400T MATRIX CONSTANT(11, 12, 13, 21, 22, 23, 31, 32, 33);
 DECLARE M401T MATRIX CONSTANT(11, 21, 31, 12, 22, 32, 13, 23, 33);
 BB = TRUE;
 DO FOR TEMPORARY I = 1 TO 3;
 	DO FOR TEMPORARY J = 1 TO 3;
 		IF M400$(I,J) ~= M400T$(I,J) THEN BB = FALSE;
 	END;
 END;
 CALL PRINT_RESULT('8.01B', BB) ASSIGN(ERRORS, TOTAL);
 
 MYTRANSPOSE:
 PROCEDURE(M) ASSIGN(MT); 
 	DECLARE MATRIX, M, MT; 
 	DO FOR TEMPORARY I = 1 TO 3; 
 		DO FOR TEMPORARY J = 1 TO 3; 
 			MT$(I, J) = M$(J, I); 
 		END; 
 	END; 
 CLOSE MYTRANSPOSE; 
 CALL MYTRANSPOSE(M400) ASSIGN(M401); 
 BB = TRUE;
 DO FOR TEMPORARY I = 1 TO 3;
 	DO FOR TEMPORARY J = 1 TO 3;
 		IF M401$(I,J) ~= M401T$(I,J) THEN BB = FALSE;
 	END;
 END;
 CALL PRINT_RESULT('8.01C', BB) ASSIGN(ERRORS, TOTAL);
 
 /*--------------------------------------------------------------------------*/	
 WRITE(6) ;
 WRITE(6) 'Some tests of automatic datatype conversions.';
 DECLARE I590 INTEGER, X590 SCALAR, 
 	B590 BIT(4), B591 BIT(5) INITIAL(23), 
 	B595 BIT(4) INITIAL(5), B596 BIT(4) INITIAL(6), B597 BIT(4) INITIAL(7),
 	C590 CHARACTER(25),
 	V590 VECTOR(5), 
 	M590 MATRIX(4,6), 
 	A590 ARRAY(5) INTEGER, A591 ARRAY(5) VECTOR(4),
 	TST CHARACTER(10);
 I590 = 6;
 IF I590 = 6 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('9.00A', BB) ASSIGN(ERRORS, TOTAL);
 I590 = 5.4;
 IF I590 = 5 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('9.00B', BB) ASSIGN(ERRORS, TOTAL);
 I590 = '7.2';
 IF I590 = 7 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('9.00C', BB) ASSIGN(ERRORS, TOTAL);
 I590 = B591;
 IF I590 = 23 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('9.00D', BB) ASSIGN(ERRORS, TOTAL);
 
 X590 = 6;
 IF X590 = 6.0 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('9.01A', BB) ASSIGN(ERRORS, TOTAL);
 X590 = 5.4;
 IF X590 = 5.4 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('9.01B', BB) ASSIGN(ERRORS, TOTAL);
 X590 = '7.2';
 IF X590 = 7.2 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('9.01C', BB) ASSIGN(ERRORS, TOTAL);
 X590 = B591;
 IF X590 = 23.0 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('9.01D', BB) ASSIGN(ERRORS, TOTAL);
 
 C590 = 6;
 IF C590 = '6' THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('9.02A', BB) ASSIGN(ERRORS, TOTAL);
 WRITE(6) 'Unfortunately, I had to hard-code the implementation-dependent';
 WRITE(6) 'arithmetical precision into test 9.02B below.';
 C590 = 5.4;
 IF C590 = ' 5.40000000000000E+00' THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('9.02B', BB) ASSIGN(ERRORS, TOTAL);
 C590 = '7.2';
 IF C590 = '7.2' THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('9.02C', BB) ASSIGN(ERRORS, TOTAL);
 C590 = B591;
 IF C590 = '10111' THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('9.02D', BB) ASSIGN(ERRORS, TOTAL);
 
 B590 = 6;
 IF B590 = B596 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('9.03A', BB) ASSIGN(ERRORS, TOTAL);
 B590 = 5.4;
 IF B590 = B595 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('9.03B', BB) ASSIGN(ERRORS, TOTAL);
 B590 = '10111';
 IF B590 = B597 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('9.03V', BB) ASSIGN(ERRORS, TOTAL);
 B590 = B591;
 IF B590 = B597 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('9.03D', BB) ASSIGN(ERRORS, TOTAL);
 
 DECLARE M591 MATRIX(2,3), M592 MATRIX(2,6), M593 MATRIX(4, 3), 
 	BBB BOOLEAN, 
 	A592 ARRAY(2, 3) MATRIX(4,6), A593 ARRAY(2, 3) MATRIX(4, 6),
 	V591 VECTOR(6), V592 VECTOR(4), 
 	A594 ARRAY(3) SCALAR, A595 ARRAY(2) SCALAR;
 DO FOR TEMPORARY I = 1 TO 4;
 	DO FOR TEMPORARY J = 1 TO 6;
 		M590$(I,J) = 10 I + J;
 	END;
 END;
 M591 = M590$(2 TO 3, 3 TO 5);
 BBB = TRUE;
 DO FOR TEMPORARY I = 1 TO 2;
 	DO FOR TEMPORARY J = 1 TO 3;
 		IF M591$(I, J) ~= 10 (I + 1) + (J + 2) THEN BBB = FALSE;
 	END;
 END;
 CALL PRINT_RESULT('9.04A', BBB) ASSIGN(ERRORS, TOTAL);
 M592 = M590$(2 TO 3, *);
 BBB = TRUE;
 DO FOR TEMPORARY I = 1 TO 2;
 	DO FOR TEMPORARY J = 1 TO 6;
 		IF M592$(I, J) ~= 10 (I + 1) + J THEN BBB = FALSE;
 	END;
 END;
 CALL PRINT_RESULT('9.04B', BBB) ASSIGN(ERRORS, TOTAL);
 M593 = M590$(*, 3 TO 5);
 BBB = TRUE;
 DO FOR TEMPORARY I = 1 TO 4;
 	DO FOR TEMPORARY J = 1 TO 3;
 		IF M593$(I, J) ~= 10 I + (J + 2) THEN BBB = FALSE;
 	END;
 END;
 CALL PRINT_RESULT('9.04C', BBB) ASSIGN(ERRORS, TOTAL);
 M592 = M590$(2 TO 3, 1 TO #);
 BBB = TRUE;
 DO FOR TEMPORARY I = 1 TO 2;
 	DO FOR TEMPORARY J = 1 TO 6;
 		IF M592$(I, J) ~= 10 (I + 1) + J THEN BBB = FALSE;
 	END;
 END;
 CALL PRINT_RESULT('9.04D', BBB) ASSIGN(ERRORS, TOTAL);

 M591 = M590$(2 AT 2, 3 AT 3);
 BBB = TRUE;
 DO FOR TEMPORARY I = 1 TO 2;
 	DO FOR TEMPORARY J = 1 TO 3;
 		IF M591$(I, J) ~= 10 (I + 1) + (J + 2) THEN BBB = FALSE;
 	END;
 END;
 CALL PRINT_RESULT('9.04E', BBB) ASSIGN(ERRORS, TOTAL);
 M592 = M590$(2 AT 2, *);
 BBB = TRUE;
 DO FOR TEMPORARY I = 1 TO 2;
 	DO FOR TEMPORARY J = 1 TO 6;
 		IF M592$(I, J) ~= 10 (I + 1) + J THEN BBB = FALSE;
 	END;
 END;
 CALL PRINT_RESULT('9.04F', BBB) ASSIGN(ERRORS, TOTAL);
 M593 = M590$(*, 3 AT 3);
 BBB = TRUE;
 DO FOR TEMPORARY I = 1 TO 4;
 	DO FOR TEMPORARY J = 1 TO 3;
 		IF M593$(I, J) ~= 10 I + (J + 2) THEN BBB = FALSE;
 	END;
 END;
 CALL PRINT_RESULT('9.04G', BBB) ASSIGN(ERRORS, TOTAL);
 M592 = M590$(2 AT 2, 6 AT 1);
 BBB = TRUE;
 DO FOR TEMPORARY I = 1 TO 2;
 	DO FOR TEMPORARY J = 1 TO 6;
 		IF M592$(I, J) ~= 10 (I + 1) + J THEN BBB = FALSE;
 	END;
 END;
 CALL PRINT_RESULT('9.04H', BBB) ASSIGN(ERRORS, TOTAL);
 DO FOR TEMPORARY I = 1 TO 2;
 	DO FOR TEMPORARY J = 1 TO 3;
 		DO FOR TEMPORARY K = 1 TO 4;
 			DO FOR TEMPORARY L = 1 TO 6;
 				A592$(I,J;K,L) = 1000 I + 100 J + 10 K + L;
 			END;
 		END;
 	END;
 END;
 BBB = TRUE;
 DO FOR TEMPORARY I = 1 TO 2;
 	DO FOR TEMPORARY J = 1 TO 3;
 		DO FOR TEMPORARY K = 1 TO 4;
 			DO FOR TEMPORARY L = 1 TO 6;
 				IF A592$(I,J;K,L) ~= 1000 I + 100 J + 10 K + L
 				THEN BBB = FALSE;
 			END;
 		END;
 	END;
 END;
 CALL PRINT_RESULT('9.04I', BBB) ASSIGN(ERRORS, TOTAL);
 V591 = A592$(1, 2; 3, *);
 BBB = TRUE;
 DO FOR TEMPORARY I = 1 TO 6; 
 	IF V591$I ~= 1230 + I THEN BBB = FALSE;
 END;
 CALL PRINT_RESULT('9.04J', BBB) ASSIGN(ERRORS, TOTAL);
 V592 = A592$(2, 1; *, 4);
 BBB = TRUE;
 DO FOR TEMPORARY I = 1 TO 4; 
 	IF V592$I ~= 2104 + 10 I THEN BBB = FALSE;
 END;
 CALL PRINT_RESULT('9.04K', BBB) ASSIGN(ERRORS, TOTAL);
 A594 = A592$(2, *; 2, 3);
 BBB = TRUE;
 DO FOR TEMPORARY I = 1 TO 3; 
 	IF A594$I ~= 2023 + 100 I THEN BBB = FALSE;
 END;
 CALL PRINT_RESULT('9.04L', BBB) ASSIGN(ERRORS, TOTAL);
 A595 = A592$(*, 2; 1, 2);
 BBB = TRUE;
 DO FOR TEMPORARY I = 1 TO 2; 
 	IF A595$I ~= 0212 + 1000 I THEN BBB = FALSE;
 END;
 CALL PRINT_RESULT('9.04M', BBB) ASSIGN(ERRORS, TOTAL);
 A593 = A592;
 BBB = TRUE;
 DO FOR TEMPORARY I = 1 TO 2;
 	DO FOR TEMPORARY J = 1 TO 3;
 		DO FOR TEMPORARY K = 1 TO 4;
 			DO FOR TEMPORARY L = 1 TO 6;
 				IF A593$(I,J;K,L) ~= 1000 I + 100 J + 10 K + L
 				THEN BBB = FALSE;
 			END;
 		END;
 	END;
 END;
 CALL PRINT_RESULT('9.04N', BBB) ASSIGN(ERRORS, TOTAL);
 A593$(1, 1; 2 TO 3, 3 TO 5) = A592$(2, 2; 1 TO 2, 2 TO 4);
 DO FOR TEMPORARY I = 1 TO 2;
 	DO FOR TEMPORARY J = 1 TO 3;
 		DO FOR TEMPORARY K = 1 TO 4;
 			DO FOR TEMPORARY L = 1 TO 6;
 				IF I = 1 AND J = 1 AND K >= 2 AND K <= 3
 					AND L >= 3 AND L <= 5 THEN 
 				DO;
	 				IF A593$(I,J;K,L) ~= 1000 (I + 1) + 
	 						     100 (J + 1) + 
	 						     10 (K - 1) + 
	 						     (L - 1)
	 				THEN BBB = FALSE;
 				END;
 				ELSE DO;
	 				IF A593$(I,J;K,L) ~= 
	 					1000 I + 100 J + 10 K + L
	 				THEN BBB = FALSE;
 				END;
 			END;
 		END;
 	END;
 END;
 CALL PRINT_RESULT('9.04O', BBB) ASSIGN(ERRORS, TOTAL);
 
 DECLARE A596I ARRAY(2,3) INTEGER, A596S ARRAY(2,3) SCALAR,
 	 A596V4 ARRAY(2,3) VECTOR(4);
 A596I = INTEGER$(2, 3)(11, 12, 13, 21, 22, 23);
 A596S = SCALAR$(2, 3)(110, 120, 130, 210, 220, 230);
 I = 111;
 A596V4 = SCALAR$(2, 3; 4)(I, 112, 113, 114,
 			   121, 122, 123, 124,
 			   131, 132, 133, 134,
 			   211, 212, 213, 214,
 			   221, 222, 223, 224,
 			   231, 232, 233, 234);
 BBB = TRUE;
 DO FOR TEMPORARY I = 1 TO 2; 
 DO FOR TEMPORARY J = 1 TO 3;
 DO FOR TEMPORARY K = 1 TO 4;
 	IF A596V4$(I, J; K) ~= 100 I + 10 J + K THEN BBB = FALSE;
 END; END; END;
 CALL PRINT_RESULT('9.04P', BBB) ASSIGN(ERRORS, TOTAL);
 
 /*--------------------------------------------------------------------------*/	
  /* Test equality with a tolerance. */
  REALLY_CLOSE_TO: FUNCTION(X, Y) BOOLEAN;
     DECLARE X SCALAR, Y SCALAR;
     DECLARE EPSILON SCALAR CONSTANT(1E-9);
     IF ABS(X - Y) < EPSILON THEN RETURN TRUE; ELSE RETURN FALSE;
  CLOSE REALLY_CLOSE_TO;
 
 /* Convert degrees to radians and vice-versa. */
 DECLARE PI CONSTANT(3.141592653589793);
 DECLARE DEGREES_PER_RADIAN CONSTANT(180 / PI);
 DEGREES_TO_RADIANS: FUNCTION(DEGREES) SCALAR;
    DECLARE SCALAR, DEGREES;
    RETURN DEGREES / DEGREES_PER_RADIAN;
 CLOSE DEGREES_TO_RADIANS;
 RADIANS_TO_DEGREES: FUNCTION(RADIANS) SCALAR;
    DECLARE SCALAR, RADIANS;
    RETURN RADIANS DEGREES_PER_RADIAN;
 CLOSE RADIANS_TO_DEGREES;
 
 RTL_TESTS: PROCEDURE ASSIGN(ERRORS, TOTAL);
    DECLARE INTEGER, ERRORS, TOTAL;
    DECLARE BOOLEAN, BB, BB2;
    DECLARE COSINE_ANGLES VECTOR(17) CONSTANT(0, 30, 45, 60, 90, 120, 135, 150, 
 				180, 210, 225, 240, 270, 300, 315, 330, 360),
            COSINE_VALUES VECTOR(17) CONSTANT(1, SQRT(3)/2, SQRT(2)/2, 1/2, 0, 
         			-1/2, -SQRT(2)/2, -SQRT(3)/2, -1, 
         			-SQRT(3)/2, -SQRT(2)/2, -1/2, 0,
         			1/2, SQRT(2)/2, SQRT(3)/2, 1),
            ARCCOS_VALUES VECTOR(6) CONSTANT(1.5707963267948966 ,
         	1.369438406004566 ,1.1592794807274085 ,0.9272952180016121 ,
         	0.6435011087932843 ,0.0),
            ARCCOSH_VALUES VECTOR(4) CONSTANT(1.3169578969248166 , 
         	1.762747174039086 , 2.0634370688955608 , 2.2924316695611777),
            ARCSIN_VALUES VECTOR(6) CONSTANT(0.0 , 0.2013579207903308 , 
         	0.41151684606748806 , 0.6435011087932845 , 0.9272952180016123 , 
         	1.5707963267948966),
            ARCSINH_VALUES VECTOR(11) CONSTANT(-2.3124383412727525 , 
         	-2.0947125472611012 , -1.8184464592320668 , -1.4436354751788103, 
         	-0.881373587019543 , 0.0 , 0.881373587019543 , 
         	1.4436354751788103 , 1.8184464592320668 , 2.0947125472611012 , 
         	2.3124383412727525),
            ARCTAN2_VALUES MATRIX(5, 5) CONSTANT(-2.356194490192345 , 
         	-2.0344439357957027 , -1.5707963267948966 , -1.1071487177940904, 
         	-0.7853981633974483 , -2.677945044588987 , -2.356194490192345 , 
         	-1.5707963267948966 , -0.7853981633974483 , -0.4636476090008061, 
         	3.141592653589793 , 3.141592653589793 , 0.0 , 0.0, 0.0 , 
         	2.677945044588987 , 2.356194490192345 , 1.5707963267948966 , 
         	0.7853981633974483 , 0.4636476090008061 , 2.356194490192345 , 
         	2.0344439357957027 , 1.5707963267948966 , 1.1071487177940904 , 
         	0.7853981633974483),
            ARCTAN_VALUES VECTOR(11) CONSTANT(-1.373400766945016 , 
         	-1.3258176636680326 , -1.2490457723982544 , -1.1071487177940904, 
         	-0.7853981633974483 , 0.0 , 0.7853981633974483 , 
         	1.1071487177940904 , 1.2490457723982544 , 1.3258176636680326 , 
         	1.373400766945016),
            ARCTANH_VALUES VECTOR(11) CONSTANT(-1.1989476363991853 , 
         	-0.8047189562170501 , -0.5493061443340548 , 
         	-0.34657359027997264 , -0.16823611831060645 , 0.0 , 
         	0.16823611831060645 , 0.34657359027997264 , 0.5493061443340548 , 
         	0.8047189562170501 , 1.1989476363991853),
            COSH_VALUES VECTOR(11) CONSTANT(74.20994852478785 , 27.308232836016487, 
         	10.067661995777765 , 3.7621956910836314 , 1.5430806348152437 , 
         	1.0 , 1.5430806348152437 , 3.7621956910836314 , 
         	10.067661995777765 , 27.308232836016487 , 74.20994852478785),
            SINH_VALUES VECTOR(11) CONSTANT(-74.20321057778875, -27.28991719712775, 
         	-10.017874927409903 , -3.626860407847019 , -1.1752011936438014 , 
         	0.0 , 1.1752011936438014 , 3.626860407847019 , 
         	10.017874927409903 , 27.28991719712775 , 74.20321057778875),
            EXP_VALUES VECTOR(11) CONSTANT(0.006737946999085467 , 
         	0.01831563888873418 , 0.049787068367863944 , 0.1353352832366127, 
         	0.36787944117144233 , 1.0 , 2.718281828459045 , 
         	7.38905609893065 , 20.085536923187668 , 54.598150033144236 , 
         	148.4131591025766),
            SQRT_VALUES VECTOR(11) CONSTANT(0.0 , 1.0 , 1.4142135623730951 , 
         	1.7320508075688772 , 2.0 , 2.23606797749979 , 2.449489742783178, 
         	2.6457513110645907 , 2.8284271247461903 , 3.0 , 
         	3.1622776601683795),
            TAN_VALUES VECTOR(11) CONSTANT(3.380515006246586 , -1.1578212823495775, 
         	0.1425465430742778 , 2.185039863261519 , -1.5574077246549023 , 
         	0.0 , 1.5574077246549023 , -2.185039863261519 , 
         	-0.1425465430742778 , 1.1578212823495775 , -3.380515006246586),
            TANH_VALUES VECTOR(11) CONSTANT(-0.9999092042625951 , 
         	-0.999329299739067 , -0.9950547536867305 , -0.9640275800758169 , 
         	-0.7615941559557649 , 0.0 , 0.7615941559557649 , 
         	0.9640275800758169 , 0.9950547536867305 , 0.999329299739067 , 
         	0.9999092042625951);
    DECLARE V VECTOR(5), V2 VECTOR(5), SUMM SCALAR, M MATRIX(5,5), MT MATRIX(5,5);
    DECLARE B5 BIT(5), B7 BIT(7);
    DECLARE A2 ARRAY(2), A7 ARRAY(7), A12 ARRAY(12), A10V5 ARRAY(10) VECTOR(5);
 
    WRITE(6) ;
    WRITE(6) 'Some unarrayed RTL tests.';
    
    IF ABS(-5.2) = 5.2 AND ABS(0) = 0 AND ABS(153) = 153 THEN BB = TRUE;
    ELSE BB = FALSE;
    CALL PRINT_RESULT('ABS', BB) ASSIGN(ERRORS, TOTAL);
    IF CEILING(-5.2) = -5 AND CEILING(-3.9) = -3 AND CEILING(0.0) = 0
       AND CEILING(1.1) = 2 AND CEILING(5.9) = 6 THEN BB = TRUE;
    ELSE BB = FALSE;
    CALL PRINT_RESULT('CEILING', BB) ASSIGN(ERRORS, TOTAL);
    /* The documentation does not give any direct information about how DIV
       behaves with negative numbers.  If we supposed (which again, the
       documentation does not tell us) that DIV and REMAINDER are mutually
       consistent, then we would expect that for any integers N and M, we'd
       have
        		N = M DIV(N, M) + REMAINDER(N, M)
       The documentation does tell us that 
       		REMAINDER(-5,3) = -2
       		REMAINDER(5, -3) = 2
       		REMAINDER(-5, -3) = -2
       So we can test for that, and test that DIV is consistent with 
       REMAINDER. */ 
    BB = TRUE;
    IF REMAINDER(5,3) ~= 2 OR REMAINDER(-5,3) ~= -2 OR REMAINDER(5,-3) ~= 2 OR
       REMAINDER(-5,-3) ~= -2 THEN DO;
          BB = FALSE;
          WRITE(6) 'REMAINDER ERROR:', REMAINDER(5,3), REMAINDER(-5,3),
             	REMAINDER(5,-3), REMAINDER(-5,-3);
       END;
    DO FOR TEMPORARY N = -10 TO 10; DO FOR TEMPORARY M = -10 TO 10;
       IF M ~= 0 THEN IF N ~= M DIV(N, M) + REMAINDER(N, M) THEN DO;
          BB = FALSE;
          WRITE(6) 'DIV/REMAINDER ERROR:', N, M, DIV(N,M), REMAINDER(N,M);
       END;
    END; END;
    CALL PRINT_RESULT('DIV and REMAINDER', BB) ASSIGN(ERRORS, TOTAL);
    BB = TRUE;
    IF FLOOR(-2.6) ~= -3 OR FLOOR(-2.2) ~= -3 OR FLOOR(-3.0) ~= -3 OR
       FLOOR(0) ~= 0 OR FLOOR(2.0) ~= 2 OR FLOOR(2.2) ~= 2 OR FLOOR(2.99) ~= 2
       THEN BB = FALSE;
    CALL PRINT_RESULT('FLOOR', BB) ASSIGN(ERRORS, TOTAL);
    IF MIDVAL(1,1,1) ~= 1 OR MIDVAL(1,1,2) ~= 1 OR MIDVAL(1,1,3) ~= 1 OR
       MIDVAL(1,2,1) ~= 1 OR MIDVAL(1,2,2) ~= 2 OR MIDVAL(1,2,3) ~= 2 OR
       MIDVAL(1,3,1) ~= 1 OR MIDVAL(1,3,2) ~= 2 OR MIDVAL(1,3,3) ~= 3 OR
       MIDVAL(2,1,1) ~= 1 OR MIDVAL(2,1,2) ~= 2 OR MIDVAL(2,1,3) ~= 2 OR
       MIDVAL(2,2,1) ~= 2 OR MIDVAL(2,2,2) ~= 2 OR MIDVAL(2,2,3) ~= 2 OR
       MIDVAL(2,3,1) ~= 2 OR MIDVAL(2,3,2) ~= 2 OR MIDVAL(2,3,3) ~= 3 OR
       MIDVAL(3,1,1) ~= 1 OR MIDVAL(3,1,2) ~= 2 OR MIDVAL(3,1,3) ~= 3 OR
       MIDVAL(3,2,1) ~= 2 OR MIDVAL(3,2,2) ~= 2 OR MIDVAL(3,2,3) ~= 3 OR
       MIDVAL(3,3,1) ~= 3 OR MIDVAL(3,3,2) ~= 3 OR MIDVAL(3,3,3) ~= 3 
       THEN BB = FALSE;
    CALL PRINT_RESULT('MIDVAL', BB) ASSIGN(ERRORS, TOTAL);
    BB = TRUE;
    IF MOD(5,3) ~= 2 OR MOD(5,-3) ~= 2 OR MOD(-5,3) ~= 1 OR MOD(-5,-3) ~= 1 
    THEN BB = FALSE;
    IF NOT REALLY_CLOSE_TO(MOD(-5,2.1), 1.3) THEN BB = FALSE;
    CALL PRINT_RESULT('MOD', BB) ASSIGN(ERRORS, TOTAL);
    BB = TRUE;
    IF NOT ODD(5) OR ODD(4) OR NOT ODD(3) OR ODD(2) OR NOT ODD(1) OR ODD(0) OR
       NOT ODD(-1) OR ODD(-2) OR NOT ODD(-3) OR 
       ODD(1.8) OR NOT ODD(1.2) OR ODD(-1.9) OR NOT ODD(-2.9) THEN BB = FALSE;
    CALL PRINT_RESULT('ODD', BB) ASSIGN(ERRORS, TOTAL);
    BB = TRUE;
    IF ROUND(-5.2) ~= -5 OR ROUND(-4.7) ~= -5 OR ROUND(-4.5) ~= -5 OR
          ROUND(-4.2) ~= -4 OR ROUND(-3) ~= -3 OR ROUND(-2.8) ~= -3 OR
          ROUND(-3.5) ~= -4 OR ROUND(-2) ~= -2 OR ROUND (-1) ~= -1 OR
          ROUND(0) ~= 0 OR ROUND(1) ~= 1 OR ROUND(1.2) ~= 1 OR ROUND(1.5) ~= 2 OR
          ROUND(2.2) ~= 2 OR ROUND(2.5) ~= 3 OR ROUND (3.2) ~= 3 THEN BB = FALSE;
    CALL PRINT_RESULT('ROUND', BB) ASSIGN(ERRORS, TOTAL);
    BB = TRUE;
    IF SIGN(-1.5) ~= -1 OR SIGN(0) ~= 1 OR SIGN(1.5) ~= 1 THEN BB = FALSE;
    CALL PRINT_RESULT('SIGN', BB) ASSIGN(ERRORS, TOTAL);
    BB = TRUE;
    IF SIGNUM(-1.5) ~= -1 OR SIGNUM(0) ~= 0 OR SIGNUM(1.5) ~= 1 THEN BB = FALSE;
    CALL PRINT_RESULT('SIGNUM', BB) ASSIGN(ERRORS, TOTAL);
    BB = TRUE;
    IF TRUNCATE(-3.4) ~= -3 OR TRUNCATE(7.8) ~= 7 THEN BB = FALSE;
    CALL PRINT_RESULT('TRUNCATE', BB) ASSIGN(ERRORS, TOTAL);
    BB = TRUE;
    DO FOR TEMPORARY I = 1 TO 6;
       IF NOT REALLY_CLOSE_TO(ARCCOS(0.2 (I-1)), ARCCOS_VALUES$I) THEN BB = FALSE;
    END;
    CALL PRINT_RESULT('ARCCOS', BB) ASSIGN(ERRORS, TOTAL);
    BB = TRUE;
    DO FOR TEMPORARY I = 2 TO 5;
       IF NOT REALLY_CLOSE_TO(ARCCOSH(I), ARCCOSH_VALUES$(I-1)) THEN BB = FALSE;
    END;
    CALL PRINT_RESULT('ARCCOSH', BB) ASSIGN(ERRORS, TOTAL);
    BB = TRUE;
    DO FOR TEMPORARY I = 1 TO 6;
       IF NOT REALLY_CLOSE_TO(ARCSIN(0.2 (I-1)), ARCSIN_VALUES$I) THEN BB = FALSE;
    END;
    CALL PRINT_RESULT('ARCSIN', BB) ASSIGN(ERRORS, TOTAL);
    BB = TRUE;
    DO FOR TEMPORARY I = -5 TO 5;
       IF NOT REALLY_CLOSE_TO(ARCSINH(I), ARCSINH_VALUES$(I+6)) THEN BB = FALSE;
    END;
    CALL PRINT_RESULT('ARCSINH', BB) ASSIGN(ERRORS, TOTAL);
 
    BB = TRUE;
    DO FOR TEMPORARY I = -2 TO 2; DO FOR TEMPORARY J = -2 TO 2;
       IF I ~= 0 OR J ~= 0 
       THEN IF NOT REALLY_CLOSE_TO(ARCTAN2(I, J), ARCTAN2_VALUES$(I+3, J+3)) 
       THEN DO;
          BB = FALSE;
          WRITE(6) 'ARCTAN2 problem: ', I, J, 
          		ARCTAN2(I, J), ARCTAN2_VALUES$(I+3, J+3);
       END;
    END; END;
    CALL PRINT_RESULT('ARCTAN2', BB) ASSIGN(ERRORS, TOTAL);
    BB = TRUE;
    DO FOR TEMPORARY I = -5 TO 5;
       IF NOT REALLY_CLOSE_TO(ARCTAN(I), ARCTAN_VALUES$(I+6)) THEN BB = FALSE;
    END;
    CALL PRINT_RESULT('ARCTAN', BB) ASSIGN(ERRORS, TOTAL);
    BB = TRUE;
    DO FOR TEMPORARY I = -5 TO 5;
       IF NOT REALLY_CLOSE_TO(ARCTANH(I/6), ARCTANH_VALUES$(I+6)) THEN BB = FALSE;
    END;
    CALL PRINT_RESULT('ARCTANH', BB) ASSIGN(ERRORS, TOTAL);
       
    BB = TRUE;
    DO FOR TEMPORARY I = 1 TO 17;
       IF NOT REALLY_CLOSE_TO(COS(DEGREES_TO_RADIANS(COSINE_ANGLES$I)), 
                              COSINE_VALUES$I)
       THEN DO; BB = FALSE; EXIT; END;
    END; 
    CALL PRINT_RESULT('COS', BB) ASSIGN(ERRORS, TOTAL);
    BB = TRUE;
    DO FOR TEMPORARY THETA = 0 TO 360;
        IF NOT REALLY_CLOSE_TO(SIN(DEGREES_TO_RADIANS(THETA)),
        			    COS(DEGREES_TO_RADIANS(THETA-90))) 
        THEN DO;
           BB = FALSE;
           EXIT;
        END;
    END;
    CALL PRINT_RESULT('SIN', BB) ASSIGN(ERRORS, TOTAL);
 
    BB = TRUE;
    DO FOR TEMPORARY I = -5 TO 5;
       IF NOT REALLY_CLOSE_TO(COSH(I), COSH_VALUES$(I+6)) THEN BB = FALSE;
    END;
    CALL PRINT_RESULT('COSH', BB) ASSIGN(ERRORS, TOTAL);
    BB = TRUE;
    DO FOR TEMPORARY I = -5 TO 5;
       IF NOT REALLY_CLOSE_TO(SINH(I), SINH_VALUES$(I+6)) THEN BB = FALSE;
    END;
    CALL PRINT_RESULT('SINH', BB) ASSIGN(ERRORS, TOTAL);
    BB = TRUE;
    DO FOR TEMPORARY I = -5 TO 5;
       IF NOT REALLY_CLOSE_TO(EXP(I), EXP_VALUES$(I+6)) THEN BB = FALSE;
    END;
    CALL PRINT_RESULT('EXP', BB) ASSIGN(ERRORS, TOTAL);
    BB = TRUE;
    DO FOR TEMPORARY I = -5 TO 5;
       IF NOT REALLY_CLOSE_TO(I, LOG(EXP(I))) THEN BB = FALSE;
    END;
    CALL PRINT_RESULT('LOG', BB) ASSIGN(ERRORS, TOTAL);
    BB = TRUE;
    DO FOR TEMPORARY I = -5 TO 5;
       IF NOT REALLY_CLOSE_TO(SQRT(I+5), SQRT_VALUES$(I+6)) THEN BB = FALSE;
    END;
    CALL PRINT_RESULT('SQRT', BB) ASSIGN(ERRORS, TOTAL);
    BB = TRUE;
    DO FOR TEMPORARY I = -5 TO 5;
       IF NOT REALLY_CLOSE_TO(TAN(I), TAN_VALUES$(I+6)) THEN BB = FALSE;
    END;
    CALL PRINT_RESULT('TAN', BB) ASSIGN(ERRORS, TOTAL);
    BB = TRUE;
    DO FOR TEMPORARY I = -5 TO 5;
       IF NOT REALLY_CLOSE_TO(TANH(I), TANH_VALUES$(I+6)) THEN BB = FALSE;
    END;
    CALL PRINT_RESULT('TANH', BB) ASSIGN(ERRORS, TOTAL);
 
    BB = TRUE;
    BB2 = TRUE;
    DO FOR TEMPORARY I = 1 TO 10;
    	SUMM = 0;
 	DO FOR TEMPORARY J = 1 TO 5;
 		V$J = RANDOM;
 		SUMM = SUMM + (V$J)**2;
 	END;
 	SUMM = SQRT(SUMM);
 	IF NOT REALLY_CLOSE_TO(ABVAL(V), SUMM) THEN BB = FALSE;
 	IF SUMM ~= 0 THEN DO;
 		V2 = UNIT(V);
 		V = V / SUMM;
 		IF NOT REALLY_CLOSE_TO(0, ABVAL(V - V2)) THEN BB2 = FALSE;
 	END;
    END;
    CALL PRINT_RESULT('ABVAL', BB) ASSIGN(ERRORS, TOTAL);
    CALL PRINT_RESULT('UNIT', BB2) ASSIGN(ERRORS, TOTAL);
    
    BB = TRUE;
    BB2 = TRUE;
    DO FOR TEMPORARY I = 1 TO 10;
       SUMM = 0;
       DO FOR TEMPORARY J = 1 TO 5;
          DO FOR TEMPORARY K = 1 TO 5;
             M$(J,K) = RANDOM;
             MT$(K,J) = M$(J,K);
             IF J = K THEN SUMM = SUMM + M$(J,K);
          END;
       END;
       IF NOT REALLY_CLOSE_TO(SUMM, TRACE(M)) THEN BB = FALSE;
       IF TRANSPOSE(M) ~= MT THEN BB2 = FALSE;
    END;
    CALL PRINT_RESULT('TRACE', BB) ASSIGN(ERRORS, TOTAL);
    CALL PRINT_RESULT('TRANSPOSE', BB2) ASSIGN(ERRORS, TOTAL);
    WRITE(6) 'NOTE: DET, INVERSE, MAX, MIN, PROD, SUM functions tested later.';
    
    BB = TRUE;
    B5 = BIN'0000000000'; B7 = BIN'0000000000';
    IF XOR(B5, B7) ~= BIN'0' THEN BB = FALSE;
    B5 = BIN'0000000000'; B7 = BIN'1111111111';
    IF XOR(B5, B7) ~= BIN'1111111' THEN BB = FALSE;
    B5 = BIN'1111111111'; B7 = BIN'0000000000';
    IF XOR(B5, B7) ~= BIN'0011111' THEN BB = FALSE;
    B5 = BIN'1111111111'; B7 = BIN'1111111111';
    IF XOR(B5, B7) ~= BIN'1100000' THEN BB = FALSE;
    B5 = BIN'1101'; B7 = BIN'1010101';
    IF XOR(B5, B7) ~= BIN'1011000' THEN BB = FALSE;
    CALL PRINT_RESULT('XOR', BB) ASSIGN(ERRORS, TOTAL);
    
    BB = TRUE;
    IF INDEX('I AM A POTATO', 'POT') ~= 8 THEN BB = FALSE;
    IF INDEX('I AM A POTATO', 'SNORKEL') ~= 0 THEN BB = FALSE;
    IF INDEX('I AM A POTATO', 'I') ~= 1 THEN BB = FALSE;
    CALL PRINT_RESULT('INDEX', BB) ASSIGN(ERRORS, TOTAL);
    BB = TRUE;
    IF LENGTH('') ~= 0 THEN BB = FALSE;
    IF LENGTH('RONALD') ~= 6 THEN BB = FALSE;
    IF LENGTH('SUPERSTRUCTURE') ~= 14 THEN BB = FALSE;
    CALL PRINT_RESULT('LENGTH', BB) ASSIGN(ERRORS, TOTAL);
    BB = TRUE;
    IF LJUST('', 10) ~= '          ' THEN BB = FALSE;
    IF LJUST('RONALD', 10) ~= 'RONALD    ' THEN BB = FALSE;
    IF LJUST('RONALD', 5) ~= 'RONAL' THEN BB = FALSE;
    /* Need also the check that the line above signaled an error, but I haven't
       implemented anything to do with that yet. */
    CALL PRINT_RESULT('LJUST', BB) ASSIGN(ERRORS, TOTAL);
    BB = TRUE;
    IF RJUST('', 10) ~= '          ' THEN BB = FALSE;
    IF RJUST('RONALD', 10) ~= '    RONALD' THEN BB = FALSE;
    IF RJUST('RONALD', 5) ~= 'RONAL' THEN BB = FALSE;
    /* Need also the check that the line above signaled an error, but I haven't
       implemented anything to do with that yet. */
    CALL PRINT_RESULT('RJUST', BB) ASSIGN(ERRORS, TOTAL);
    BB = TRUE;
    IF TRIM('') ~= '' THEN BB = FALSE;
    IF TRIM('RONALD ') ~= 'RONALD' THEN BB = FALSE;
    IF TRIM('RONALD      ') ~= 'RONALD' THEN BB = FALSE;
    IF TRIM(' RONALD') ~= 'RONALD' THEN BB = FALSE;
    IF TRIM('    RONALD') ~= 'RONALD' THEN BB = FALSE;
    IF TRIM('   RONALD     ') ~= 'RONALD' THEN BB = FALSE;
    CALL PRINT_RESULT('TRIM', BB) ASSIGN(ERRORS, TOTAL);
 
    WRITE(6) 'Note that there is no apparent way to provide an automated';
    WRITE(6) 'check for most time-related RTL functions.  For informational';
    WRITE(6) 'purposes only, here are some current readings for them:';
    WRITE(6) 'CLOCKTIME =', CLOCKTIME, ', DATE =', DATE, ', RUNTIME =', RUNTIME;
    WRITE(6) 'Note that the HAL/S runtime-error system has not yet been';
    WRITE(6) 'implemented, and therefore cannot be tested at present.  Here';
    WRITE(6) 'are the outputs of the error-reporting functions:';
    WRITE(6) 'ERRGRP =', ERRGRP, ', ERRNUM =', ERRNUM;
    
    MONTE_PI: FUNCTION(N) SCALAR;
       DECLARE N INTEGER, HIT INTEGER;
       HIT = 0;
       DO FOR TEMPORARY I = 1 TO N;
     	  IF RANDOM**2 + RANDOM**2 <= 1 THEN HIT = HIT + 1;
       END;
       RETURN ABS(PI - 4 HIT / N);
    CLOSE MONTE_PI;
    BB = FALSE;
    DO FOR TEMPORARY I = 1 TO 20;
       IF MONTE_PI(1000) < 0.1 THEN DO; BB = TRUE; EXIT; END;
    END;
    CALL PRINT_RESULT('RANDOM', BB) ASSIGN(ERRORS, TOTAL);
    
    /* Swiped this from Wikipedia's Python code in the article called
       "Algorithms for calculating variance". */
    SHIFTED_VARIANCE: PROCEDURE(N) ASSIGN(MEAN, VARIANCE);
       DECLARE N INTEGER, MEAN SCALAR, VARIANCE SCALAR;
       DECLARE K SCALAR, EX SCALAR, EX2 SCALAR;
       K = RANDOMG;
       EX, EX2 = 0.0;
       DO FOR TEMPORARY I = 2 TO N;
          TEMPORARY X;
          X = RANDOMG;
          EX = EX + X - K;
          EX2 = EX2 + (X - K)**2;
       END;
       MEAN = EX / N + K;
       VARIANCE = (EX2 - EX**2 / N) / (N-1);
    CLOSE SHIFTED_VARIANCE;
    BB = FALSE;
    DO FOR TEMPORARY I = 1 TO 20;
       TEMPORARY MEAN, VARIANCE;
       CALL SHIFTED_VARIANCE(1000) ASSIGN(MEAN, VARIANCE);
       IF ABS(MEAN) < 0.1 AND ABS(VARIANCE - 1) < 0.1 THEN DO;
          BB = TRUE;
          EXIT;
       END;
    END;
    CALL PRINT_RESULT('RANDOMG', BB) ASSIGN(ERRORS, TOTAL);
    
    BB = TRUE;
    IF SHL(-2, 2) ~= -8 THEN BB = FALSE;
    IF SHL(1, 1) ~= 2 OR SHL(2, 1) ~= 4 OR SHL(1, 2) ~= 4 THEN BB = FALSE;
    CALL PRINT_RESULT('SHL', BB) ASSIGN(ERRORS, TOTAL);
    BB = TRUE;
    IF SHR(-4,2) ~= -1 THEN BB = FALSE;
    IF SHR(8, 1) ~= 4 OR SHR(4, 1) ~= 2 OR SHR(8, 2) ~= 2 THEN BB = FALSE;
    CALL PRINT_RESULT('SHR', BB) ASSIGN(ERRORS, TOTAL);
    
    BB = TRUE;
    IF SIZE(A2) ~= 2 OR SIZE(A7) ~= 7 OR SIZE(A12) ~= 12 OR SIZE(A10V5) ~= 10
    THEN BB = FALSE;
    CALL PRINT_RESULT('SIZE', BB) ASSIGN(ERRORS, TOTAL);
 
 CLOSE RTL_TESTS;
 CALL RTL_TESTS ASSIGN(ERRORS, TOTAL);

 /*--------------------------------------------------------------------------*/
 
  /* Several functions for checking if a matrix is the identity. */
  IS_IDENTITY2X2: FUNCTION(M) BOOLEAN;
     DECLARE M MATRIX(2,2);
     DO FOR TEMPORARY I = 1 TO 2; DO FOR TEMPORARY J = 1 TO 2;
        IF I = J THEN DO;
     	   IF NOT REALLY_CLOSE_TO(M$(I,J), 1) THEN RETURN FALSE;
        END;
        ELSE DO;
     	   IF NOT REALLY_CLOSE_TO(M$(I,J), 0) THEN RETURN FALSE;
        END;
     END; END;
     RETURN TRUE;
  CLOSE IS_IDENTITY2X2;
  
  IS_IDENTITY3X3: FUNCTION(M) BOOLEAN;
     DECLARE M MATRIX(3,3);
     DO FOR TEMPORARY I = 1 TO 3; DO FOR TEMPORARY J = 1 TO 3;
        IF I = J THEN DO;
     	   IF NOT REALLY_CLOSE_TO(M$(I,J), 1) THEN DO;
     	   	/*WRITE(6) 'M =', M;*/
     	   	RETURN FALSE;
     	   END;
        END;
        ELSE DO;
     	   IF NOT REALLY_CLOSE_TO(M$(I,J), 0) THEN DO;
     	      /*WRITE(6) 'M =', M;*/
     	      RETURN FALSE;
     	   END;
        END;
     END; END;
     RETURN TRUE;
  CLOSE IS_IDENTITY3X3;
  
  IS_IDENTITY4X4: FUNCTION(M) BOOLEAN;
     DECLARE M MATRIX(4,4);
     DO FOR TEMPORARY I = 1 TO 4; DO FOR TEMPORARY J = 1 TO 4;
        IF I = J THEN DO;
     	   IF NOT REALLY_CLOSE_TO(M$(I,J), 1) THEN RETURN FALSE;
        END;
        ELSE DO;
     	   IF NOT REALLY_CLOSE_TO(M$(I,J), 0) THEN RETURN FALSE;
        END;
     END; END;
     RETURN TRUE;
  CLOSE IS_IDENTITY4X4;
  
  IS_IDENTITY5X5: FUNCTION(M) BOOLEAN;
     DECLARE M MATRIX(5,5);
     DO FOR TEMPORARY I = 1 TO 5; DO FOR TEMPORARY J = 1 TO 5;
        IF I = J THEN DO;
     	   IF NOT REALLY_CLOSE_TO(M$(I,J), 1) THEN RETURN FALSE;
        END;
        ELSE DO;
     	   IF NOT REALLY_CLOSE_TO(M$(I,J), 0) THEN RETURN FALSE;
        END;
     END; END;
     RETURN TRUE;
  CLOSE IS_IDENTITY5X5;
  
  /* The following function encapsulates a lot of vector and matrix tests.
     It was originally a separate program (see DATATYPES-029.hal), but I 
     decided to integrated it into the validation tests, and thought it best
     to make it a self-contained procedure to avoid collisions between the 
     namespaces. */
  DATATYPES_029:
  PROCEDURE ASSIGN(ERRORS, TOTAL);
     DECLARE ERRORS INTEGER, TOTAL INTEGER;
     DECLARE BB BOOLEAN;
     DECLARE S SCALAR INITIAL(5);
     DECLARE I INTEGER INITIAL(6);
     DECLARE V VECTOR INITIAL(10, 11, 12), VV VECTOR;
     DECLARE M MATRIX INITIAL(20, 21, 22, 23, 24, 25, 26, 27, 28), MM MATRIX;
     DECLARE VECTOR, X INITIAL(5, 6, 2), Y INITIAL(1, 1, 1);
     DECLARE DUMMY2 VECTOR(2), DUMMY3 VECTOR(3);
     
     DECLARE M2 MATRIX(2,2) INITIAL(0, 0.3, -0.1, 0.4);
     DECLARE DUMMY2X2 MATRIX(2,2), DUMMY3X3 MATRIX(3,3);
     DECLARE V1 VECTOR(3) INITIAL(1.0, -1.0, 1.0);
     DECLARE V2 VECTOR(2) INITIAL(0.5, 0.6);
     DECLARE DUMMY2X3 MATRIX(2,3), DUMMY3X2 MATRIX(3,2);
     DECLARE M1A MATRIX(2,3) INITIAL(1.0, 1.0, 2.0, 0.5, -0.5, 1.0);
     DECLARE M2A MATRIX(3,2) INITIAL(0.0, 0.5, 0.0, 1.0, 0.0, 1.0);
     DECLARE M3 MATRIX(3,2) INITIAL(0.5, 1.0, 0.0, 1.0, 0.2, 0.4);
     
     DECLARE A2A MATRIX(2,2) INITIAL(3, 7, 1, -4);
     DECLARE MATRIX(2,2), A2B INITIAL(0.5, 1.0, -0.5, 0.0), A2C;
     DECLARE A3A MATRIX(3,3) INITIAL(-2,2,-3,-1,1,3,2,0,-1);
     DECLARE A3I MATRIX(3,3);
     DECLARE A4A MATRIX(4,4) INITIAL(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
     DECLARE A4B MATRIX(4,4) INITIAL(1,2,3,4,8,5,6,7,9,12,10,11,13,14,16,15);
     DECLARE A4I MATRIX(4,4), A4C MATRIX(4,4);
     DECLARE A5A MATRIX(5,5) INITIAL(1,2,3,4,1,8,5,6,7,2,9,12,10,11,3,
     				     13,14,16,15,4,10,8,6,4,2);
     DECLARE A5I MATRIX(5,5);
     DECLARE A6A MATRIX(2,2) INITIAL(-1, 1.5, 1, -1);
     DECLARE A6B MATRIX(2,2);
     DECLARE A23A MATRIX(2,3) INITIAL(1.0, 0.0, 3.0, 2.0, 0.0, 4.0);
     DECLARE DUMMY4X4 MATRIX(4,4), DUMMY5X5 MATRIX(5,5);
     
     DECLARE TST CHARACTER(20);
    
  WRITE(6);
  WRITE(6) 'See "Programming in HAL/S", p. 2-9.';
  WRITE(6) 'Some vector and matrix arithmetic tests.';

  IF V$1 = 10 AND V$2 = 11 AND V$3 = 12 THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('VECTOR INITIALIZATION', BB) ASSIGN(ERRORS, TOTAL);
  
E	 -   -
M    S = V . V;
  IF S = 365 THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('DOT PRODUCT', BB) ASSIGN(ERRORS, TOTAL);
  
E         -   -
M    VV = V * V;
  IF VV$1 = 0 AND VV$2 = 0 AND VV$3 = 0 THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('CROSS PRODUCT', BB) ASSIGN(ERRORS, TOTAL);
  
  IF X$1 = 5 AND X$2 = 6 AND X$3 = 2 AND Y$1 = 1 AND Y$2 = 1 AND Y$3 = 1
  THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('VECTOR INITIALIZATION 2', BB) ASSIGN(ERRORS, TOTAL);
  
  S = X.Y;
  IF S = 13 THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('DOT PRODUCT 2', BB) ASSIGN(ERRORS, TOTAL);

  VV = X * Y;
  IF VV$1 = 4 AND VV$2 = -3 AND VV$3 = -1 THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('CROSS PRODUCT 2', BB) ASSIGN(ERRORS, TOTAL);
  
  BB = TRUE;
  DO;
     TEMPORARY K INTEGER;
     K = 20;
     DO FOR TEMPORARY I = 1 TO 3; DO FOR TEMPORARY J = 1 TO 3;
        IF M$(I,J) ~= K THEN BB = FALSE;
        K = K + 1;
     END; END;
  END;
  CALL PRINT_RESULT('MATRIX INITIALIZATION', BB) ASSIGN(ERRORS, TOTAL);
  
  TST = 'VECTOR TIMES MATRIX: ';
E    -    - *
M    VV = V M;
  IF VV$1 = 765 AND VV$2 = 798 AND VV$3 = 831 THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('VECTOR TIMES MATRIX', BB) ASSIGN(ERRORS, TOTAL);  
  IF NOT BB THEN WRITE(6) 'VV =', VV;
  
E    *    - -
M    MM = V V;
  BB = TRUE;
  DO FOR TEMPORARY I = 1 TO 3; DO FOR TEMPORARY J = 1 TO 3;
     IF MM$(I,J) ~= (9 + I)(9 + J) THEN BB = FALSE;
  END; END;
  CALL PRINT_RESULT('VECTOR OUTER PRODUCT', BB) ASSIGN(ERRORS, TOTAL);
  
  S = 5;
E    *    * *
M    MM = M M;
  IF MM$(1,1) = 1455 AND MM$(1,2) = 1518 AND MM$(1,3) = 1581 AND
     MM$(2,1) = 1662 AND MM$(2,2) = 1734 AND MM$(2,3) = 1806 AND
     MM$(3,1) = 1869 AND MM$(3,2) = 1950 AND MM$(3,3) = 2031
  THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('MATRIX MULTIPLICATION', BB) ASSIGN(ERRORS, TOTAL);
  IF NOT BB THEN WRITE(6) 'MM =', MM;
  
E    -    -
M    VV = V S;
  IF VV$1 = 50 AND VV$2 = 55 AND VV$3 = 60 THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('VECTOR TIMES SCALAR', BB) ASSIGN(ERRORS, TOTAL);
  IF NOT BB THEN WRITE(6) 'VV =', VV;
  
  WRITE(6) ;
  WRITE(6) 'See "HAL/S Programmers Guide", pp. 7-7 THROUGH 7-10.';
  I = 10; 
  CALL PRINT_RESULT('SCALAR TIMES INTEGER', REALLY_CLOSE_TO(1.5E-2 I, 0.15)) ASSIGN(ERRORS, TOTAL);
  
  S = 1.5;
  DUMMY2X2 = S M2;
  IF REALLY_CLOSE_TO(DUMMY2X2$(1,1), 0) AND REALLY_CLOSE_TO(DUMMY2X2$(1,2), 0.45) AND
     REALLY_CLOSE_TO(DUMMY2X2$(2,1), -0.15) AND REALLY_CLOSE_TO(DUMMY2X2$(2,2), 0.6)
  THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('SCALAR TIMES MATRIX', BB) ASSIGN(ERRORS, TOTAL);
  IF NOT BB THEN WRITE(6) 'S =', S, ', M2 =', M2, ', DUMMY2X2 =', DUMMY2X2;
  
  DUMMY2X2 = M2 S;
  IF REALLY_CLOSE_TO(DUMMY2X2$(1,1), 0) AND REALLY_CLOSE_TO(DUMMY2X2$(1,2), 0.45) AND
     REALLY_CLOSE_TO(DUMMY2X2$(2,1), -0.15) AND REALLY_CLOSE_TO(DUMMY2X2$(2,2), 0.6)
  THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('MATRIX TIMES SCALAR', BB) ASSIGN(ERRORS, TOTAL);

  DUMMY3X2 = V1 V2;
  IF REALLY_CLOSE_TO(DUMMY3X2$(1,1), 0.5) AND REALLY_CLOSE_TO(DUMMY3X2$(1,2), 0.6) AND
     REALLY_CLOSE_TO(DUMMY3X2$(2,1), -0.5) AND REALLY_CLOSE_TO(DUMMY3X2$(2,2), -0.6) AND
     REALLY_CLOSE_TO(DUMMY3X2$(3,1), 0.5) AND REALLY_CLOSE_TO(DUMMY3X2$(3,2), 0.6)
  THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('VECTOR OUTER PRODUCT 2', BB) ASSIGN(ERRORS, TOTAL);

  DUMMY2X3 = V2 V1;
  IF REALLY_CLOSE_TO(DUMMY2X3$(1,1), 0.5) AND REALLY_CLOSE_TO(DUMMY2X3$(1,2), -0.5) AND
     REALLY_CLOSE_TO(DUMMY2X3$(1,3), 0.5) AND REALLY_CLOSE_TO(DUMMY2X3$(2,1), 0.6) AND
     REALLY_CLOSE_TO(DUMMY2X3$(2,2), -0.6) AND REALLY_CLOSE_TO(DUMMY2X3$(2,3), 0.6)
  THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('VECTOR OUTER PRODUCT 3', BB) ASSIGN(ERRORS, TOTAL);
  IF NOT BB THEN WRITE(6) 'V2 =', V2, ', V1 =', V1, ', DUMMY2X3 =', DUMMY2X3;

  DUMMY2X2 = M1A M2A;
  IF REALLY_CLOSE_TO(DUMMY2X2$(1,1), 0.0) AND REALLY_CLOSE_TO(DUMMY2X2$(1,2), 3.5) AND
     REALLY_CLOSE_TO(DUMMY2X2$(2,1), 0) AND REALLY_CLOSE_TO(DUMMY2X2$(2,2), 0.75)
  THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('MATRIX MULTIPLICATION 2', BB) ASSIGN(ERRORS, TOTAL);

  DUMMY3X3 = M2A M1A;
  IF REALLY_CLOSE_TO(DUMMY3X3$(1,1), 0.25) AND REALLY_CLOSE_TO(DUMMY3X3$(1,2), -0.25) AND
     REALLY_CLOSE_TO(DUMMY3X3$(1,3), 0.5) AND REALLY_CLOSE_TO(DUMMY3X3$(2,1), 0.5) AND
     REALLY_CLOSE_TO(DUMMY3X3$(2,2), -0.5) AND REALLY_CLOSE_TO(DUMMY3X3$(2,3), 1.0) AND
     REALLY_CLOSE_TO(DUMMY3X3$(3,1), 0.5) AND REALLY_CLOSE_TO(DUMMY3X3$(3,2), -0.5) AND
     REALLY_CLOSE_TO(DUMMY3X3$(3,3), 1.0)
  THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('MATRIX MULTIPLICATION 3', BB) ASSIGN(ERRORS, TOTAL);
  
  DUMMY2 = V1 M3;
  IF REALLY_CLOSE_TO(DUMMY2$1, 0.7) AND REALLY_CLOSE_TO(DUMMY2$2, 0.4) 
  THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('VECTOR TIMES MATRIX 2', BB) ASSIGN(ERRORS, TOTAL);
  
  DUMMY3 = M3 V2;
  IF REALLY_CLOSE_TO(DUMMY3$1, 0.85) AND REALLY_CLOSE_TO(DUMMY3$2, 0.6) AND 
     REALLY_CLOSE_TO(DUMMY3$3, 0.34)
  THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('MATRIX TIMES VECTOR', BB) ASSIGN(ERRORS, TOTAL);
  
  WRITE(6);
  WRITE(6) 'Stuff related to matrix determinants.';
  IF REALLY_CLOSE_TO(DET(A2A), -19) THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('2X2 DETERMINANT', BB) ASSIGN(ERRORS, TOTAL);
  IF REALLY_CLOSE_TO(DET(A3A), 18) THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('3X3 DETERMINANT', BB) ASSIGN(ERRORS, TOTAL);
  IF REALLY_CLOSE_TO(DET(A4A), 0) THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('4X4 DETERMINANT', BB) ASSIGN(ERRORS, TOTAL);
  IF REALLY_CLOSE_TO(DET(A4B), -348) THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('4X4 DETERMINANT 2', BB) ASSIGN(ERRORS, TOTAL);
  IF REALLY_CLOSE_TO(DET(A5A), -240) THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('5X5 DETERMINANT', BB) ASSIGN(ERRORS, TOTAL);
  
  WRITE(6);
  WRITE(6) 'Stuff related to matrix inverses.';
  A6B = INVERSE(A6A);
  IF REALLY_CLOSE_TO(A6B$(1,1), 2.0) AND REALLY_CLOSE_TO(A6B$(1,2), 3.0) AND
     REALLY_CLOSE_TO(A6B$(2,1), 2.0) AND REALLY_CLOSE_TO(A6B$(2,2), 2.0)
  THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('2X2 INVERSE', BB) ASSIGN(ERRORS, TOTAL);
  CALL PRINT_RESULT('2X2 MATRIX TIMES INVERSE', IS_IDENTITY2X2(A6A A6B)) ASSIGN(ERRORS, TOTAL);
  CALL PRINT_RESULT('2X2 MATRIX TIMES INVERSE 2', IS_IDENTITY2X2(A6B A6A)) ASSIGN(ERRORS, TOTAL);

  A3I = INVERSE(A3A);
  CALL PRINT_RESULT('3X3 MATRIX TIMES INVERSE', IS_IDENTITY3X3(A3A A3I)) ASSIGN(ERRORS, TOTAL);
  CALL PRINT_RESULT('3X3 MATRIX TIMES INVERSE 2', IS_IDENTITY3X3(A3I A3A)) ASSIGN(ERRORS, TOTAL);

  A4I = INVERSE(A4B);
  A4C = A4B**(-1);
  CALL PRINT_RESULT('4X4 MATRIX TIMES INVERSE', IS_IDENTITY4X4(A4B A4I)) ASSIGN(ERRORS, TOTAL);
  CALL PRINT_RESULT('4X4 MATRIX TIMES INVERSE 2', IS_IDENTITY4X4(A4I A4B)) ASSIGN(ERRORS, TOTAL);
  CALL PRINT_RESULT('4X4 MATRIX TIMES INVERSE 3', IS_IDENTITY4X4(A4C A4B)) ASSIGN(ERRORS, TOTAL);

  A5I = INVERSE(A5A);
  CALL PRINT_RESULT('5X5 MATRIX TIMES INVERSE', IS_IDENTITY5X5(A5A A5I)) ASSIGN(ERRORS, TOTAL);
  CALL PRINT_RESULT('5X5 MATRIX TIMES INVERSE 2', IS_IDENTITY5X5(A5I A5A)) ASSIGN(ERRORS, TOTAL);

  WRITE(6);
  WRITE(6) 'Stuff related to matrix "exponentiation".';
  DUMMY2X2 = A2B**2;
  IF REALLY_CLOSE_TO(DUMMY2X2$(1,1), -.25) AND REALLY_CLOSE_TO(DUMMY2X2$(1,2), 0.5) AND
     REALLY_CLOSE_TO(DUMMY2X2$(2,1), -0.25) AND REALLY_CLOSE_TO(DUMMY2X2$(2,2), -0.5)
  THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('2X2 MATRIX **2', BB) ASSIGN(ERRORS, TOTAL);
  DUMMY2X2 = A2B**(-1);
  IF REALLY_CLOSE_TO(DUMMY2X2$(1,1), 0.0) AND REALLY_CLOSE_TO(DUMMY2X2$(1,2), -2.0) AND
     REALLY_CLOSE_TO(DUMMY2X2$(2,1), 1.0) AND REALLY_CLOSE_TO(DUMMY2X2$(2,2), 1.0)
  THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('2X2 MATRIX **(-1)', BB) ASSIGN(ERRORS, TOTAL);
  DUMMY2X2 = A2B**0;
  IF REALLY_CLOSE_TO(DUMMY2X2$(1,1), 1) AND REALLY_CLOSE_TO(DUMMY2X2$(1,2), 0) AND
     REALLY_CLOSE_TO(DUMMY2X2$(2,1), 0) AND REALLY_CLOSE_TO(DUMMY2X2$(2,2), 1)
  THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('2X2 MATRIX **0', BB) ASSIGN(ERRORS, TOTAL);
  DUMMY2X2 = A2B**T;
  IF REALLY_CLOSE_TO(DUMMY2X2$(1,1), 0.5) AND REALLY_CLOSE_TO(DUMMY2X2$(1,2), -0.5) AND
     REALLY_CLOSE_TO(DUMMY2X2$(2,1), 1.0) AND REALLY_CLOSE_TO(DUMMY2X2$(2,2), 0.0)
  THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('2X2 MATRIX **T', BB) ASSIGN(ERRORS, TOTAL);
  DUMMY3X2 = A23A**T;
  IF REALLY_CLOSE_TO(DUMMY3X2$(1,1), 1) AND REALLY_CLOSE_TO(DUMMY3X2$(1,2), 2) AND
     REALLY_CLOSE_TO(DUMMY3X2$(2,1), 0) AND REALLY_CLOSE_TO(DUMMY3X2$(2,2), 0) AND
     REALLY_CLOSE_TO(DUMMY3X2$(3,1), 3) AND REALLY_CLOSE_TO(DUMMY3X2$(3,2), 4)
  THEN BB = TRUE; ELSE BB = FALSE;
  CALL PRINT_RESULT('2X3 MATRIX **T', BB) ASSIGN(ERRORS, TOTAL);
  
  CLOSE DATATYPES_029;
  CALL DATATYPES_029 ASSIGN(ERRORS, TOTAL);
 
 /*--------------------------------------------------------------------------*/	
  INITIAL_AND_CONSTANT_032:
  PROCEDURE ASSIGN(ERRORS, TOTAL);
     DECLARE ERRORS INTEGER, TOTAL INTEGER;
     DECLARE X SCALAR INITIAL(0);
     DECLARE MAX_SPEED SCALAR INITIAL(14000);
     DECLARE FEET_TO_MILES SCALAR CONSTANT(1 / 5280);
     DECLARE SEC_TO_HR CONSTANT(60 (60));
     DECLARE MAX_MPH INITIAL(14000 FEET_TO_MILES / SEC_TO_HR);
     DECLARE BB BOOLEAN;
     
     WRITE(6) ;
     WRITE(6) 'See "Programming in HAL/S" p. 2-12.';
     IF X = 0 THEN BB = TRUE; ELSE BB = FALSE;
     CALL PRINT_RESULT('SCALAR INITIAL', BB) ASSIGN(ERRORS, TOTAL);
     IF MAX_SPEED = 14000 THEN BB = TRUE; ELSE BB = FALSE;
     CALL PRINT_RESULT('SCALAR INITIAL 2', BB) ASSIGN(ERRORS, TOTAL);
     IF REALLY_CLOSE_TO(FEET_TO_MILES, 189.393939394E-6) 
     THEN BB = TRUE; ELSE BB = FALSE;
     CALL PRINT_RESULT('SCALAR INITIAL 3', BB) ASSIGN(ERRORS, TOTAL);
     IF SEC_TO_HR = 3600 THEN BB = TRUE; ELSE BB = FALSE;
     CALL PRINT_RESULT('SCALAR INITIAL 4', BB) ASSIGN(ERRORS, TOTAL);
     IF REALLY_CLOSE_TO(MAX_MPH, 736.531986532E-6)
     THEN BB = TRUE; ELSE BB = FALSE;
     CALL PRINT_RESULT('SCALAR INITIAL 5', BB) ASSIGN(ERRORS, TOTAL);
     
  CLOSE INITIAL_AND_CONSTANT_032;
  CALL INITIAL_AND_CONSTANT_032 ASSIGN(ERRORS, TOTAL);
 
 /*--------------------------------------------------------------------------*/	
  WRITE(6) ;
  WRITE(6) 'See "Programming in HAL/S" pp. 3-9 and 3-10."'; 
  ROWS_047:
  PROCEDURE ASSIGN(ERRORS, TOTAL);
     DECLARE ERRORS INTEGER, TOTAL INTEGER;
     DECLARE MM MATRIX INITIAL(11, 12, 13, 21, 22, 23, 31, 32, 33), 
     	     M MATRIX,
     	     TEMP VECTOR,
     	     C SCALAR INITIAL(2),
     	     I INTEGER INITIAL(1),
     	     J INTEGER INITIAL(2),
     	     BB BOOLEAN;
     		
C  MULTIPLY A ROW BY A (NONZERO) CONSTANT:

  M = MM;
E    -        -
M    M    = C MM   ;
S     I,*       I,*
  BB = TRUE;
  DO FOR TEMPORARY J = 1 TO 3;
     IF M$(1,J) ~= 2 (10 + J) THEN BB = FALSE;
  END;
  DO FOR TEMPORARY I = 2 TO 3; DO FOR TEMPORARY J = 1 TO 3;
     IF M$(I,J) ~= 10 I + J THEN BB = FALSE;
  END; END;
  CALL PRINT_RESULT('SCALAR TIMES MATRIX ROW', BB) ASSIGN(ERRORS, TOTAL);
     
C  ADD A CONSTANT MULTIPLE OF ROW J TO ROW I:

  M = MM;
E    -      -         -
M    M    = MM    + C MM   ;
S     I,*     I,*       J,*
  BB = TRUE;
  DO FOR TEMPORARY J = 1 TO 3;
     IF M$(1,J) ~= (10 + J) + 2 (20 + J) THEN BB = FALSE;
  END;
  DO FOR TEMPORARY I = 2 TO 3; DO FOR TEMPORARY J = 1 TO 3;
     IF M$(I,J) ~= 10 I + J THEN BB = FALSE;
  END; END;
  CALL PRINT_RESULT('LINEAR COMBO OF MATRIX ROWS', BB) ASSIGN(ERRORS, TOTAL);
    
C  EXCHANGE ROWS I AND J:
 
  M = MM;
E     -     -
M    TEMP = M   ;
S            I,*

E    -      -
M    M    = M   ;
S     I,*    J,*

E    -       -
M    M    = TEMP;
S     J,*
  BB = TRUE;
  DO FOR TEMPORARY I = 1 TO 3; DO FOR TEMPORARY J = 1 TO 3;
     TEMPORARY K INTEGER;
     IF I = 1 THEN K = 20;
     IF I = 2 THEN K = 10;
     IF I = 3 THEN K = 30;
     IF M$(I,J) ~= K + J THEN BB = FALSE;
  END; END;
  CALL PRINT_RESULT('SWAP MATRIX ROWS', BB) ASSIGN(ERRORS, TOTAL);
     
  CLOSE ROWS_047;
 CALL ROWS_047 ASSIGN(ERRORS, TOTAL);
 
 /*--------------------------------------------------------------------------*/
 WRITE(6) ;
 WRITE(6) 'Some REPLACE-macro tests.';
 WRITE(6) 'See p. 3-14 of "Programming in HAL/S".';
 
 TABLE_052:
 PROCEDURE ASSIGN(ERRORS, TOTAL);
     DECLARE ERRORS INTEGER, TOTAL INTEGER;
     REPLACE LOG2(X) BY "LOG(X)/LOG(2)";
     REPLACE CHECK(X, VALUE) BY "REALLY_CLOSE_TO(LOG2(X), VALUE)";
     
     CALL PRINT_RESULT('REPLACE 0.25', CHECK(0.25, -2)) ASSIGN(ERRORS, TOTAL);
     CALL PRINT_RESULT('REPLACE 0.5', CHECK(0.5, -1)) ASSIGN(ERRORS, TOTAL);
     CALL PRINT_RESULT('REPLACE 1', CHECK(1, 0)) ASSIGN(ERRORS, TOTAL);
     CALL PRINT_RESULT('REPLACE 2', CHECK(2, 1)) ASSIGN(ERRORS, TOTAL);
     CALL PRINT_RESULT('REPLACE 4', CHECK(4, 2)) ASSIGN(ERRORS, TOTAL);
     CALL PRINT_RESULT('REPLACE 8', CHECK(8, 3)) ASSIGN(ERRORS, TOTAL);
     CALL PRINT_RESULT('REPLACE 16', CHECK(16, 4)) ASSIGN(ERRORS, TOTAL);
 CLOSE TABLE_052;
 CALL TABLE_052 ASSIGN(ERRORS, TOTAL);
 
 /*--------------------------------------------------------------------------*/
 WRITE(6) ;
 WRITE(6) 'Tests of discrete FOR-loop, with and without WHILE/UNTIL clause.';
 WRITE(6) 'See p. 5-6 of "Programming in HAL/S".';
 /*
 DECLARE SMALL_PRIMES ARRAY(26) INTEGER CONSTANT(2, 3, 5, 7, 11, 13, 17, 19, 23, 
 					         29, 31, 37, 41, 43, 47, 53, 59, 
 					         61, 67, 71, 73, 79, 83, 89, 97, 
 					         101);
 */
 DECLARE SMALL_PRIMES ARRAY(26) INTEGER;
 SMALL_PRIMES = INTEGER(2, 3, 5, 7, 11, 13, 17, 19, 23, 
 					         29, 31, 37, 41, 43, 47, 53, 59, 
 					         61, 67, 71, 73, 79, 83, 89, 97, 
 					         101);
 BB = TRUE;
 DO FOR TEMPORARY I = 2 TO 101;
    TEMPORARY IS_PRIME BOOLEAN, IS_IN_ARRAY BOOLEAN;
    IS_PRIME = TRUE;
    DO FOR TEMPORARY J = 2, 3, 5, 7, 11, 13, 17, 19, 23, 29, 31, 37, 41, 43, 47;
       IF I NOT = J AND REMAINDER(I, J) = 0 THEN DO;
          IS_PRIME = FALSE;
          EXIT;
       END;
    END;
    IS_IN_ARRAY = FALSE;
    DO FOR TEMPORARY J = 1 TO 26;
       IF I = SMALL_PRIMES$J THEN DO;
          IS_IN_ARRAY = TRUE;
          EXIT;
       END;
    END;
    IF IS_PRIME ~= IS_IN_ARRAY THEN DO;
       BB = FALSE;
       EXIT;
    END;
 END;
 CALL PRINT_RESULT('DISCRETE FOR', BB) ASSIGN(ERRORS, TOTAL);
 
 DECLARE II INTEGER, III INTEGER;
 III = 0;
 DO FOR II = 2, 6, 69, -5, 21, 62 WHILE II > 0; III = III + 1; END;
 IF II = -5 AND III = 3 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('DISCRETE FOR WHILE', BB) ASSIGN(ERRORS, TOTAL);
 III = 0;
 DO FOR II = 2, 6, 69, -5, 21, 62 UNTIL II < 0; III = III + 1; END;
 IF II = -5 AND III = 4 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('DISCRETE FOR UNTIL', BB) ASSIGN(ERRORS, TOTAL);
 III = 0;
 BB = TRUE;
 DO FOR II = 2, 3, 5, 7, 11, 13, 17; 
    III = III + 1; 
    IF II ~= SMALL_PRIMES$III THEN BB = FALSE;
 END;
 IF II ~= 17 THEN BB = FALSE;
 CALL PRINT_RESULT('DISCRETE FOR PRIMES', BB) ASSIGN(ERRORS, TOTAL);
 
 BB = TRUE;
 III = 1;
 DO FOR II = 1, 2 II, 2 II, 2 II, 2 II, 2 II, 2 II;
    IF II ~= III THEN BB = FALSE;
    III = 2 III;
 END;
 IF II NOT = 64 THEN BB = FALSE;
 CALL PRINT_RESULT('DISCRETE FOR EXPRESSIONS', BB) ASSIGN(ERRORS, TOTAL);
 
 DO FOR II = 1, 2 II, 2 II, 2 II, 2 II, 2 II, 2 II;
    IF II > 4 THEN EXIT;
 END;
 IF II = 8 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('DISCRETE FOR EXIT', BB) ASSIGN(ERRORS, TOTAL);
 
 BB = TRUE;
 III = 1;
 DO FOR II = 1, 2 II, 2 II, 2 II, 2 II, 2 II, 2 II;
    IF II < 32 THEN DO;
       IF II ~= III THEN BB = FALSE;
       III = 2 III;
       REPEAT;
    END; 
    EXIT;
 END;
 IF II NOT = 32 THEN BB = FALSE;
 CALL PRINT_RESULT('DISCRETE FOR REPEAT', BB) ASSIGN(ERRORS, TOTAL);
 
 /*--------------------------------------------------------------------------*/
 WRITE(6) ;
 WRITE(6) 'Some test cases for DO CASE statements.';
 DO FOR TEMPORARY I = 1 TO 10;
    TEMPORARY II INTEGER;
    II = 100;
    DO CASE I;
       II = 1;
       II = 2;
       II = 3;
       II = 4;
       II = 5;
    END;
    BB = TRUE;
    IF I <= 5 AND II ~= I THEN BB = FALSE;
    IF I > 5 AND II ~= 100 THEN BB = FALSE;
 END;
 CALL PRINT_RESULT('DO CASE NO ELSE', BB) ASSIGN(ERRORS, TOTAL);
 
 DO FOR TEMPORARY I = 1 TO 10;
    TEMPORARY II INTEGER;
    II = 100;
    DO CASE I;
       ELSE II = 99;
       II = 1;
       II = 2;
       II = 3;
       II = 4;
       II = 5;
    END;
    BB = TRUE;
    IF I <= 5 AND II ~= I THEN BB = FALSE;
    IF I > 5 AND II ~= 99 THEN BB = FALSE;
 END;
 CALL PRINT_RESULT('DO CASE ELSE', BB) ASSIGN(ERRORS, TOTAL);
 
 DO FOR TEMPORARY I = 1 TO 10;
    TEMPORARY II INTEGER;
    II = 100;
    DO CASE I;
       ELSE DO;
       	  TEMPORARY K INTEGER;
       	  K = 0;
          DO FOR TEMPORARY J = 1 TO 98; K = K + 1; END;
          II = K;
       END;
       II = 1;
       II = 2;
       II = 3;
       DO;
       	  TEMPORARY K INTEGER;
       	  K = 0;
          DO FOR TEMPORARY J = 1 TO 4; K = K + 1; END;
          II = K;
       END;
       II = 5;
    END;
    BB = TRUE;
    IF I <= 5 AND II ~= I THEN BB = FALSE;
    IF I > 5 AND II ~= 98 THEN BB = FALSE;
 END;
 CALL PRINT_RESULT('DO CASE BLOCKS', BB) ASSIGN(ERRORS, TOTAL);
 
 /*--------------------------------------------------------------------------*/
 WRITE(6) ;
 WRITE(6) 'Some tests of REPEAT inside of loops.';
 DECLARE REPEAT_EXPECTED VECTOR(55) 
 	   		CONSTANT(1, 1, 2, 1, 2, 3, 1, 2, 3, 4, 1, 2, 3, 4, 5,
 				 1, 2, 3, 4, 5, 6, 1, 2, 3, 4, 5, 6, 7,
 				 1, 2, 3, 4, 5, 6, 7, 8,
 				 1, 2, 3, 4, 5, 6, 7, 8, 9,
 				 1, 2, 3, 4, 5, 6, 7, 8, 9, 10);
 BB = TRUE;
 DO;
    TEMPORARY K INTEGER;
    K = 0;
    REPEAT_LABEL1: 
    DO FOR TEMPORARY I = 1 TO 10;
       DO FOR TEMPORARY J = 1 TO 10;
          IF J > I THEN REPEAT REPEAT_LABEL1;
          K = K + 1;
          IF J ~= REPEAT_EXPECTED$K THEN BB = FALSE;
          /*WRITE(6) I, J;*/
       END;
    END;
    IF K ~= 55 THEN BB = FALSE;
 END;
 CALL PRINT_RESULT('NESTED DO FOR REPEAT', BB) ASSIGN(ERRORS, TOTAL);
 
 BB = TRUE;
 DO;
    TEMPORARY K INTEGER;
    K = 0;
    REPEAT_LABEL1: 
    DO FOR TEMPORARY I = 1 TO 10 WHILE I < 100;
       DO FOR TEMPORARY J = 1 TO 10;
          IF J > I THEN REPEAT REPEAT_LABEL1;
          K = K + 1;
          IF J ~= REPEAT_EXPECTED$K THEN BB = FALSE;
          /*WRITE(6) I, J;*/
       END;
    END;
    IF K ~= 55 THEN BB = FALSE;
 END;
 CALL PRINT_RESULT('NESTED DO FOR WHILE REPEAT', BB) ASSIGN(ERRORS, TOTAL);
 
 BB = TRUE;
 DO;
    TEMPORARY K INTEGER;
    K = 0;
    REPEAT_LABEL1: 
    DO FOR TEMPORARY I = 1 TO 10 UNTIL I > 100;
       DO FOR TEMPORARY J = 1 TO 10;
          IF J > I THEN REPEAT REPEAT_LABEL1;
          K = K + 1;
          IF J ~= REPEAT_EXPECTED$K THEN BB = FALSE;
          /*WRITE(6) I, J;*/
       END;
    END;
    IF K ~= 55 THEN BB = FALSE;
 END;
 CALL PRINT_RESULT('NESTED DO FOR UNTIL REPEAT', BB) ASSIGN(ERRORS, TOTAL);
 
 BB = TRUE;
 DO;
    TEMPORARY K INTEGER;
    K = 0;
    REPEAT_LABEL1: 
    DO FOR TEMPORARY I = 1, 2, 3, 4, 5, 6, 7, 8, 9, 10;
       DO FOR TEMPORARY J = 1 TO 10;
          IF J > I THEN REPEAT REPEAT_LABEL1;
          K = K + 1;
          IF J ~= REPEAT_EXPECTED$K THEN BB = FALSE;
          /*WRITE(6) I, J;*/
       END;
    END;
    IF K ~= 55 THEN BB = FALSE;
 END;
 CALL PRINT_RESULT('NESTED DO FOR DISCRETE REPEAT', BB) ASSIGN(ERRORS, TOTAL);
 
 BB = TRUE;
 DO;
    TEMPORARY K INTEGER;
    K = 0;
    REPEAT_LABEL1: 
    DO FOR TEMPORARY I = 1, 2, 3, 4, 5, 6, 7, 8, 9, 10 WHILE I < 100;
       DO FOR TEMPORARY J = 1 TO 10;
          IF J > I THEN REPEAT REPEAT_LABEL1;
          K = K + 1;
          IF J ~= REPEAT_EXPECTED$K THEN BB = FALSE;
          /*WRITE(6) I, J;*/
       END;
    END;
    IF K ~= 55 THEN BB = FALSE;
 END;
 CALL PRINT_RESULT('NESTED DO FOR DISCRETE WHILE REPEAT', BB) ASSIGN(ERRORS, TOTAL);
 
 BB = TRUE;
 DO;
    TEMPORARY K INTEGER;
    K = 0;
    REPEAT_LABEL1: 
    DO FOR TEMPORARY I = 1, 2, 3, 4, 5, 6, 7, 8, 9, 10 UNTIL I > 100;
       DO FOR TEMPORARY J = 1 TO 10;
          IF J > I THEN REPEAT REPEAT_LABEL1;
          K = K + 1;
          IF J ~= REPEAT_EXPECTED$K THEN BB = FALSE;
          /*WRITE(6) I, J;*/
       END;
    END;
    IF K ~= 55 THEN BB = FALSE;
 END;
 CALL PRINT_RESULT('NESTED DO FOR DISCRETE UNTIL REPEAT', BB) ASSIGN(ERRORS, TOTAL);
 
 BB = TRUE;
 DO;
    TEMPORARY K INTEGER, I INTEGER;
    K = 0;
    I = 0;
    REPEAT_LABEL1: 
    DO WHILE I < 10;
       I = I + 1;
       DO FOR TEMPORARY J = 1 TO 10;
          IF J > I THEN REPEAT REPEAT_LABEL1;
          K = K + 1;
          IF J ~= REPEAT_EXPECTED$K THEN BB = FALSE;
          /*WRITE(6) I, J;*/
       END;
    END;
    IF K ~= 55 THEN BB = FALSE;
 END;
 CALL PRINT_RESULT('NESTED DO WHILE REPEAT', BB) ASSIGN(ERRORS, TOTAL);
 
 BB = TRUE;
 DO;
    TEMPORARY K INTEGER, I INTEGER;
    K = 0;
    I = 0;
    REPEAT_LABEL1: 
    DO UNTIL I = 10;
       I = I + 1;
       DO FOR TEMPORARY J = 1 TO 10;
          IF J > I THEN REPEAT REPEAT_LABEL1;
          K = K + 1;
          IF J ~= REPEAT_EXPECTED$K THEN BB = FALSE;
          /*WRITE(6) I, J;*/
       END;
    END;
    IF K ~= 55 THEN BB = FALSE;
 END;
 CALL PRINT_RESULT('NESTED DO UNTIL REPEAT', BB) ASSIGN(ERRORS, TOTAL);
 
 /*--------------------------------------------------------------------------*/
 WRITE(6) ;
 WRITE(6) 'Some tests of the "=" and "NOT =" relational operators.';
 TEST_EQUALS: PROCEDURE ASSIGN(ERRORS, TOTAL);
    DECLARE ERRORS INTEGER, TOTAL INTEGER;
    DECLARE INTEGER, I1 INITIAL(1), I2 INITIAL(2);
    DECLARE SCALAR, S1 INITIAL(1.0), S2 INITIAL(2.0);
    DECLARE BIT(5), B1 INITIAL(27);
    DECLARE BIT(7), B2 INITIAL(27);
    DECLARE BIT(7), B3 INITIAL(28);
    DECLARE C1 CHARACTER(20) INITIAL('HELLO'), C2 CHARACTER(15) INITIAL('HELLO');
    DECLARE C3 CHARACTER(20) INITIAL('PUMPERNICKEL');
    DECLARE V1 VECTOR(3) INITIAL(1,2,3), V2 VECTOR(4) INITIAL(1,2,3,4);
    DECLARE M1 MATRIX(3,2) INITIAL(1,2,3,4,5,6), M2 MATRIX(3,2) INITIAL(2,1,3,4,5,6);
    DECLARE AI1 ARRAY(5) INTEGER INITIAL(1,2,3,4,5), 
    	    AI2 ARRAY(5) INTEGER INITIAL(2,1,3,4,5),
    	    AI3 ARRAY(3,2) INTEGER INITIAL(1,2,3,4,5,6), 
    	    AI4 ARRAY(3,2) INTEGER INITIAL(2,1,3,4,5,6),
    	    AS1 ARRAY(5) SCALAR INITIAL(1,2,3,4,5), 
    	    AS2 ARRAY(5) SCALAR INITIAL(2,1,3,4,5),
    	    AS3 ARRAY(3,2) SCALAR INITIAL(1,2,3,4,5,6), 
    	    AS4 ARRAY(3,2) SCALAR INITIAL(2,1,3,4,5,6),
    	    AC1 ARRAY(5) CHARACTER(32) INITIAL(1,2,3,4,5), 
    	    AC2 ARRAY(5) CHARACTER(32) INITIAL(2,1,3,4,5),
    	    AC3 ARRAY(3,2) CHARACTER(32) INITIAL(1,2,3,4,5,6), 
    	    AC4 ARRAY(3,2) CHARACTER(32) INITIAL(2,1,3,4,5,6),
    	    AB1 ARRAY(5) BIT(4) INITIAL(1,2,3,4,5), 
    	    AB2 ARRAY(5) BIT(5) INITIAL(2,1,3,4,5),
    	    AB3 ARRAY(3,2) BIT(6) INITIAL(1,2,3,4,5,6), 
    	    AB4 ARRAY(3,2) BIT(7) INITIAL(2,1,3,4,5,6),
    	    AV1 ARRAY(5) VECTOR(2) INITIAL(1,2,3,4,5,6,7,8,9,10), 
    	    AV2 ARRAY(5) VECTOR(2) INITIAL(2,1,3,4,5,6,7,8,9,10),
    	    AV3 ARRAY(3,2) VECTOR(2) INITIAL(1,2,3,4,5,6,7,8,9,10,11,12), 
    	    AV4 ARRAY(3,2) VECTOR(2) INITIAL(2,1,3,4,5,6,7,8,9,10,11,12),
    	    AM1 ARRAY(5) MATRIX(2,2) INITIAL(1,2,3,4,5,6,7,8,9,10,1,2,3,4,5,6,7,8,9,10), 
    	    AM2 ARRAY(5) MATRIX(2,2) INITIAL(2,1,3,4,5,6,7,8,9,10,1,2,3,4,5,6,7,8,9,10),
    	    AM3 ARRAY(3,2) MATRIX(2,2) INITIAL(1,2,3,4,5,6,7,8,9,10,11,12,1,2,3,4,5,6,7,8,9,10,11,12), 
    	    AM4 ARRAY(3,2) MATRIX(2,2) INITIAL(2,1,3,4,5,6,7,8,9,10,11,12,1,2,3,4,5,6,7,8,9,10,11,12)
    	    ;
    DECLARE BB BOOLEAN;
    IF I1 = I1 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('INTEGER EQUALITY 1', BB) ASSIGN(ERRORS, TOTAL);
    IF I1 = I2 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('INTEGER EQUALITY 2', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF I1 NOT = I1 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('INTEGER INEQUALITY 1', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF I1 NOT = I2 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('INTEGER INEQUALITY 2', BB) ASSIGN(ERRORS, TOTAL);
    IF S1 = S1 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('SCALAR EQUALITY 1', BB) ASSIGN(ERRORS, TOTAL);
    IF S1 = S2 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('SCALAR EQUALITY 2', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF S1 NOT = S1 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('SCALAR INEQUALITY 1', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF S1 NOT = S2 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('SCALAR INEQUALITY 2', BB) ASSIGN(ERRORS, TOTAL);
    IF I1 = S1 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('INTEGER:SCALAR EQUALITY 1', BB) ASSIGN(ERRORS, TOTAL);
    IF I1 = S2 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('INTEGER:SCALAR EQUALITY 2', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF I1 NOT = S1 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('INTEGER:SCALAR INEQUALITY 1', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF I1 NOT = S2 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('INTEGER:SCALAR INEQUALITY 2', BB) ASSIGN(ERRORS, TOTAL);
    IF C1 = C1 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('STRING EQUALITY 1', BB) ASSIGN(ERRORS, TOTAL);
    IF C1 = C2 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('STRING EQUALITY 2', BB) ASSIGN(ERRORS, TOTAL);
    IF C1 = C3 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('STRING EQUALITY 3', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF C1 NOT = C1 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('STRING INEQUALITY 1', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF C1 NOT = C2 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('STRING INEQUALITY 2', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF C1 NOT = C3 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('STRING INEQUALITY 2', BB) ASSIGN(ERRORS, TOTAL);
    IF B1 = B1 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('BIT EQUALITY 1', BB) ASSIGN(ERRORS, TOTAL);
    IF B1 = B2 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('BIT EQUALITY 2', BB) ASSIGN(ERRORS, TOTAL);
    IF B1 = B3 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('BIT EQUALITY 3', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF B1 NOT = B1 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('BIT INEQUALITY 1', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF B1 NOT = B2 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('BIT INEQUALITY 2', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF B1 NOT = B3 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('BIT INEQUALITY 3', BB) ASSIGN(ERRORS, TOTAL);
    IF V1 = V1 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('VECTOR EQUALITY 1', BB) ASSIGN(ERRORS, TOTAL);
    IF V1 = V2 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('VECTOR EQUALITY 2', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF V1 NOT = V1 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('VECTOR INEQUALITY 1', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF V1 NOT = V2 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('VECTOR INEQUALITY 2', BB) ASSIGN(ERRORS, TOTAL);
    IF M1 = M1 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('MATRIX EQUALITY 1', BB) ASSIGN(ERRORS, TOTAL);
    IF M1 = M2 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('MATRIX EQUALITY 2', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF M1 NOT = M1 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('MATRIX INEQUALITY 1', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF M1 NOT = M2 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('MATRIX INEQUALITY 2', BB) ASSIGN(ERRORS, TOTAL);
    IF AI1 = AI1 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 1D INTEGER EQUALITY 1', BB) ASSIGN(ERRORS, TOTAL);
    IF AI1 = AI2 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 1D INTEGER EQUALITY 2', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF AI1 NOT = AI1 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 1D INTEGER INEQUALITY 1', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF AI1 NOT = AI2 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 1D INTEGER INEQUALITY 2', BB) ASSIGN(ERRORS, TOTAL);
    IF AS1 = AS1 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 1D SCALAR EQUALITY 1', BB) ASSIGN(ERRORS, TOTAL);
    IF AS1 = AS2 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 1D SCALAR EQUALITY 2', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF AS1 NOT = AS1 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 1D SCALAR INEQUALITY 1', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF AS1 NOT = AS2 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 1D SCALAR INEQUALITY 2', BB) ASSIGN(ERRORS, TOTAL);
    IF AI3 = AI3 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 2D INTEGER EQUALITY 1', BB) ASSIGN(ERRORS, TOTAL);
    IF AI3 = AI4 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 2D INTEGER EQUALITY 2', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF AI3 NOT = AI4 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 2D INTEGER INEQUALITY 1', BB) ASSIGN(ERRORS, TOTAL);
    IF AI3 NOT = AI4 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 2D INTEGER INEQUALITY 2', BB) ASSIGN(ERRORS, TOTAL);
    IF AS3 = AS3 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 2D SCALAR EQUALITY 1', BB) ASSIGN(ERRORS, TOTAL);
    IF AS3 = AS4 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 2D SCALAR EQUALITY 2', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF AS3 NOT = AS3 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 2D SCALAR INEQUALITY 1', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF AS3 NOT = AS4 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 2D SCALAR INEQUALITY 2', BB) ASSIGN(ERRORS, TOTAL);
    IF AI3 = AS3 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 2D INTEGER:SCALAR EQUALITY 1', BB) ASSIGN(ERRORS, TOTAL);
    IF AI3 = AS4 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 2D INTEGER:SCALAR EQUALITY 2', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF AI3 NOT = AS3 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 2D INTEGER:SCALAR INEQUALITY 1', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF AI3 NOT = AS4 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 2D INTEGER:SCALAR INEQUALITY 2', BB) ASSIGN(ERRORS, TOTAL);
    IF AB1 = AB1 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 1D BIT EQUALITY 1', BB) ASSIGN(ERRORS, TOTAL);
    IF AB1 = AB2 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 1D BIT EQUALITY 2', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF AB1 NOT = AB1 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 1D BIT INEQUALITY 1', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF AB1 NOT = AB2 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 1D BIT INEQUALITY 2', BB) ASSIGN(ERRORS, TOTAL);
    IF AB3 = AB3 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 2D BIT EQUALITY 1', BB) ASSIGN(ERRORS, TOTAL);
    IF AB3 = AB4 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 2D BIT EQUALITY 2', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF AB3 NOT = AB3 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 2D BIT INEQUALITY 1', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF AB3 NOT = AB4 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 2D BIT INEQUALITY 2', BB) ASSIGN(ERRORS, TOTAL);
    IF AV1 = AV1 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 1D VECTOR EQUALITY 1', BB) ASSIGN(ERRORS, TOTAL);
    IF AV1 = AV2 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 1D VECTOR EQUALITY 2', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF AV1 NOT = AV1 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 1D VECTOR INEQUALITY 1', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF AV1 NOT = AV2 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 1D VECTOR INEQUALITY 2', BB) ASSIGN(ERRORS, TOTAL);
    IF AV3 = AV3 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 2D VECTOR EQUALITY 1', BB) ASSIGN(ERRORS, TOTAL);
    IF AV3 = AV4 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 2D VECTOR EQUALITY 2', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF AV3 NOT = AV3 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 2D VECTOR INEQUALITY 1', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF AV3 NOT = AV4 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 2D VECTOR INEQUALITY 2', BB) ASSIGN(ERRORS, TOTAL);
    IF AM1 = AM1 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 1D MATRIX EQUALITY 1', BB) ASSIGN(ERRORS, TOTAL);
    IF AM1 = AM2 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 1D MATRIX EQUALITY 2', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF AM1 NOT = AM1 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 1D MATRIX INEQUALITY 1', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF AM1 NOT = AM2 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 1D MATRIX INEQUALITY 2', BB) ASSIGN(ERRORS, TOTAL);
    IF AM3 = AM3 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 2D MATRIX EQUALITY 1', BB) ASSIGN(ERRORS, TOTAL);
    IF AM3 = AM4 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 2D MATRIX EQUALITY 2', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF AM3 NOT = AM3 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 2D MATRIX INEQUALITY 1', NOT BB) ASSIGN(ERRORS, TOTAL);
    IF AM3 NOT = AM4 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('ARRAY 2D MATRIX INEQUALITY 2', BB) ASSIGN(ERRORS, TOTAL);
 CLOSE TEST_EQUALS;
 CALL TEST_EQUALS ASSIGN(ERRORS, TOTAL);
 
 /*--------------------------------------------------------------------------*/
 WRITE(6);
 WRITE(6) 'Test unary minus on composite datatypes.';
 TEST_UNARY_MINUS: PROCEDURE ASSIGN(ERRORS, TOTAL);
    DECLARE ERRORS INTEGER, TOTAL INTEGER;
    DECLARE INTEGER, I INITIAL(5), II;
    DECLARE SCALAR, S INITIAL(6), SS;
    DECLARE VECTOR, V INITIAL(7,8,9), VV;
    DECLARE MATRIX, M INITIAL(10,11,12,13,14,15,16,17,18), MM;
    DECLARE ARRAY(3) INTEGER, AI INITIAL(20,21,22), AII;
    DECLARE ARRAY(3) SCALAR, AS INITIAL(23,24,25), ASS;
    DECLARE ARRAY(2) VECTOR, AV INITIAL(31,32,33,34,35,36), AVV;
    DECLARE ARRAY(2) MATRIX(2,2), AM INITIAL(41,42,43,44,45,46,47,48), AMM;
    DECLARE ARRAY(2,2) VECTOR(2), A2V INITIAL(51,52,53,54,55,56,57,58), A2VV;
    DECLARE BOOLEAN, BB;
    II = -I;
    IF II = -5 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('Unary minus INTEGER', BB) ASSIGN(ERRORS, TOTAL);
    SS = -S;
    IF SS = -6 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('Unary minus SCALAR', BB) ASSIGN(ERRORS, TOTAL);
    VV = -V;
    CALL CHECK_LINEAR('Unary minus VECTOR', -7, -1, SCALAR(1#VV)) ASSIGN(ERRORS, TOTAL);
    MM = -M;
    CALL CHECK_LINEAR('Unary minus MATRIX', -10, -1, SCALAR(1#MM)) ASSIGN(ERRORS, TOTAL);
    AII = -AI;
    CALL CHECK_LINEAR('Unary minus 1D ARRAY INTEGER', -20, -1, SCALAR(1#AII)) ASSIGN(ERRORS, TOTAL);
    ASS = -AS;
    CALL CHECK_LINEAR('Unary minus 1D ARRAY SCALAR', -23, -1, SCALAR(1#ASS)) ASSIGN(ERRORS, TOTAL);
    AVV = -AV;
    CALL CHECK_LINEAR('Unary minus 1D ARRAY VECTOR', -31, -1, SCALAR(1#AVV)) ASSIGN(ERRORS, TOTAL);
    AMM = -AM;
    CALL CHECK_LINEAR('Unary minus 1D ARRAY MATRIX', -41, -1, SCALAR(1#AMM)) ASSIGN(ERRORS, TOTAL);
    A2VV = -A2V;
    CALL CHECK_LINEAR('Unary minus 2D ARRAY VECTOR', -51, -1, SCALAR(1#A2VV)) ASSIGN(ERRORS, TOTAL);
 CLOSE TEST_UNARY_MINUS;
 CALL TEST_UNARY_MINUS ASSIGN(ERRORS, TOTAL);
    
 /*--------------------------------------------------------------------------*/
 
 ARRAYED_ARITHMETIC: PROCEDURE ASSIGN(ERRORS, TOTAL);
    DECLARE ERRORS INTEGER, TOTAL INTEGER;
    DECLARE INTEGER, I, J;
    DECLARE BOOLEAN, BB;
    DECLARE VECTOR, ZEROVECTOR CONSTANT(0,0,0);
    DECLARE ARRAY(2,3) INTEGER, AI1, AI2, AI3;
    DECLARE ARRAY(2,3) SCALAR, AS1, AS2, AS3;
    DECLARE ARRAY(2,3) VECTOR, AV1, AV2, AV3;
    DECLARE ARRAY(2,3) MATRIX, AM1, AM2, AM3;
    
    /* The stuff that follows is all for setting up various data objects
       declared above with "random" data. */
    
    RANDOM_VECTOR: FUNCTION VECTOR;
       DECLARE V VECTOR, I INTEGER;
       DO FOR I = 1 TO 3; V$I = RANDOM; END;
       RETURN V;
    CLOSE RANDOM_VECTOR;
    
    RANDOM_MATRIX: FUNCTION MATRIX;
       DECLARE M MATRIX, I INTEGER;
       DO FOR I = 1 TO 3; 
          M$(I,*) = RANDOM_VECTOR; 
       END;
       RETURN M;
    CLOSE RANDOM_MATRIX;
    
    DO FOR I = 1 TO 2; 
       DO FOR J = 1 TO 3; 
          DO UNTIL AI1$(I,J) ~= 0; AI1$(I,J) = 100 RANDOM; END; 
          DO UNTIL AI2$(I,J) ~= 0; AI2$(I,J) = 100 RANDOM; END;
          DO UNTIL AS1$(I,J) ~= 0; AS1$(I,J) = 100 RANDOM; END;
          DO UNTIL AS2$(I,J) ~= 0; AS2$(I,J) = 100 RANDOM; END;
          DO UNTIL AV1$(I,J) ~= ZEROVECTOR; AV1$(I,J) = RANDOM_VECTOR; END;
          DO UNTIL AV2$(I,J) ~= ZEROVECTOR; AV2$(I,J) = RANDOM_VECTOR; END;
          DO UNTIL ABS(DET(AM1$(I,J))) > 0.1; AM1$(I,J) = RANDOM_MATRIX; END;
          DO UNTIL ABS(DET(AM2$(I,J))) > 0.1; AM2$(I,J) = RANDOM_MATRIX; END;
       END; 
    END;
    
    WRITE(6) ;
    WRITE(6) 'Some tests of arrayed arithmetic.';
    
    AI3 = AI1 + AI2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AI3$(I,J) ~= AI1$(I,J) + AI2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed addition of INTEGER', BB) ASSIGN(ERRORS, TOTAL);
    AS3 = AI1 + AS2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AS3$(I,J) ~= AI1$(I,J) + AS2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed addition of INTEGER to SCALAR', BB) ASSIGN(ERRORS, TOTAL);
    AS3 = AS1 + AI2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AS3$(I,J) ~= AS1$(I,J) + AI2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed addition of SCALAR to INTEGER', BB) ASSIGN(ERRORS, TOTAL);
    AS3 = AS1 + AS2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AS3$(I,J) ~= AS1$(I,J) + AS2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed addition of SCALAR', BB) ASSIGN(ERRORS, TOTAL);
    AV3 = AV1 + AV2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AV3$(I,J) ~= AV1$(I,J) + AV2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed addition of VECTOR', BB) ASSIGN(ERRORS, TOTAL);
    AM3 = AM1 + AM2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AM3$(I,J) ~= AM1$(I,J) + AM2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed addition of MATRIX', BB) ASSIGN(ERRORS, TOTAL);

    AI3 = AI1 - AI2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AI3$(I,J) ~= AI1$(I,J) - AI2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed subtraction of INTEGER', BB) ASSIGN(ERRORS, TOTAL);
    AS3 = AI1 - AS2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AS3$(I,J) ~= AI1$(I,J) - AS2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed subtraction of SCALAR from INTEGER', BB) ASSIGN(ERRORS, TOTAL);
    AS3 = AS1 - AI2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AS3$(I,J) ~= AS1$(I,J) - AI2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed subtraction of INTEGER from SCALAR', BB) ASSIGN(ERRORS, TOTAL);
    AS3 = AS1 - AS2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AS3$(I,J) ~= AS1$(I,J) - AS2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed subtraction of SCALAR', BB) ASSIGN(ERRORS, TOTAL);
    AV3 = AV1 - AV2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AV3$(I,J) ~= AV1$(I,J) - AV2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed subtraction of VECTOR', BB) ASSIGN(ERRORS, TOTAL);
    AM3 = AM1 - AM2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AM3$(I,J) ~= AM1$(I,J) - AM2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed subtraction of MATRIX', BB) ASSIGN(ERRORS, TOTAL);

    AI3 = AI1 / AI2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AI3$(I,J) ~= ROUND(AI1$(I,J) / AI2$(I,J)) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed division of INTEGER', BB) ASSIGN(ERRORS, TOTAL);
    AS3 = AI1 / AS2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AS3$(I,J) ~= AI1$(I,J) / AS2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed division of SCALAR by INTEGER', BB) ASSIGN(ERRORS, TOTAL);
    AS3 = AS1 / AI2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AS3$(I,J) ~= AS1$(I,J) / AI2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed division of INTEGER by SCALAR', BB) ASSIGN(ERRORS, TOTAL);
    AS3 = AS1 / AS2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AS3$(I,J) ~= AS1$(I,J) / AS2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed division of SCALAR', BB) ASSIGN(ERRORS, TOTAL);
    AV3 = AV1 / AI2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AV3$(I,J) ~= AV1$(I,J) / AI2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed division of VECTOR by INTEGER', BB) ASSIGN(ERRORS, TOTAL);
    AV3 = AV1 / AS2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AV3$(I,J) ~= AV1$(I,J) / AS2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed division of VECTOR by SCALAR', BB) ASSIGN(ERRORS, TOTAL);
    AM3 = AM1 / AI2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AM3$(I,J) ~= AM1$(I,J) / AI2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed division of MATRIX by INTEGER', BB) ASSIGN(ERRORS, TOTAL);
    AM3 = AM1 / AS2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AM3$(I,J) ~= AM1$(I,J) / AS2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed division of MATRIX by SCALAR', BB) ASSIGN(ERRORS, TOTAL);

    AS3 = AV1 . AV2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AS3$(I,J) ~= AV1$(I,J) . AV2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed dot product', BB) ASSIGN(ERRORS, TOTAL);

    AV3 = AV1 * AV2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AV3$(I,J) ~= AV1$(I,J) * AV2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed cross product', BB) ASSIGN(ERRORS, TOTAL);
    
    AI3 = AI1 AI2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AI3$(I,J) ~= AI1$(I,J) AI2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed multiplication of INTEGER', BB) ASSIGN(ERRORS, TOTAL);
    AS3 = AI1 AS2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AS3$(I,J) ~= AI1$(I,J) AS2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed multiplication of INTEGER by SCALAR', BB) ASSIGN(ERRORS, TOTAL);
    AS3 = AS1 AI2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AS3$(I,J) ~= AS1$(I,J) AI2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed multiplication of SCALAR by INTEGER', BB) ASSIGN(ERRORS, TOTAL);
    AS3 = AS1 AS2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AS3$(I,J) ~= AS1$(I,J) AS2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed multiplication of SCALAR', BB) ASSIGN(ERRORS, TOTAL);
    AV3 = AI1 AV2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AV3$(I,J) ~= AI1$(I,J) AV2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed multiplication of INTEGER by VECTOR', BB) ASSIGN(ERRORS, TOTAL);
    AV3 = AS1 AV2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AV3$(I,J) ~= AS1$(I,J) AV2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed multiplication of SCALAR by VECTOR', BB) ASSIGN(ERRORS, TOTAL);
    AV3 = AV1 AI2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AV3$(I,J) ~= AV1$(I,J) AI2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed multiplication of VECTOR by INTEGER', BB) ASSIGN(ERRORS, TOTAL);
    AV3 = AV1 AS2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AV3$(I,J) ~= AV1$(I,J) AS2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed multiplication of VECTOR by SCALAR', BB) ASSIGN(ERRORS, TOTAL);
    AM3 = AI1 AM2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AM3$(I,J) ~= AI1$(I,J) AM2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed multiplication of INTEGER by MATRIX', BB) ASSIGN(ERRORS, TOTAL);
    AM3 = AS1 AM2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AM3$(I,J) ~= AS1$(I,J) AM2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed multiplication of SCALAR by MATRIX', BB) ASSIGN(ERRORS, TOTAL);
    AM3 = AM1 AI2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AM3$(I,J) ~= AM1$(I,J) AI2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed multiplication of MATRIX by INTEGER', BB) ASSIGN(ERRORS, TOTAL);
    AM3 = AM1 AS2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AM3$(I,J) ~= AM1$(I,J) AS2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed multiplication of MATRIX by SCALAR', BB) ASSIGN(ERRORS, TOTAL);
    AV3 = AI1 AV2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AV3$(I,J) ~= AI1$(I,J) AV2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed multiplication of INTEGER by VECTOR', BB) ASSIGN(ERRORS, TOTAL);
    AV3 = AS1 AV2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AV3$(I,J) ~= AS1$(I,J) AV2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed multiplication of SCALAR by VECTOR', BB) ASSIGN(ERRORS, TOTAL);
    AV3 = AV1 AI2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AV3$(I,J) ~= AV1$(I,J) AI2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed multiplication of VECTOR by INTEGER', BB) ASSIGN(ERRORS, TOTAL);
    AM3 = AV1 AV2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AM3$(I,J) ~= AV1$(I,J) AV2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed outer product of VECTOR', BB) ASSIGN(ERRORS, TOTAL);
    AM3 = AM1 AM2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AM3$(I,J) ~= AM1$(I,J) AM2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed MATRIX multiplication', BB) ASSIGN(ERRORS, TOTAL);
    AV3 = AV1 AM2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AV3$(I,J) ~= AV1$(I,J) AM2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed VECTOR times MATRIX', BB) ASSIGN(ERRORS, TOTAL);
    AV3 = AM1 AV2;
    BB = TRUE;
    DO FOR I = 1 TO 2; DO FOR J = 1 TO 3; 
       IF AV3$(I,J) ~= AM1$(I,J) AV2$(I,J) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed MATRIX times VECTOR', BB) ASSIGN(ERRORS, TOTAL);
    
 CLOSE ARRAYED_ARITHMETIC;
 CALL ARRAYED_ARITHMETIC ASSIGN(ERRORS, TOTAL);

 /*--------------------------------------------------------------------------*/
 NF_TEST: PROCEDURE ASSIGN(ERRORS, TOTAL);
    DECLARE ERRORS INTEGER, TOTAL INTEGER;
    DECLARE 	E FUNCTION INTEGER, F FUNCTION SCALAR, 
    		F1 FUNCTION SCALAR, F2 FUNCTION SCALAR, F3 FUNCTION SCALAR,
    		G FUNCTION VECTOR, H FUNCTION MATRIX, 
    		C FUNCTION CHARACTER(10), B FUNCTION BIT(5),
    		I INTEGER, S SCALAR, V VECTOR, M MATRIX,
    		A CHARACTER(10), D BIT(5);
    DECLARE 	MM MATRIX INITIAL(1, 2, 3, 4, 5, 6, 7, 8, 9),
    		VV VECTOR INITIAL(4, 5, 6),
    		SS SCALAR INITIAL(77),
    		II INTEGER INITIAL(89),
    		AA CHARACTER(10) INITIAL('HELLO'),
    		DD BIT(5) INITIAL(26),
    		BB BOOLEAN;
 
    WRITE(6);
    WRITE(6) 'No-argument FUNCTION tests.';
    IF II = E THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('No-argument INTEGER function test', BB) ASSIGN(ERRORS, TOTAL);
    IF SS = F THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('No-argument SCALAR function test 1', BB) ASSIGN(ERRORS, TOTAL);
    IF SS = F1 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('No-argument SCALAR function test 2', BB) ASSIGN(ERRORS, TOTAL);
    IF SS = F2 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('No-argument SCALAR function test 3', BB) ASSIGN(ERRORS, TOTAL);
    IF SS = F3 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('No-argument SCALAR function test 4', BB) ASSIGN(ERRORS, TOTAL);
    IF VV = G THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('No-argument VECTOR function test', BB) ASSIGN(ERRORS, TOTAL);
    IF MM = H THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('No-argument MATRIX function test', BB) ASSIGN(ERRORS, TOTAL);
    /*
    IF AA = C THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('No-argument CHARACTER function test', BB) ASSIGN(ERRORS, TOTAL);
    IF DD = B THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('No-argument BIT function test', BB) ASSIGN(ERRORS, TOTAL);
    */

    E: FUNCTION INTEGER; RETURN ROUND(F+12); CLOSE E;
    F: FUNCTION SCALAR; RETURN G . G; CLOSE F;
    F1: FUNCTION SCALAR; RETURN G .G; CLOSE F1;
    F2: FUNCTION SCALAR; RETURN G. G; CLOSE F2;
    F3: FUNCTION SCALAR; RETURN G.G; CLOSE F3;
    G: FUNCTION VECTOR; DECLARE MM MATRIX; MM = H; RETURN MM$(2,*); CLOSE G;
    H: FUNCTION MATRIX; 
       DECLARE MM MATRIX CONSTANT(1, 2, 3, 4, 5, 6, 7, 8, 9);
       RETURN MM;
    CLOSE H;
    C: FUNCTION CHARACTER(10); RETURN 'HELLO'; CLOSE C;
    B: FUNCTION BIT(5); RETURN 26; CLOSE B;
 CLOSE NF_TEST;
 CALL NF_TEST ASSIGN(ERRORS, TOTAL);
 
 /*--------------------------------------------------------------------------*/
 ARRAY_FUNCTIONS: PROCEDURE ASSIGN(ERRORS, TOTAL);
    DECLARE ERRORS INTEGER, TOTAL INTEGER;
    DECLARE A ARRAY(6) INITIAL(1, 2, 3, 4, 5, 6);
    DECLARE BB;
    WRITE(6) ;
    WRITE(6) 'Array functions.';
    IF MIN(A) = 1 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('Array function MIN', BB) ASSIGN(ERRORS, TOTAL);
    IF MAX(A) = 6 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('Array function MAX', BB) ASSIGN(ERRORS, TOTAL);
    IF SUM(A) = 21 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('Array function SUM', BB) ASSIGN(ERRORS, TOTAL);
    IF PROD(A) = 720 THEN BB = TRUE; ELSE BB = FALSE;
    CALL PRINT_RESULT('Array function PROD', BB) ASSIGN(ERRORS, TOTAL);
 CLOSE ARRAY_FUNCTIONS;
 CALL ARRAY_FUNCTIONS ASSIGN(ERRORS, TOTAL);
 
 /*--------------------------------------------------------------------------*/
 ARRAYED_RTL: PROCEDURE ASSIGN(ERRORS, TOTAL);
    DECLARE ERRORS INTEGER, TOTAL INTEGER;
    DECLARE ARRAY(3,2), A, A1, A2, A3, B2, AB BOOLEAN;
    DECLARE BB BOOLEAN, I INTEGER, J INTEGER;
    DO FOR I = 1 TO 3; DO FOR J = 1 TO 2;
    	A1$(I,J) = 20 (RANDOM - 0.5);
    	DO UNTIL ROUND(A2$(I,J)) ~= 0; A2$(I,J) = 20 (RANDOM - 0.5); END;
    	B2$(I,J) = 100 (RANDOM + 1);
    	A3$(I,J) = 20 (RANDOM - 0.5);
    END; END;
    WRITE(6) ;
    WRITE(6) 'Arrayed RTL functions.';
    A = ABS(A1);
    BB = TRUE;
    DO FOR I = 1 TO 3; DO FOR J = 1 TO 2;
       IF A$(I,J) ~= ABS(A1$(I,J)) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed ABS', BB) ASSIGN(ERRORS, TOTAL);
    A = CEILING(A1);
    BB = TRUE;
    DO FOR I = 1 TO 3; DO FOR J = 1 TO 2;
       IF A$(I,J) ~= CEILING(A1$(I,J)) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed CEILING', BB) ASSIGN(ERRORS, TOTAL);
    A = DIV(A1, A2);
    BB = TRUE;
    DO FOR I = 1 TO 3; DO FOR J = 1 TO 2;
       IF A$(I,J) ~= DIV(A1$(I,J), A2$(I,J)) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed DIV', BB) ASSIGN(ERRORS, TOTAL);
    A = FLOOR(A1);
    BB = TRUE;
    DO FOR I = 1 TO 3; DO FOR J = 1 TO 2;
       IF A$(I,J) ~= FLOOR(A1$(I,J)) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed FLOOR', BB) ASSIGN(ERRORS, TOTAL);
    A = MIDVAL(A1, A2, A3);
    BB = TRUE;
    DO FOR I = 1 TO 3; DO FOR J = 1 TO 2;
       IF A$(I,J) ~= MIDVAL(A1$(I,J), A2$(I,J), A3$(I,J)) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed MIDVAL', BB) ASSIGN(ERRORS, TOTAL);
    A = MOD(A1, A2);
    BB = TRUE;
    DO FOR I = 1 TO 3; DO FOR J = 1 TO 2;
       IF A$(I,J) ~= MOD(A1$(I,J), A2$(I,J)) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed MOD', BB) ASSIGN(ERRORS, TOTAL);
    AB = ODD(A1);
    BB = TRUE;
    DO FOR I = 1 TO 3; DO FOR J = 1 TO 2;
       IF AB$(I,J) ~= ODD(A1$(I,J)) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed ODD', BB) ASSIGN(ERRORS, TOTAL);
    A = REMAINDER(A1, A2);
    BB = TRUE;
    DO FOR I = 1 TO 3; DO FOR J = 1 TO 2;
       IF A$(I,J) ~= REMAINDER(A1$(I,J), A2$(I,J)) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed REMAINDER', BB) ASSIGN(ERRORS, TOTAL);
    A = ROUND(A1);
    BB = TRUE;
    DO FOR I = 1 TO 3; DO FOR J = 1 TO 2;
       IF A$(I,J) ~= ROUND(A1$(I,J)) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed ROUND', BB) ASSIGN(ERRORS, TOTAL);
    A = SIGN(A1);
    BB = TRUE;
    DO FOR I = 1 TO 3; DO FOR J = 1 TO 2;
       IF A$(I,J) ~= SIGN(A1$(I,J)) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed SIGN', BB) ASSIGN(ERRORS, TOTAL);
    A = SIGNUM(A1);
    BB = TRUE;
    DO FOR I = 1 TO 3; DO FOR J = 1 TO 2;
       IF A$(I,J) ~= SIGNUM(A1$(I,J)) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed SIGNUM', BB) ASSIGN(ERRORS, TOTAL);
    A = TRUNCATE(A1);
    BB = TRUE;
    DO FOR I = 1 TO 3; DO FOR J = 1 TO 2;
       IF A$(I,J) ~= TRUNCATE(A1$(I,J)) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed TRUNCATE', BB) ASSIGN(ERRORS, TOTAL);
    A = ARCCOS(A1/10);
    BB = TRUE;
    DO FOR I = 1 TO 3; DO FOR J = 1 TO 2;
       IF A$(I,J) ~= ARCCOS(A1$(I,J)/10) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed ARCCOS', BB) ASSIGN(ERRORS, TOTAL);
    A = ARCCOSH(B2);
    BB = TRUE;
    DO FOR I = 1 TO 3; DO FOR J = 1 TO 2;
       IF A$(I,J) ~= ARCCOSH(B2$(I,J)) THEN BB = FALSE;
    END; END;
    CALL PRINT_RESULT('Arrayed ARCCOSH', BB) ASSIGN(ERRORS, TOTAL);
    WRITE(6) 'More RTL functions could be tested for arrayness, but since';
    WRITE(6) 'they''re all implemented using the same framework, and since';
    WRITE(6) 'we''ve already tested single-argument, double-argument, and';
    WRITE(6) 'triple-argument functions, it seems kind of pointless to';
    WRITE(6) 'continue doing so.  More of these tests can be implemented';
    WRITE(6) 'later if the need arises.';
    
 CLOSE ARRAYED_RTL;
 CALL ARRAYED_RTL ASSIGN(ERRORS, TOTAL);
 
 /*--------------------------------------------------------------------------*/
 WRITE(6);
 WRITE(6) 'Dot-products A.B vs structure references A.B.';
  /* Explanation:  In the final WRITE statement, we should see 14, which is the
    dot product of vector (1,2,3) with itself.  But if the preprocessor doesn't
    correctly mangle the leading G (in G . G), we'll instead see an emulation
    error. */
 DOTPRODUCT_VS_STRUCTURE: FUNCTION; 
 DECLARE G FUNCTION VECTOR;
 F: FUNCTION SCALAR; RETURN G.G; CLOSE F;
 G: FUNCTION VECTOR; DECLARE V VECTOR INITIAL(1,2,3); RETURN V; CLOSE G;
 RETURN F;
 CLOSE DOTPRODUCT_VS_STRUCTURE; 
 IF DOTPRODUCT_VS_STRUCTURE = 14 THEN BB = TRUE; ELSE BB = FALSE;
 CALL PRINT_RESULT('Dot-product vs structure-reference', BB) ASSIGN (ERRORS, TOTAL);
 
 /*--------------------------------------------------------------------------*/	
 WRITE(6) ;
 IF ALLTESTS THEN DO;
    TEMPORARY STIME, ETIME; 
    WRITE(6) 'Not a pass/fail test. Informational execution-speed timing measurement.'; 
    STIME = RUNTIME; 
    M = 100000; 
    I = 0; DO WHILE I < M; I = I + 1; END; 
    ETIME = RUNTIME; 
    WRITE(6) M, 'iterations,', ETIME-STIME, 'seconds, ', 
 	  ROUND(M/(ETIME-STIME)), 'iterations/second.'; 
 END;
 ELSE WRITE(6) 'Skipping informational test(s).';
 	
 /*--------------------------------------------------------------------------*/	
 WRITE(6); 
 WRITE(6) 'Total errors:', ERRORS;
 WRITE(6) 'Total tests:', TOTAL;
 
